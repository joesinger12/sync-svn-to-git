// SpaceFunctionDefaults
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//

// No direct correlation of ACM topics - just miscellaneous rules pertaining to
// InteriorLightingSystem objects
//
// External References:
//     NACMSpaceTypeData table defined in file 'App5-4A_SpaceUseData.csv'
//
// The main objects in the SDD are:
//     SpcFuncDefaults / SpaceFunctionDefaults
//

// -------------- Function Type ------------------------------------------------
RULE SpcFuncDefaults:SpcFunc
  DESCRIPTION
    "The area category occupancy type from Nonresidential Appendix 5.4A"
  INPUTCLASS
    Compulsory
// OPTION list defined in BEMEnums.txt to facilitate Building:FuncClassMthd
//        & CompleteBldgFunc dependencies
// DEFAULT defined in BEMEnums.txt
ENDRULE

RULE NEW SpcFuncDefaults:HasNoInternalLds
  DATATYPE
    Integer
  LONGFORM
    HasNoInternalLoads
  DESCRIPTION
    "A flag that indicates whether the space has no internal loads/schedules for
     reference in other rules. 1 indicates there should not be internal loads, 
     0 indicates the space can have internal loads."
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalStatus(SpcFunc) = 0 )
    then 0
    else 
    if( SpcFunc = "Unoccupied-Include in Gross Floor Area" .OR.
        SpcFunc = "Unoccupied-Exclude from Gross Floor Area" .OR.
        SpcFunc = "- specify -" )
    then 1
    else 0
    endif endif
ENDRULE

RULE SpcFuncDefaults:VentSpcFunc
  DESCRIPTION
    "The ventilation occupancy category for the space."
  HELP
    "The list of available ventialtion categories available is depends on the 
     SpaceFunction (SpcFunc) selected. If you do not see the category that is applicable,
     try selecting another related SpcFunc."
  RESETS
    ResetThisWhenTheFollowingIsModified
      SpcFunc
  INPUTCLASS : T24N_2016
    NotInput
  INPUTCLASS
    Default
//OPTION
// Defined in BEMEnums.txt
  DEFAULT : T24N_2016
    UNDEFINED  
  DEFAULT
    if( SpcFunc = "_Invalid from 2016 - Corridors, Restrooms, Stairs, and Support Areas" .OR.
        SpcFunc = "_Invalid from 2016 - Police Station and Fire Station" .OR. 
        SpcFunc = "_Invalid from 2016 - Housing, Public and Common Areas: Multi-family, Dormitory" .OR.
        SpcFunc = "_Invalid from 2016 - Housing, Public and Common Areas: Senior Housing" .OR.
        SpcFunc = "- specify -" ) 
    then "NA"
    else SpaceFunctionData:VentSpcFuncDef("FuncType", SpcFunc)
    endif 
ENDRULE

RULE NEW SpcFuncDefaults:OccClass
  LONGFORM
    OccupancyClassification
  DESCRIPTION
    "The occupancy classification of the SpaceFunction assigned to the 
     SpaceFunctionDefault." 
  INPUTCLASS
    NotInput
  OPTION
    Nonresidential
    Residential
  DEFAULT : T24N
    if( SpcFunc = "High-Rise Residential Living Spaces" .OR.
        SpcFunc = "Hotel/Motel Guest Room" )
    then "Residential"
    else "Nonresidential"
    endif
  DEFAULT : S901G ECBC
    if( SpcFunc = "Courthouse/Police Station/Penitentiary - Confinement Cells" .OR.
        SpcFunc = "Dormitory - Living Quarters" .OR.
        SpcFunc = "Fire Stations - Sleeping Quarters" .OR.
        SpcFunc = "Hospital - Nursery" .OR.
        SpcFunc = "Hospital - Patient Room" .OR.
        SpcFunc = "Hotel/Highway Lodging - Hotel Guest Rooms" .OR.
        SpcFunc = "Hotel/Highway Lodging - Highway Lodging Guest Rooms" .OR.
        SpcFunc = "Multifamily - Dwelling/Sleeping Units" )
    then "Residential"
    else "Nonresidential"
    endif
ENDRULE

RULE NEW SpcFuncDefaults:IsRes
  DATATYPE
    Integer
  LONGFORM
    IsResidential
  DESCRIPTION
    "An integer that indicates if the SpaceFunction is residential." 
  INPUTCLASS
    NotInput
  DEFAULT
    if( OccClass = "Residential" )
    then 1
    else 0
    endif
ENDRULE

// -------------- Function Schdule Group ---------------------------------------
RULE SpcFuncDefaults:FuncSchGrp
  DESCRIPTION
     "A type specifying occupancy categories used to determine schedules."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:FuncSchGrp("FuncType", SpcFunc)
    endif
ENDRULE
RULE NEW SpcFuncDefaults:FuncSchGrpIsEditable
  DESCRIPTION
    "The schedule group for the space is editable"
  DATATYPE
    Integer
  LONGFORM
    FunctionScheduleGroupIsEditable
  INPUTCLASS
    NotInput
  DEFAULT
    SpaceFunctionData:FuncSchGrpIsEditable("FuncType", SpcFunc)
ENDRULE
// -----------------------------------------------------------------------------
// Hot water heating
RULE SpcFuncDefaults:HotWtrHtgRt
  RULESETS
    T24N_2016 S901G ECBC
  DESCRIPTION
     "The amount of energy required in providing hot water for each person in a
     space during occupied hours."
  INPUTCLASS
    NotInput  IgnoreUserInput  "HotWtrHtgRt removed prior to release of 2013-3e and 2016.2"
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else
     if ( SpcFunc = "High-Rise Residential Living Spaces" )
     then 0
    else SpaceFunctionData:HotWtrHtgRt("FuncType",SpcFunc)
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
// Hot water heating
RULE SpcFuncDefaults:HotWtrHrlyUsePerPers
  DESCRIPTION
     "The amount of energy required in providing hot water for each person in a
     space during occupied hours."
  INPUTCLASS
    NotInput  IgnoreUserInput  "HotWtrHrlyUsePerPers removed as user input prior to release of 2013-3e and 2016.2"
  DEFAULT : T24N_2016
    UNDEFINED
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else
     if ( SpcFunc = "High-Rise Residential Living Spaces" )
     then 0
    else SpaceFunctionData:HotWtrHrlyUsePerPers("FuncType",SpcFunc)
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:HotWtrHtgSchRef
  DESCRIPTION
    "A fractional schedule reflecting the time pattern of water heating use.
     This input modifies the hot water heating rate."
  INPUTCLASS
    Prescribed
ENDRULE

// -------------- Internal Loads --------------------
// Occupancy
RULE SpcFuncDefaults:OccDens
  DESCRIPTION
     "The area density of people associated with a space function
      expressed in people per 1000 square feet."
  INPUTCLASS  
    NotInput  IgnoreUserInput  "SpcFuncDefaults:OccDens no longer an allowed input as of version 2016.1.0 / 2013 v3d"
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:OccDens("FuncType", SpcFunc)
    endif
ENDRULE

//   Occupant Fraction
RULE SpcFuncDefaults:OccFrac
  RULESETS : T24N
  DESCRIPTION
    "The expected fraction of the exiting density of people in a Space, based 
     on SpaceFunction, which will determine the design occupancy."  
  INPUTCLASS
    Default
  MINIMUM
    0.5
  MAXIMUM
    5.0
  UNITS 
    people/1000ft2
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else Proj:OccFrac
    endif
ENDRULE

RULE SpcFuncDefaults:OccDensSim
  DESCRIPTION
    "The density of people associated with the space function, including
     the occupancy fraction."
  INPUTCLASS  
    NotInput
  DEFAULT : T24N_2016
    UNDEFINED
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else ValidOr( OccDens, 0 ) * Max( ValidOr( OccFrac, 0 ), 0.5 )
    endif
ENDRULE

RULE NEW SpcFuncDefaults:VentOccDensSim
  DATATYPE
    Float
  LONGFORM
    VentilationOccupancyDensitySimulated
  DESCRIPTION
     "The density of people associated with the ventilation space function."
  INPUTCLASS  
    NotInput
  DEFAULT : T24N_2016
    UNDEFINED
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else VentilationSpaceFunctionData:VentOccDensSim("VentFuncType", VentSpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:OccSensHtRt
  DESCRIPTION
    "The rate of sensible heat released per person which is a function of
    activity."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:OccSensHtRt("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:OccLatHtRt
  DESCRIPTION
     "The rate of latent heat released per person which is a function of
     activity."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:OccLatHtRt("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:OccSchRef
  DESCRIPTION
    "Reference to a schedule that describes the fraction of occupancy on an 
     hourly basis."
  INPUTCLASS 
    Prescribed
ENDRULE  

// -----------------------------------------------------------------------------
// Receptacle Power
RULE SpcFuncDefaults:RecptPwrDens
  DESCRIPTION
     "The usage of electrical devices plugged into receptacles in a space based
     on the occupancy type."
  DEFAULT : T24N
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else if( SpcFunc = "Computer Room" )
    then 20.001 // greater than 20 W/ft2 is Standard's definition for 'Computer Room'
    else SpaceFunctionData:RecptPwrDens("FuncType", SpcFunc)
    endif endif
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:RecptPwrDens("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Receptacle Schedule
RULE SpcFuncDefaults:RecptSchRef
  DESCRIPTION
    "The use of electrical devices represented by a 24 hour schedule 
     (fraction of density) associated with the occupancy type selected 
     from the  Area Category Method or Complete Building Method in ACM
     Appendix 5.4B." 
  INPUTCLASS 
    Prescribed
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ElecEqpPwrDens
  DESCRIPTION
     "Commercial electric power density is the average power density for all
     commercial electric equipment, assuming constant year-round operation."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else if( Proj:GasType != "None" )
    then 0
    else SpaceFunctionData:GasEqpPwrDens("FuncType", SpcFunc) / 3.412
    endif endif
  CHECKCODE
    if ( SpcFunc = "Scientific Laboratory Area" .OR. SpcFunc = "Laboratory, Scientific")
    then UNCHANGED
	  else if( Proj:GasType == "None" .AND. IfValidAnd( ElecEqpPwrDens < SpaceFunctionData:GasEqpPwrDens("FuncType", SpcFunc) / 3.412 ) )
	  then PostError("Space function defaults '%s' specifies process electric equipment 
	                 power lower than the prescribed value.  Make the process electric
	                 equipment power equal or greater than the default value.", Name) 
	  else UNCHANGED
	  endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ElecEqpSchRef
  DESCRIPTION
    "The use of electric equipment represented by a a 24 hour schedule
    (fraction of density) associated with the occupancy type selected from the
     Area Category Method or Complete Building Method in ACM Appendix 5.4B." 
  INPUTCLASS 
    Prescribed
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:GasEqpPwrDens
  DESCRIPTION
     "Commercial gas power density is the average power density for all
     commercial gas equipment, assuming constant year-round operation."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else if( Proj:GasType = "None" )
    then 0
    else SpaceFunctionData:GasEqpPwrDens("FuncType", SpcFunc)
    endif endif
  CHECKCODE
    if ( SpcFunc = "Scientific Laboratory Area" .OR. SpcFunc = "Laboratory, Scientific") ; JP 211130 space type name change (tic #3301)
    then UNCHANGED
    else if( Proj:GasType = "None" .AND. IfValidAnd( GasEqpPwrDens > 0 ) )
	  then PostError("Space function defaults '%s' specifies process gas equipment 
	                 but the project gas type is specified as none.  Make the 
	                 process gas equipment load zero or change the project gas 
	                 type.", Name)
	  else if( Proj:GasType != "None" .AND. IfValidAnd( GasEqpPwrDens < SpaceFunctionData:GasEqpPwrDens("FuncType", SpcFunc) ) )
	  then PostError("Space function defaults '%s' specifies process gas equipment 
	                 power lower than the prescribed value.  Make the process gas
	                 equipment power equal or greater than the default value.", Name) 
	  else UNCHANGED
	  endif endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:GasEqpSchRef
  DESCRIPTION
    "The use of gas equipment represented by a a 24 hour schedule
    (fraction of density) associated with the occupancy type selected from the
     Area Category Method or Complete Building Method in ACM Appendix 5.4B." 
  INPUTCLASS 
    Prescribed
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:CommRfrgEPD
  DESCRIPTION
     "Total power density for refrigeration equipment including walk-in
     refrigerators and freezers and refrigerated casework."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:CommRfrgEPD("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:CommRfrgEqpSchRef
  DESCRIPTION
    "Reference to a schedule that describes the fraction of refrigeration
     equipment usage on an hourly basis"
  INPUTCLASS
    Prescribed
ENDRULE

// -----------------------------------------------------------------------------
;RULE SpcFuncDefaults:CommRfrgEqpCOP
;  DESCRIPTION
;    "The coefficient of performance of the refrigeration system. This is used
;     only to determine the heat removed or added to the space, not to determine
;     the refrigeration power or energy. "
;  HELP
;    "This value is prescribed to be 3.6 for refrigerators and 1.8 for freezers.
;     These values are consistent with the assumptions for the default values
;     for walk-ins, which assume an EER of 12.39 for refrigerators and 6.33 for
;     freezers"
;//  INPUTCLASS
;//    Prescribed
;  DEFAULT
;    if( LocalStatus(SpcFunc) < 2 .OR. CommRfrgEPD < 0.001 ) then
;      UNDEFINED
;    else 3.6
;    endif
;ENDRULE

// -----------------------------------------------------------------------------
// Process electrical
RULE SpcFuncDefaults:ProcElecPwrDens
  DESCRIPTION
   "Process electrical power density resulting from an activity or treatment
    not related to the space conditioning, lighting, service water heating, or
    ventilating of a building as it relates to human occupancy.  Process load
    may include sensible and/or latent components. For data centers this
    includes transformers, UPS, PDU, server fans, power supplies, etc."
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
; RULE SpcFuncDefaults:ProcElecSensHtRt
;   DESCRIPTION
;    "The internal energy of a building resulting from an activity or
;     treatment not related to the space conditioning, lighting, service water
;     heating, or ventilating of a building as it relates to human occupancy."
;   DEFAULT
;     1.0
; ENDRULE
;
; RULE SpcFuncDefaults:ProcElecLatHtRt
;   DESCRIPTION
;    "The latent internal energy of a building resulting from an activity or
;     treatment not related to the space conditioning, lighting, service water
;	   heating, or ventilating of a building as it relates to human occupancy."
;   DEFAULT
;     0
; ENDRULE

// -----------------------------------------------------------------------------
// Interior lighting
RULE SpcFuncDefaults:IntLPDReg
  DESCRIPTION
    "Total regulated connected lighting power density for all interior lighting
     systems in a Space. This includes the loads for lamps and ballasts."
  DEFAULT : S901G ECBC
    if( HasNoInternalLds = 1 )
    then //selected space function indicates that space should have no internal loads
      UNDEFINED
    else if( SpcFunc = "Multifamily - Dwelling/Sleeping Units" )
    then // no default for Multifamily - Dwelling/Sleeping Units,
      0.6  // default for Complete Building Method - Multifamily
    else  // default based on lookup for space function.
      SpaceFunctionData:IntLPDReg("FuncType", SpcFunc)
    endif endif
  DEFAULT
    if( HasNoInternalLds = 1 )
    then //selected space function indicates that space should have no internal loads
      UNDEFINED
    else  // default based on lookup for space function.
      SpaceFunctionData:IntLPDReg("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgRegSchRef
  DESCRIPTION
    "Reference to a unique Schedule Name of interior type for association with
     an interior lighting system." // ACM or SDD Definition
  INPUTCLASS 
    Prescribed
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLPDNonReg
  DESCRIPTION
   "Total non-regulated connected lighting power density for all interior
    lighting systems in a Space. This includes the loads for lamps and
    ballasts."
  DEFAULT
    if( HasNoInternalLds = 1 ) 
    then //selected space function indicates that space should have no internal loads
      UNDEFINED
    else // default for non regulated LPD is zero
      UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgNonRegSchRef
  DESCRIPTION
    "Reference to a unique Schedule Name of interior type for association with
     an interior lighting system." // ACM or SDD Definition
  INPUTCLASS 
    Default
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgRegHtGnSpcFrac
  DESCRIPTION
    "Fraction of regulated interior lighting heat gain going to space air."
  DEFAULT
    if( IfValidAnd( IntLPDReg > 0 ) ) 
    then // standard design values for Recessed/Downlight, Linear Fluorescent
      0.69
		else	
      UNDEFINED
    endif
  CHECKCODE
    if( LocalStatus( IntLtgRegHtGnSpcFrac ) > 4 )
    then  PostWarning( "Fraction of regulated light heat to space 
                        in space function default %s
                        has been edited which is not allowed.
                        The user input value will be ignored", Name )
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgRegHtGnRadFrac
  DESCRIPTION
   "The radiant portion of the Heat Gain Space Fraction."
  DEFAULT
    if( IfValidAnd( IntLPDReg > 0 ) ) 
    then  // standard design values for Recessed/Downlight, Linear Fluorescent
      0.58
		else	
      UNDEFINED
    endif
  CHECKCODE
    if( LocalStatus( IntLtgRegHtGnRadFrac ) > 4 )
    then  PostWarning( "Radiant fraction of regulated light in 
                        space function default %s
                        has been edited which is not allowed.
                        The user input value will be ignored", Name )
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgNonRegHtGnSpcFrac
  DESCRIPTION
   "Fraction of non-regulated interior lighting heat gain going to space air."
  DEFAULT
    if( IfValidAnd( IntLPDNonReg > 0 ) ) 
    then  // standard design values for Recessed/Downlight, Linear Fluorescent
      0.69
		else	
      UNDEFINED
    endif
  CHECKCODE
    if( LocalStatus( IntLtgNonRegHtGnSpcFrac ) > 4 )
    then  PostWarning( "Fraction of non-regulated light heat to space 
                        space function default %s
                        has been edited which is not allowed.
                        The user input value will be ignored", Name )
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:IntLtgNonRegHtGnRadFrac
  DESCRIPTION
   "The radiant portion of the Heat Gain Space Fraction."
  DEFAULT
    if( IfValidAnd( IntLPDNonReg > 0 ) ) 
    then // standard design values for Recessed/Downlight, Linear Fluorescent
      0.58
		else	
      UNDEFINED
    endif
  CHECKCODE
    if( LocalStatus( IntLtgNonRegHtGnRadFrac ) > 4 )
    then  PostWarning( "Radiant fraction of non-regulated light in 
                        space function default %s
                        has been edited which is not allowed.
                        The user input value will be ignored", Name )
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Process loads
RULE SpcFuncDefaults:ProcElecPwrDens
  DESCRIPTION
    "Process load is the electric energy consumption in the conditioned space
     of a building resulting from an activity or treatment not related to the
     space conditioning, lighting, service water heating, or ventilating of a
     building as it relates to human occupancy.  Process load may include
     convective (sensible) and/or latent components "
  INPUTCLASS
    Optional
  MINIMUM
    0
//  COMMONMINIMUM
//    0.2
  COMMONMAXIMUM
    10
  MAXIMUM
    100
  UNITS
    W/ft2
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcElecSchRef
  DESCRIPTION
    "The use of process electric represented by a a 24 hour schedule
    (fraction of density) associated with the occupancy type selected from the
     Area Category Method or Complete Building Method in ACM Appendix 5.4B."
  INPUTCLASS 
    Optional
ENDRULE  

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcElecRadFrac
  DESCRIPTION
    "The fraction of radiant heat gain to a space based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcElecLatFrac
  DESCRIPTION
    "The fraction of latent heat gain to a space based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcElecLostFrac
  DESCRIPTION
    "The fraction of heat lost to the exterior is based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcGasPwrDens
  DESCRIPTION
    "Process load is the gas energy consumption in the conditioned space of a
     building resulting from an activity or treatment not related to the space
     conditioning, lighting, service water heating, or ventilating of a
     building as it relates to human occupancy.  Process load may include
     convective (sensible) and/or latent components "
  INPUTCLASS
    Optional
  MINIMUM
    0
//  COMMONMINIMUM
//    0.2
  COMMONMAXIMUM
    20
  MAXIMUM
    100
  UNITS
    Btu/h-ft2
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
  CHECKCODE
    if( Proj:GasType = "None" .AND. IfValidAnd( ProcGasPwrDens > 0 ) )
    then
      PostError("Space function defaults '%s' specifies a process gas load 
                 but the project gas type is specified as none.  Make the 
                 process gas load zero or change the project gas 
                 type.", Name)
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcGasSchRef
  DESCRIPTION
    "The use of process gas equipment represented by a a 24 hour schedule
    (fraction of density) associated with the occupancy type selected from the
     Area Category Method or Complete Building Method in ACM Appendix 5.4B."
  INPUTCLASS 
    Optional
ENDRULE 

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcGasRadFrac
  DESCRIPTION
    "The fraction of radiant heat gain to a space based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcGasLatFrac
  DESCRIPTION
    "The fraction of latent heat gain to a space based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:ProcGasLostFrac
  DESCRIPTION
    "The fraction of heat lost to the exterior is based on appliance energy use."
  HELP
    "Fraction convective (sensible) is typically equal to 1.0 minus fraction
     radiant, minus fraction latent, minus fraction lost."
  INPUTCLASS
    Optional
  DEFAULT
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else 0.0
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Infiltration
RULE SpcFuncDefaults:InfMthd
;  OPTION     Specified in BEMEnums
;     FlowSpace
;     FlowArea
;     FlowExteriorArea
;     FlowExteriorWallArea
;     AirChangesPerHour
  DEFAULT
     FlowExteriorWallArea
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:DsgnInfRt
  UNITS
    cfm/ft2
  DEFAULT : S901G ECBC
    ( 0.112 * 0.4 * (Proj:TotExtSurfArea / Proj:AboveGrdExtWallArea) )
  DEFAULT
    0.0448
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:InfModelCoefA
// unitless
  DEFAULT
    0
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:InfModelCoefB
  UNITS
    1/°F
  DEFAULT
    0
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:InfModelCoefC
  UNITS
    hr/mile
  DEFAULT
    0.10016
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:InfModelCoefD
  UNITS
    hr2/mile2
  DEFAULT
    0
ENDRULE


// -----------------------------------------------------------------------------
// Ventilation rates
RULE SpcFuncDefaults:VentPerArea
  DESCRIPTION
     "The design outside air flow rate divided by the floor area of the space."
  INPUTCLASS
    Default   
  DEFAULT : T24N_2016
    if( HasNoInternalLds = 1 )
    then 0
    else if( SpcFunc = "Hotel/Motel Guest Room" )
    then 0.15
    else SpaceFunctionData:CodeVentPerArea("FuncType", SpcFunc)
    endif endif		
  DEFAULT : T24N_2019
    if( HasNoInternalLds = 1 )
    then 0
    else if( SpcFunc = "Hotel/Motel Guest Room" )
    then 0.15
    else if( SpcFunc = "High-Rise Residential Living Spaces" )
    then 0.03
    else if( SpcFunc = "Scientific Laboratory Area" )
    then ValidOr( ExhPerArea, 0 )
    else VentilationSpaceFunctionData:CodeVentPerArea("VentFuncType", VentSpcFunc) 
    endif endif	endif endif
  DEFAULT : T24N
    if( HasNoInternalLds = 1 )
    then 0
    else if( SpcFunc = "Hotel/Motel Guest Room" )
    then 0.15
    else if( SpcFunc = "High-Rise Residential Living Spaces" )
    then 0.03
    else if( SpcFunc = "Laboratory, Scientific" ) ; JP 211130 space type name change (tic #3301)
    then ValidOr( ExhPerArea, 0 )
    else VentilationSpaceFunctionData:CodeVentPerArea("VentFuncType", VentSpcFunc) 
    endif endif	endif endif
  DEFAULT : S901G ECBC
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:S6212010_CodeVentPerArea("FuncType", SpcFunc)
    endif
ENDRULE

// Ventilation rates
RULE NEW SpcFuncDefaults:CodeVentPerAreaForDCV
  DATATYPE
    Float
  LONGFORM
    CodeVentilationPerAreaForDCV
  DESCRIPTION
     "The minimum outside air flow rate divided by the floor area of the space,
      when DCV controls are used.."
  INPUTCLASS
    NotInput   
  DEFAULT : T24N_2016
    UNDEFINED	
  DEFAULT : T24N
    if( HasNoInternalLds = 1 )
    then 0
    else VentilationSpaceFunctionData:CodeVentPerAreaForDCV("VentFuncType", VentSpcFunc) 
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:VentPerPerson
  DESCRIPTION
     "The design outside air flow rate divided by the design occupancy of the space."
  INPUTCLASS
    Default
  DEFAULT : T24N_2016
    if( HasNoInternalLds = 1 )
    then 0
    else
    if( SpcFunc = "Laboratory, Scientific" .OR.
        SpcFunc = "Laboratory, Equipment Room" .OR.
        SpcFunc = "Parking Garage Building, Parking Area" .OR.
        SpcFunc = "Parking Garage Area Dedicated Ramps" .OR. 
        SpcFunc = "Parking Garage Area Daylight Adaptation Zones" .OR.
        SpcFunc = "Hotel/Motel Guest Room" )
    then 0
    else
    if( SpcFunc = "High-Rise Residential Living Spaces" )
    then 5
    else 15
    endif endif endif
  DEFAULT : T24N_2019
    if( HasNoInternalLds = 1 )
    then 0
    else
    if( SpcFunc = "Scientific Laboratory Area" .OR.
        SpcFunc = "Parking Garage Area (Parking Zone)" .OR.
        SpcFunc = "Parking Garage Area (Dedicated Ramps)" .OR. 
        SpcFunc = "Parking Garage Area (Daylight Adaptation Zones)" .OR.
        SpcFunc = "Hotel/Motel Guest Room" )
    then 0
    else
    if( SpcFunc = "High-Rise Residential Living Spaces" )
    then 7.5
    else
    if( IfValidAnd( OccDensSim > 0 ) .AND. 
        IfValidAnd( VentOccDensSim > 0 ) )
    then Min( ( VentOccDensSim / OccDensSim ) , 1 ) * 15
    else
    if( IfValidAnd( CodeVentPerAreaForDCV > 0 ) )
    then 15 // Space could have DCV
    else 0
    endif endif endif endif endif
  DEFAULT : T24N
    if( HasNoInternalLds = 1 )
    then 0
    else
    if( SpcFunc = "Laboratory, Scientific" .OR. ; JP 211130 space type name change (tic #3301)
        SpcFunc = "Parking Garage Area (Parking Zone and Ramps)" .OR.
        SpcFunc = "Parking Garage Area (Daylight Adaptation Zones)" .OR.
        SpcFunc = "Hotel/Motel Guest Room" )
    then 0
    else
    if( SpcFunc = "High-Rise Residential Living Spaces" )
    then 7.5
    else
    if( IfValidAnd( CodeVentPerAreaForDCV > 0 ) )
    then 15 // Space could have DCV
    else 0
    endif endif endif endif
  DEFAULT : S901G ECBC
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else SpaceFunctionData:S6212010_CodeVentPerPerson("FuncType", SpcFunc)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:VentACH
  DESCRIPTION
    "The design outside air changes per hour."
  INPUTCLASS
    Default
  DEFAULT : T24N_2016
    if( HasNoInternalLds = 1 )
    then UNDEFINED
    else
    if( SpcFunc = "Laboratory, Scientific" .OR.
        SpcFunc = "Laboratory, Equipment Room" )
    then 6
    else 0
    endif endif
  DEFAULT : T24N_2019
    if( SpcFunc = "Scientific Laboratory Area" )
    then ValidOr( ExhACH, 0 )
    else 0
    endif
  DEFAULT : T24N
    if( SpcFunc = "Laboratory, Scientific" ) ; JP 211130 space type name change (tic #3301)
    then ValidOr( ExhACH, 0 )
    else 0
    endif
  DEFAULT : S901G ECBC
    0
ENDRULE

// -----------------------------------------------------------------------------
RULE SpcFuncDefaults:VentPerSpc
  DESCRIPTION
    "The design outside air changes per hour."
  INPUTCLASS
    Optional
  DEFAULT : T24N_2016
    if( SpcFunc = "Hotel/Motel Guest Room" )
    then 30
    else 0
    endif
  DEFAULT : T24N
   if( SpcFunc = "Hotel/Motel Guest Room" )
    then 30
    else 0
    endif
  DEFAULT : S901G ECBC
    0
ENDRULE


// -----------------------------------------------------------------------------
// Exhaust rates
RULE SpcFuncDefaults:ExhPerArea
  DESCRIPTION
     "The design exhaust flow rate divided by the floor area of the space."
  INPUTCLASS
    Default
  UNITS
    cfm/ft2
  DEFAULT : T24N_2016
    if( SpcFunc = "Parking Garage Building, Parking Area" .OR.
        SpcFunc = "Parking Garage Area Dedicated Ramps" .OR. 
        SpcFunc = "Parking Garage Area Daylight Adaptation Zones" )
    then VentPerArea
    else 0
    endif
  DEFAULT : T24N
    if( IfValidAnd( VentSpcFunc != "NA" ) )
    then VentilationSpaceFunctionData:CodeExhPerArea("VentFuncType", VentSpcFunc) 
    else 0
    endif
  DEFAULT : S901G ECBC
    0
ENDRULE

RULE SpcFuncDefaults:ExhACH
  DESCRIPTION
    "The design exhaust air changes per hour for the space."
  INPUTCLASS
    Default
  UNITS
    ACH
  DEFAULT : T24N_2016
    if( SpcFunc = "Laboratory, Scientific" .OR.
        SpcFunc = "Laboratory, Equipment Room" )
    then VentACH
    else 0
    endif
  DEFAULT : T24N
    0
  DEFAULT : S901G ECBC
    0
ENDRULE

RULE SpcFuncDefaults:ExhContPerFixture
  DESCRIPTION
    "The design continuous exhaust air flow rate per fixture (toilet, showerhead, etc)."
  INPUTCLASS : T24N_2016
    NotInput
  INPUTCLASS
    Optional
  UNITS
    cfm
  DEFAULT : T24N_2016
    0
  DEFAULT : T24N
    // Used for toilet and shower rooms
    if( IfValidAnd( VentSpcFunc != "NA" ) )
    then VentilationSpaceFunctionData:CodeExhContPerFixture("VentFuncType", VentSpcFunc) 
    else 0
    endif
ENDRULE

RULE SpcFuncDefaults:ExhNumFixtures
  DESCRIPTION
    "The number of fixtures in the space for fixture-dependent exhaust
     (toilets, showerheads, etc)."
  INPUTCLASS : T24N_2016
    NotInput
  INPUTCLASS
    Optional
  DEFAULT
    0
ENDRULE

RULE SpcFuncDefaults:IsIntrmtExh
  DESCRIPTION
    "A flag that indicates the exhaust system is intermittent. 
     (toilet, showerhead, etc)."
  HELP
    "Intermittent exhaust can either not be modeled, or if this box is checked,
     the intermittent exhaust is excluded from the analysis."
  INPUTCLASS
    Prescribed
  DEFAULT
    0
ENDRULE

RULE SpcFuncDefaults:ExhPerSpc
  DESCRIPTION
    "The design exhaust air flow rate for the space."
  INPUTCLASS
    Optional
  UNITS
    cfm
  DEFAULT
    0
ENDRULE

// Not yet supported
;RULE SpcFuncDefaults:ExhIntrmtPerFixture
;  DESCRIPTION
;    "The design intermittent exhaust air flow rate per fixture 
;     (toilet, showerhead, etc)."
;  INPUTCLASS
;    Optional
;  UNITS
;    cfm
;  DEFAULT : T24N_2016
;    0
;  DEFAULT : T24N
;    // Used for toilet and shower rooms
;    if( IfValidAnd( VentSpcFunc != "NA" ) )
;    then VentilationSpaceFunctionData:CodeExhIntrmtPerFixture("VentFuncType", VentSpcFunc) 
;    else 0
;    endif
;ENDRULE


