// Building - General
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------



// ********** Building Stories *************************************************
// ------------ Above-grade stories ------------    
RULE Bldg:AboveGrdStoryCnt
  DESCRIPTION
    "The number of above-grade building stories."
  HELP
    "This property is one determining factor for the basline HVAC system type. 
     A “floor” is considered a “Habitable Story”, defined in the Standards as a 
     story that is at least 50 percent above grade."
  RESETS
    ResetThisWhenTheFollowingIsModified
      Bldg:WholeBldgModeled
  INPUTCLASS : T24N 
    Required 
  INPUTCLASS : S901G ECBC
    Optional
  MINIMUM 
    1
  REPORTPRECISION
    0
  DEFAULT
    SumChildrenIf( Story:Mult, Story:Z > -1 ) +
    SumChildrenIf( ResZnGrp:NumStories, ResZnGrp:Z > -1 )
ENDRULE

// ------------ Total number of stories ------------  
RULE Bldg:TotStoryCnt
  DESCRIPTION
    "The total number of stories in a building, New and Existing."
  HELP
    "Includes both above grade stories and any below grade stories."
  RESETS
    ResetThisWhenTheFollowingIsModified
      Bldg:WholeBldgModeled
  INPUTCLASS 
    Required
  MINIMUM 
    1
  REPORTPRECISION
    0
  DEFAULT : S901G ECBC
    NonResTotStoryCnt + ResTotStoryCnt
  DEFAULT : T24N
    SumChildren( Story:Mult ) +
    SumChildren( ResZnGrp:NumStories )
// ISSUE: Add QC check to compare to number of Bldg:Story objects?
ENDRULE


// ------------ Low/HighRiseMFam flags ------------  SAC 02/15/22
RULE Proj:IsLowRiseMFam
  DESCRIPTION
    "Whether this project is classified as LowRise multifamily."
  INPUTCLASS 
    NotInput
  DEFAULT
    if (IfValidAnd( ResDwellUnits > 0 ) .AND. IfValidAnd( Bldg:AboveGrdStoryCnt < 4 ))
    then  1
    else  0  endif
ENDRULE

RULE Proj:IsHighRiseMFam
  DESCRIPTION
    "Whether this project is classified as HighRise multifamily."
  INPUTCLASS 
    NotInput
  DEFAULT
    if (IfValidAnd( ResDwellUnits > 0 ) .AND. IfValidAnd( Bldg:AboveGrdStoryCnt >= 4 ))
    then  1
    else  0  endif
ENDRULE



// ********** Floor Areas and Volumes ******************************************
// ------------ Calculate Bldg total floor area ------------
RULE NEW Story:TotFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    TotalFloorAreaWithMultiplier
  DESCRIPTION
    "The total floor area (conditioned and unconditioned) of each story, 
     including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  REPORTPRECISION
    0
  DEFAULT 
    SumChildren( Spc:FlrAreaWithMult )
ENDRULE
; SAC 09/16/21 (MFam)  ; updated to include Mults - SAC 05/25/23 (tic #3392)
RULE NEW ResZnGrp:TotFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    TotalFloorAreaWithMultiplier
  DESCRIPTION
    "The total floor area (conditioned and unconditioned) of each 
     ResZnGrp, including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  REPORTPRECISION
    0
  DEFAULT 
    SumChildren( ResZn:FlrAreaWithMult ) +
    SumChildren( ResOtherZn:FlrAreaWithMult )
ENDRULE

RULE Bldg:TotFlrArea
  DESCRIPTION
    "The total floor area (conditioned and unconditioned) of the building,
     including multipliers."
  HELP
    "The total building floor area, including new and existing areas. 
     Default includes entire area of spaces modeled explicitly. If
     whole building is not modeled, then the total floor area 
     should be entered here. " 
  RESETS
    ResetThisWhenTheFollowingIsModified
      Bldg:WholeBldgModeled
  INPUTCLASS 
    Default
  MINIMUM 
    1
  UNITS 
    ft2 
  REPORTPRECISION
    0
  DEFAULT
    SumChildren( Story:TotFlrAreaWithMult ) +
    SumChildren( ResZnGrp:TotFlrAreaWithMult )
  CHECKCODE
    if( WholeBldgModeled = 1 .AND. LocalStatus( TotFlrArea ) > 4 )
    then
      PostError("Building total floor area should not be user defined if the
                 whole building is modeled. Uncheck this option and restore the
                 default to perform compliance.")
    else UNCHANGED
    endif
ENDRULE

// ------------ Calculate Bldg total conditioned volume ------------
RULE NEW Story:CondVolWithMult
  DATATYPE
    Float
  LONGFORM
    ConditionedVolume
  DESCRIPTION
    "The story conditioned volume, including multipliers" 
  INPUTCLASS
    NotInput
  UNITS 
    ft2  
  MINIMUM 
    0
  DEFAULT 
    SumChildren( Spc:CondVolWithMult )
ENDRULE
; SAC 09/16/21 (MFam)
RULE NEW ResZnGrp:CondVolWithMult
  DATATYPE
    Float
  LONGFORM
    ConditionedVolume
  DESCRIPTION
    "The ResZnGrp conditioned volume, including multipliers" 
  INPUTCLASS
    NotInput
  UNITS 
    ft2  
  MINIMUM 
    0
  DEFAULT 
    SumChildren( ResZn:CondVolWithMult )
ENDRULE

RULE Bldg:TotCondVol
  DESCRIPTION
    "The total building conditioned volume." 
  INPUTCLASS
    Default
  MINIMUM 
    1
  UNITS 
    ft2  
  REPORTPRECISION
    0
  DEFAULT 
    SumChildren( Story:CondVolWithMult ) +
    SumChildren( ResZnGrp:CondVolWithMult )
ENDRULE

// ------------ Calculate Story floor area by category -------------------------
// Residential
RULE NEW Story:ResCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    ResidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total Story residential (conditioned) floor area,
     including multipliers.  This includes high-rise
     Residential and Hotel / Motel." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( Spc:ResCondFlrAreaWithMult )
ENDRULE
; SAC 09/16/21 (MFam)
RULE NEW ResZnGrp:ResCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    ResidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total ResZnGrp residential (conditioned) floor area,
     including multipliers.  This includes high-rise
     Residential and Hotel / Motel." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( ResZn:CondFlrAreaWithMult ) +
    SumChildren( ResOtherZn:ResCondFlrAreaWithMult )
ENDRULE

// Nonresidential
RULE NEW Story:NonResCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    NonresidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total Story nonresidential (conditioned) floor area,
     including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2    
  DEFAULT 
    SumChildren( Spc:NonResCondFlrAreaWithMult ) 
ENDRULE
; SAC 09/16/21 (MFam)
RULE NEW ResZnGrp:NonResCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    NonresidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total ResZnGrp nonresidential (conditioned) floor area,
     including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2    
  DEFAULT 
    SumChildren( ResOtherZn:NonResCondFlrAreaWithMult ) 
ENDRULE

// Total
RULE NEW Story:TotCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    TotalConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total Story (conditioned) floor area,
     including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2    
  DEFAULT 
    NonResCondFlrAreaWithMult + ResCondFlrAreaWithMult
ENDRULE
; SAC 09/16/21 (MFam)
RULE NEW ResZnGrp:TotCondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    TotalConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total ResZnGrp (conditioned) floor area,
     including multipliers."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2    
  DEFAULT 
    NonResCondFlrAreaWithMult + ResCondFlrAreaWithMult
ENDRULE

// Total
RULE NEW Story:SpcCnt
  DATATYPE
    Float
  LONGFORM
    SpaceCount
  DESCRIPTION
    "The total number of child Spaces for the BuildingStory."
  INPUTCLASS
    NotInput 
  DEFAULT 
    ChildCount( Spc)
  CHECKSIM
    if( SpcCnt = 0 )
    then 
      PostError("BuildingStory '%s' has no child spaces. Remove this BuildingStory
                 object from the model, or add Spaces.",
                 Name)
   else UNCHANGED
   endif
ENDRULE
; SAC 09/16/21 (MFam)   - removed ResGarage references - SAC 02/05/22 (MFam)
RULE NEW ResZnGrp:ChildZnCnt
  DATATYPE
    Float
  LONGFORM
    ChildZoneCount
  DESCRIPTION
    "The total number of children (ResZn, ResOtherZn, ResAttic &
     ResCrawlSpc) for the ResidentialZoneGroup."
  INPUTCLASS
    NotInput 
  DEFAULT 
    ChildCount( ResZn ) + ChildCount( ResOtherZn ) +
    ChildCount( ResAttic ) + ChildCount( ResCrawlSpc )
  CHECKSIM
    if( ChildZnCnt = 0 )
    then 
      PostError("ResidentialZoneGroup '%s' has no children. Remove this
                 ResidentialZoneGroup object from the model, or add
                 valid child objects (ResZn, ResOtherZn,
                 ResAttic and/or ResCrawlSpc).", Name)
   else UNCHANGED
   endif
ENDRULE


// ------------ Calculate Bldg floor areas by category -------------------------
// Bldg Totals ----------
RULE Bldg:ResFlrArea
  DESCRIPTION
    "The total building residential (conditioned) floor area." 
  HELP
    "The total building residential floor area, including multipliers. 
     Default includes entire area of residential spaces modeled explicitly. 
     If whole building is not modeled, then the total residential floor area 
     should be entered here. " 
  INPUTCLASS
    Default
  MINIMUM 
    0
  UNITS 
    ft2  
  REPORTPRECISION
    0
  DEFAULT 
    SumChildren( Story:ResCondFlrAreaWithMult ) +
    SumChildren( ResZnGrp:ResCondFlrAreaWithMult ) 
  CHECKCODE
    if( WholeBldgModeled = 1 .AND. LocalStatus( ResFlrArea ) > 4 )
    then
      PostError("Building total, conditioned, RESIDENTIAL floor area should not 
                 be user defined if the whole building is modeled. Uncheck this
                 option and restore the default to perform compliance.")
    else UNCHANGED
    endif
  ENDRULE

RULE Bldg:NonResFlrArea
  DESCRIPTION
    "The total building nonresidential (conditioned) floor area."
  HELP
    "The total building nonresidential floor area, including multipliers. 
     Default includes entire area of nonresidential spaces modeled explicitly. 
     If whole building is not modeled, then the total nonresidential floor area 
     should be entered here. " 
  RESETS
    ResetThisWhenTheFollowingIsModified
      Bldg:WholeBldgModeled
  INPUTCLASS
    Default
  MINIMUM 
    0
  COMMONMINIMUM
    1
  UNITS 
    ft2    
  REPORTPRECISION
    0
  DEFAULT 
    SumChildren( Story:NonResCondFlrAreaWithMult ) +
    SumChildren( ResZnGrp:NonResCondFlrAreaWithMult ) 
  CHECKCODE
    if( WholeBldgModeled = 1 .AND. LocalStatus( NonResFlrArea ) > 4 )
    then
      PostError("Building total, conditioned, NONRESIDENTIAL floor area should not 
                 be user defined if the whole building is modeled. Uncheck this
                 option and restore the default to perform compliance.")
    else UNCHANGED
    endif
ENDRULE

RULE Bldg:TotCondFlrArea
  DESCRIPTION
    "The total building conditioned floor area." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  COMMONMINIMUM
    1
  UNITS 
    ft2  
  REPORTPRECISION
    0
  DEFAULT 
    ResFlrArea + NonResFlrArea
ENDRULE


// HighRiseResidential
RULE NEW Story:HighRiseResCondFlrAreaWithMult
  RULESETS
    T24N  
  DATATYPE
    Float
  LONGFORM
    HighRiseResidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total Story residential (conditioned) floor area,
     including multipliers.  This includes high-rise
     Residential only." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( Spc:HighRiseResCondFlrAreaWithMult )
ENDRULE
; SAC 09/15/21 (MFam)   ; revised to only set >0 if Bldg >= 4 flrs - SAC 04/21/22
RULE NEW ResZnGrp:HighRiseResCondFlrAreaWithMult
  RULESETS
    T24N  
  DATATYPE
    Float
  LONGFORM
    HighRiseResidentialConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total residential zone group (conditioned) floor area." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    if (IfValidAnd( Bldg:AboveGrdStoryCnt >= 4 ))
    then  SumChildren( ResZn:CondFlrAreaWithMult )
    else  0  endif
  SIZING
    HighRiseResCondFlrAreaWithMult
  ANNUAL
    HighRiseResCondFlrAreaWithMult
ENDRULE

RULE ResZnGrp:TreeDescrip
  DEFAULT
    if (SumChildren( ResZn:FloorArea ) > 0.1)
    then  Format( "%s dwellings (%s ft2)", FltToStr( SumChildren( ResZn:NumDwellingUnits ) ),
                                           FltToStr( SumChildren( ResZn:FloorArea        ) ) )
    else  UNDEFINED  endif
ENDRULE
RULE ResZnGrp:TreeDescrip
  DEFAULT
    if (SumChildren( ResOtherZn:Area ) > 0.1)
    then  if (LocalStatus( TreeDescrip ) > 0)
          then  TreeDescrip + Format( " & %s ft2 other/common", FltToStr( SumChildren( ResOtherZn:Area ) ) )
          else                Format(    "%s ft2 other/common", FltToStr( SumChildren( ResOtherZn:Area ) ) )  endif
    else  UNCHANGED  endif
ENDRULE
RULE ResZnGrp:TreeDescrip
  DEFAULT
    if (SumChildren( ResOtherZn:Area ) > (SumChildren( ResOtherZn:CondFloorArea ) + 0.1) .AND.
        SumChildren( ResOtherZn:CondFloorArea ) > 0.1)
    then  TreeDescrip + Format( " (%s ft2 uncond)", FltToStr( (SumChildren( ResOtherZn:Area ) - SumChildren( ResOtherZn:CondFloorArea )) ) )
    else  UNCHANGED  endif
ENDRULE

; SAC 04/22/24 (tic #3554)
RULE ResZnGrp:HasIntCorridors
  DESCRIPTION
    "Whether this zone group includes interior corridor space(s), impacting how
     natural (cooling) ventilation is simulated." 
  INPUTCLASS
    Default
  DEFAULT
    if ( SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "- specify -"                              ) > 1 .OR.
         SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "Aging Eye/Low-vision (Corridor Area)"     ) > 1 .OR.
         SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "Corridor Area"                            ) > 1 .OR.
         SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "Unoccupied-Exclude from Gross Floor Area" ) > 1 .OR.
         SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "Unoccupied-Include in Gross Floor Area"   ) > 1 .OR.
         SumChildrenIf( ResOtherZn:Area, ResOtherZn:SpcFunc = "All other"                                ) > 1 )
    then  1  else  0  endif
ENDRULE
         // ??   317,    "Concourse and Atria Area"


RULE NEW Bldg:HighRiseResFlrArea
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
    HighRiseResidentialFloorArea
  DESCRIPTION
    "The total building residential (conditioned) floor area.  This is high-rise
     Residential only." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( Story:HighRiseResCondFlrAreaWithMult ) +
    SumChildren( ResZnGrp:HighRiseResCondFlrAreaWithMult ) 
ENDRULE

// Hotel-Motel
RULE NEW Story:HotelMotelGuestRmCondFlrAreaWithMult
  RULESETS
    T24N  
  DATATYPE
    Float
  LONGFORM
    HotelMotelGuestRoomConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The total Story residential (conditioned) floor area,
     including multipliers.  This includes Hotel/Motel Guest Room only." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( Spc:HotelMotelGuestRmCondFlrAreaWithMult )
ENDRULE

RULE NEW Bldg:HotelMotelGuestRmFlrArea
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
    HotelMotelGuestRoomFloorArea
  DESCRIPTION
    "The total building residential (conditioned) floor area.  This is 
     Hotel/Motel Guest Room only." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumChildren( Story:HotelMotelGuestRmCondFlrAreaWithMult ) 
ENDRULE



// -------------- Living Unit Count --------------------
RULE Bldg:HighRiseResLivingUnitCnt
  RULESETS
    T24N
  DESCRIPTION
    "The total number of high-rise residential units in the
     building."
  HELP
    ""
  REFERENCE 
    Standards Definitions - High-Rise Residential Buidling
  INPUTCLASS
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0
  COMMONMAXIMUM
    100
  MAXIMUM
    10000
  REPORTPRECISION
    0
  DEFAULT
    if (IfValidAnd( Proj:IsHighRiseMFam > 0 ))
    then  SumChildrenIf( Spc:ResLivingUnitCntWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces" ) + ValidOr( ResProj:NumDwellingUnits,0 )
    else  SumChildrenIf( Spc:ResLivingUnitCntWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces" )
    endif
  CHECKCODE
    if( HighRiseResLivingUnitCnt = 0 .AND. HighRiseResFlrArea > 0 )
    then
      PostError("Buildings with spaces classified as High-Rise Residential Living 
                 Space are required to provide the number of High-Rise Res. 
                 living units in the building.")
    else if( HighRiseResLivingUnitCnt > 0 .AND. HighRiseResFlrArea = 0 )
    then 
      PostError("Buildings with no spaces classified as High-Rise Residential
                 Living Units may not specify HighRiseResLivingUnitCnt > 0.")
    else UNCHANGED
    endif endif
ENDRULE

RULE Bldg:LowRiseResLivingUnitCnt
  RULESETS
    T24N
  DESCRIPTION
    "The total number of low-rise residential units in the
     building."
  HELP
    ""
  REFERENCE 
    Standards Definitions - Low-Rise Residential Buidling
  INPUTCLASS
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0
  COMMONMAXIMUM
    100
  MAXIMUM
    10000
  REPORTPRECISION
    0
  DEFAULT
    if (IfValidAnd( Proj:IsLowRiseMFam > 0 ))
    then  ValidOr( ResProj:NumDwellingUnits,0 )
    else  0
    endif
ENDRULE

RULE Bldg:ResLivingUnitCnt
  RULESETS
    T24N
  DESCRIPTION
    "The total number of residential units in the building."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    HighRiseResLivingUnitCnt + LowRiseResLivingUnitCnt
ENDRULE

// -----------------------------------------------------------------------------
RULE Bldg:HotelMotelGuestRmCnt
  RULESETS
    T24N
  DESCRIPTION
    "The total number of Hotel/Motel units in the building."
  HELP
    ""
  REFERENCE 
    Standards Definitions - Hotel/Motel
  INPUTCLASS
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0
  COMMONMAXIMUM
    100
  MAXIMUM
    10000
  REPORTPRECISION
    0
  DEFAULT
    SumChildrenIf( Spc:HotelMotelGuestRmCntWithMult, Spc:SpcFunc = "Hotel/Motel Guest Room" )
  CHECKCODE
    if( HotelMotelGuestRmCnt = 0 .AND. HotelMotelGuestRmFlrArea > 0 )
    then
      PostError("Buildings with spaces classified as Hotel/Motel Guest Room 
                 Space are required to provide the number of Hotel/Motel 
                 Guest Rooms in the building.")
    else if( HotelMotelGuestRmCnt > 0 .AND. HotelMotelGuestRmFlrArea = 0 )
    then 
      PostError("Buildings with no spaces classified as Hotel/Motel Guest Rooms
                 may not specify HotelMotelGuestRmCnt > 0.")
    else UNCHANGED
    endif endif
ENDRULE

RULE Bldg:LivingUnitCntRpt
  DESCRIPTION
    "The total number of Residential or Hotel/Motel living units in the
     building."
  HELP
    ""
  REFERENCE 
    ACM-5.9.1 Water Heating
  INPUTCLASS : T24N
    NotInput
  INPUTCLASS : S901G ECBC
    Optional
  MINIMUM
    0
  COMMONMINIMUM
    0
  COMMONMAXIMUM
    100
  MAXIMUM
    10000
  REPORTPRECISION
    0
  DEFAULT : T24N
    ResLivingUnitCnt + HotelMotelGuestRmCnt
  DEFAULT : S901G ECBC
    0
ENDRULE


RULE NEW Bldg:HighRiseResVentType
  DATATYPE
    Enumeration
  LONGFORM
    HighRiseResidentialVentilationType
  DESCRIPTION
    "The type of ventilation systems serving high-rise residential
     zones in the building."
  HELP
    "Used to determine compliance with requirements on system type;
     i.e. balanced vs. supply/exhaust only. The criteria is determined
     only by zones with a new HVAC system."
  INPUTCLASS
    NotInput
  OPTION 
    Balanced
    SupplyOnly
    ExhaustOnly
    Mixed
  DEFAULT
    if( SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:IsNewHVAC > 0 ) > 0 )
    then 
      if( SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:HighRiseResVentIsBalanced > 0 ) = 
          SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:IsNewHVAC > 0 ) )
      then "Balanced"
      else 
      if( SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:HighRiseResVentIsSup > 0 ) = 
          SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:IsNewHVAC > 0 ) )
      then "SupplyOnly" 
      else 
      if( SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:HighRiseResVentIsExh > 0 ) = 
          SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:IsNewHVAC > 0 ) )
      then "ExhaustOnly" 
      else "Mixed"
      endif endif endif
    else UNDEFINED
    endif
  CHECKCODE : T24N_2016
    UNCHANGED
  CHECKCODE : T24N
    if( Proj:IsAddOrAlt = 0 .AND.
        SumChildrenIf( ThrmlZn:IsHighRiseRes, ThrmlZn:IsNewHVAC > 0 ) > 0 .AND. 
        IfValidAnd( HighRiseResVentType = "Mixed" ) )
    then // Is a new HRR project, and has 'Mixed' ventiltion type.
         // Per Ticket  3201, check does not apply to AddOrAlt projects
      PostError("The new building project contains high-rise residential zones 
                 with a mixture of balanced, supply, and/or exhaust
                 ventilation systems. This is not allowed. All ventilation
                 systems shall be of the same type. If 'SupplyOnly' or
                 'ExhaustOnly' systems are used, dwelling unit envelope air
                 leakage shall comply with 120.1(b)2.A.iv.b.")
    else UNCHANGED
    endif
ENDRULE



// -------------- Average Living Unit Floor Area --------------------
//RULE Bldg:AvgLivingUnitFlArea
//  RULESETS
//    T24N
//  DESCRIPTION
//    "This is floor area of a typical living unit served by the water heater."
//  HELP
//    ""
//  REFERENCE 
//    ACM-5.9.1 Water Heating
//  INPUTCLASS
//    NotInput
//  MINIMUM
//    0 
//  COMMONMINIMUM
//    0
//  COMMONMAXIMUM
//    2500
//  MAXIMUM
//    15000
//  UNITS
//    ft2
//  REPORTPRECISION
//    0
//  DEFAULT
//    if( HighRiseResLivingUnitCnt = 0 )
//    then 0
//    else if
//      (((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces")) /
//         HighRiseResLivingUnitCnt) > 0)
//    then ((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces")) /
//           HighRiseResLivingUnitCnt)
////    else if
////      (((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "Hotel/Motel Guest Room")) /
////         HotelMotelGuestRmCnt) > 0)
////    then ((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "Hotel/Motel Guest Room")) /
////           HotelMotelGuestRmCnt)
//    else 0
////    endif
//    endif endif
//  SIZING
//    if( HighRiseResLivingUnitCnt = 0 )
//    then 0
//    else if
//      (((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces")) /
//         HighRiseResLivingUnitCnt) > 0)
//    then ((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "High-Rise Residential Living Spaces")) /
//           HighRiseResLivingUnitCnt)
////    else if
////      (((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "Hotel/Motel Guest Room")) /
////         HotelMotelGuestRmCnt) > 0)
////    then ((SumChildrenIf(Spc:FlrAreaWithMult, Spc:SpcFunc = "Hotel/Motel Guest Room")) /
////           HotelMotelGuestRmCnt)
//    else 0
////    endif
//    endif endif
//  ANNUAL
//    AvgLivingUnitFlArea
//ENDRULE


// ---------- Calculate Bldg Res/NonRes Floor Area Ratios ---------------------
// Used for setting Bldg:CompleteBldgOccClass
RULE NEW Bldg:ResAreaRatio
  DATATYPE
    Float
  LONGFORM
    ResidentialAreaRatio  
  INPUTCLASS
    NotInput
  UNITS 
    ft2   
  DEFAULT 
    if( Bldg:ResFlrArea + Bldg:NonResFlrArea > 0 )
    then Bldg:ResFlrArea / ( Bldg:ResFlrArea + Bldg:NonResFlrArea )
    else 0
    endif
ENDRULE

RULE NEW Bldg:NonResAreaRatio
  DATATYPE
    Float
  LONGFORM
    NonResidentialAreaRatio  
  INPUTCLASS
    NotInput
  UNITS 
    ft2   
  DEFAULT 
    if( Bldg:ResFlrArea + Bldg:NonResFlrArea > 0 )
    then Bldg:NonResFlrArea / ( Bldg:ResFlrArea + Bldg:NonResFlrArea )
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE Bldg:BldgType
  RULESETS
    T24N
  DESCRIPTION
    "The Building Type for reporting purposes" 
  INPUTCLASS
    NotInput
  OPTION
    Relocatable Public School Building
    Hotel-Motel
    High-Rise Residential
    Low-Rise Residential
    Nonresidential
    Mixed Occupancy
    Healthcare
  DEFAULT
    if( Bldg:RelocPubSchoolBldg = 1 )
    then 
      "Relocatable Public School Building"
    else if( IfValidAnd( Proj:HlthCareFacility = 1 ) )
    then
      "Healthcare"
    else if( IfValidAnd( Bldg:HotelMotelGuestRmCnt > 5 ) )
    then 
      "Hotel-Motel"
    else if( Bldg:AboveGrdStoryCnt >= 4 .AND.
             ( ValidOr( ResFlrArea, 0 ) / ValidOr( TotCondFlrArea, 1 ) ) >= 0.8 )
    then
      "High-Rise Residential"
    else if( Bldg:AboveGrdStoryCnt < 4 .AND. IfValidAnd( Bldg:LowRiseResLivingUnitCnt > 0 ) .AND.
             ( ValidOr( ResFlrArea, 0 ) / ValidOr( TotCondFlrArea, 1 ) ) >= 0.8 )
    then
      "Low-Rise Residential"
    else if( Bldg:AboveGrdStoryCnt > 0 .AND.
             ( ValidOr( ResFlrArea, 0 ) <= 0 ) )
    then "Nonresidential" 
    else "Mixed Occupancy"
    endif endif endif endif endif endif
  CHECKCODE : T24N_2016 T24N_2019
    if( AboveGrdStoryCnt < 4 .AND.
        HighRiseResFlrArea >= ( TotCondFlrArea * 0.20 ) )
    then
      PostError("The residential floor area of this building is greater than or equal to 20% of the total 
      building area and the building is less than 4 stories tall. The non-residential portion should be 
      modeled in CBECC-Com and the residential in CBECC-Res.")
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW Proj:BldgTypeScreenVal
  LONGFORM
    BuildingTypeScreenValue
  DATATYPE
    Integer
  DESCRIPTION
    "Screen Condition for displaying Prescriptive DHW exceptional condition combo"
  INPUTCLASS
    NotInput
  DEFAULT
    if( Bldg:BldgType = "High-Rise Residential" .OR.  Bldg:BldgType = "Hotel-Motel" ) then 1
    else 0
    endif
ENDRULE


//-----------------------------------------------------------------------------

RULE Proj:ResTDVSrcEmisFac
  DESCRIPTION
    "For T24N 2022 research purposes only. If checked, the Residential TDV, 
     Source Energy and Emission Factors will be applied to the 
     highrise residential and hotel/motel projects."
  DEFAULT : T24N_2016 T24N_2019
    0
  DEFAULT : T24N
    if( IfValidAnd(BldgTypeScreenVal > 0 ) )
    then 
      1
    else
      0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW Proj:ResTDVSrcEmisFacChange
  DATATYPE
    Integer  
  HELP
    "Flag for checking and switching the TDV/Source/Emmissions factors to Residential."
  INPUTCLASS : T24N_2016 T24N_2019
    NotInput
  INPUTCLASS : T24N
    NotInput
  DEFAULT : T24N_2016 T24N_2019
    0
  DEFAULT : T24N   
    if( ResTDVSrcEmisFac > 0 .AND. IfValidAnd(Bldg:HotelMotelGuestRmCnt > 5)  )
    then 1
    else
    if( ResTDVSrcEmisFac > 0 .AND. IfValidAnd( Bldg:AboveGrdStoryCnt >= 4 ) .AND. IfValidAnd(Bldg:HighRiseResLivingUnitCnt > 0) )
    then 1
    else 0
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW Proj:ShdgCredRpt
  DESCRIPTION
    "A text string (Yes or No) to be reported indicating that compliance credit
     is being taking for window shading, used for reporting."
  INPUTCLASS
    NotInput
  ANNUAL_PROPOSED
    if( SumChildren(FenCons:ShdgCred) > 0 ) then "Yes"
    else "No"
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE Bldg:ShdgCredRpt
  DESCRIPTION
    "A text string (Yes or No) to be reported indicating that compliance credit
     is being taking for window shading, used for reporting."
  INPUTCLASS
    NotInput
  ANNUAL_PROPOSED
    Proj:ShdgCredRpt
ENDRULE

// -----------------------------------------------------------------------------
RULE Bldg:WinShdgNoteRpt
  DESCRIPTION
    "A text string consisting of a note to Table B of the ENV DETAILS section 
     which will include text if the note applies or will be blank otherwise."
  INPUTCLASS
    NotInput
  ANNUAL_PROPOSED
    if( ChildCount(ExtShdgObj) > 0 ) then
      "Windows in this building may be shaded by shading devices not tied directly to windows.  Check building plans."
    else " "
    endif
  ANNUAL_BASELINE
    " "
ENDRULE

// -----------------------------------------------------------------------------
RULE Bldg:OrientationRpt
  DESCRIPTION
    "A text string for reporting the cardinal direction (N, S, E, W) of the 
     building for inclusion in Table A of the general section of the 
     compliance forms."
  INPUTCLASS
    NotInput
  DEFAULT
    if( BldgAz >  45 .AND. BldgAz <= 135 ) 
    then Format("(E) %.0f deg", BldgAz)
    else if( BldgAz > 135 .AND. BldgAz <= 225 ) 
    then Format("(S) %.0f deg", BldgAz)
    else if( BldgAz > 225 .AND. BldgAz <= 315 ) 
    then Format("(W) %.0f deg", BldgAz)
    else if( BldgAz > 315 .OR.  BldgAz <=  45 ) 
    then Format("(N) %.0f deg", BldgAz)
    else "Not Defined"
    endif endif endif endif
ENDRULE
