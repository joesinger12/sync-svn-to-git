// ThermalZone - Zone Level Sizing
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  This rule file addresses the following building descriptors:

     

// ********** Design Supply Air Temperatures ***********************************
RULE NEW ThrmlZn:IsExtZn
  DATATYPE
    Integer
  LONGFORM
    IsExteriorZone
  DESCRIPTION
    "A flag that specifies if a thermal zone is an Interior or Exterior zone." 
  HELP
    "For the purposes of the ACM, an exterior zone is defined as one that has 
     any exterior walls, ceilings or floors. An interior zone is defined as 
     having no surfaces exposed to ambient or below-grade conditions.
     See building descriptor 'Design Airflow' for detail."
  REFERENCE 
    NACM Section 5.6.5.1
  INPUTCLASS 
    NotInput
  DEFAULT
    if( MaxRevRef( Spc:ThrmlZnRef, Spc:IsExtSpc ) > 0 )
    then 1 // Zone has exterior exposure
    else 0 // Zone has no exterior exposure
    endif
  SIZING
    u:IsExtZn
  ANNUAL
    z:IsExtZn
ENDRULE

TABLE DsgnTStatSetpt
  SchGrpName            MaxHtgSetpt       MinClgSetpt
  "Assembly"            70.0              75.0
  "Data"                60.0              80.0  
  "Health"              70.0              75.0
  "Laboratory"          70.0              75.0
  "Manufacturing"       70.0              75.0
  "Office"              70.0              75.0
  "Parking"             60.0              85.0
  "ResidentialLiving"   68.0              78.0
  "ResidentialCommon"   68.0              78.0
  "Restaurant"          70.0              75.0
  "Retail"              70.0              75.0
  "School"              70.0              75.0
  "Warehouse"           70.0              75.0
  "AllOn"               70.0              75.0
ENDTABLE


// System level clg/htg sizing ratios, for defaulting zone sizing factors
// when using HVACAutosizing > 0
RULE ZnSys:ClgSizingRat
  DESCRIPTION
    "Multiplier used to default zone airflow and component cooling sizing ratios."
  INPUTCLASS 
    Optional
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    1.25
  DEFAULT 
    if( IfValidAnd( Proj:HVACAutoSizing > 0 ) )
    then Proj:ClgSizingRat
    else 1.15
    endif
ENDRULE
RULE ZnSys:HtgSizingRat
  DESCRIPTION
    "Multiplier used to default zone airflow and component heating sizing ratios."
  INPUTCLASS 
    Optional
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  DEFAULT 
    if( IfValidAnd( Proj:HVACAutoSizing > 0 ) )
    then Proj:HtgSizingRat
    else 1.25
    endif
ENDRULE

RULE NEW ThrmlZn:HVACAutoSizing
// Not currently used
  DATATYPE
    Integer
  DESCRIPTION
    "Whether or not the zone airflows are to be sized by the simulation."
  INPUTCLASS
    NotInput   
  DEFAULT
    Proj:HVACAutoSizing
ENDRULE
// ==================== COOLING ================================================
RULE ThrmlZn:ClgDsgnSizingFac
  DESCRIPTION
    "The factor used to adjust the design cooling air flow. 
     Applicable to sizing runs only."
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Cooling Sizing Factor."
  INPUTCLASS 
    Optional
  MINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  DEFAULT
    if( IsCond > 0 )
    then
      if( IfValidAnd( Proj:HVACAutoSizing > 0 ) .AND. 
          LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then PriAirCondgSysRef[1]:ClgSizingRat
      else 1.0
      endif
    else UNDEFINED
    endif
  SIZING
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then 1.0
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND. 
          LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then PriAirCondgSysRef[1]:ClgSizingRat
      else 1.0
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then 1.0
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) )
      then z:ClgDsgnSizingFac
      else 1.0
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:ClgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling condition.  
     Applicable to sizing runs only."
// Calculated from z:ClgDsgnFlowSim. This value reflects zone multipliers, 
// so SDD reported value is divided by ThrmlZn:Mult
  INPUTCLASS 
    Optional
  SIZING
    if( IsCond > 0 )
    then
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then ClgDsgnFlow
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  ANNUAL  
    if( IsCond > 0 )
    then
      if( ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
          IfValidAnd( z:ClgDsgnFlowSim >= 0 ) )
      then z:ClgDsgnFlowSim / Mult
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then z:ClgDsgnFlow
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:ClgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling conditions,
     For simulation only, includes zone multipliers."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // E+ Report: HVAC Sizing Summary -> Zone Cooling -> Calculated Design Air Flow
    // This simulation value includes zone multipliers 
  SIZING
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then UNDEFINED
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then ClgDsgnFlow * Mult
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif  
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then UNDEFINED
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then ClgDsgnFlow * Mult
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:ClgDsgnSupAirTempDiff
  DESCRIPTION
    "The temperature difference between the cooling supply air temperature
     and room air temperature used for sizing zone/system air flows." 
  HELP
    "This value, combined with the design zone temperature setpoint, is used to 
     calculated the default design supply air temperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.3
  INPUTCLASS 
    Default
  MINIMUM
    3
  COMMONMAXIMUM
    21
  MAXIMUM
    40
  DEFAULT : T24N
    if( IsCond > 0 .AND. LocalCompAssigned( PriAirCondgSysRef[1] ) )
    then
;      if( IsExtZn > 0 )
;      then 20
;      else 15
;      endif
      if( IfValidAnd( HasNoClg > 0 ) )
      then UNDEFINED
      else if( LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType( "ZnSys" ) )
      then 20
      else if( PriAirCondgSysRef[1]:IsMultiZnSys )
      then
        if( IsExtZn = 0 )
        then 15
        else 20
        endif
      else 20
      endif endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IfValidAnd( HasClg > 0 ) )
    then 20
    else UNDEFINED
    endif
  SIZING : T24N_2013 T24N_2016 T24N_2019
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) )
    then
;      if( IsExtZn > 0 )
;      then 20
;      else 15
;      endif
      if( zp:HtgOnlySysArea > 0 .OR. HasNoClg > 0 )
      then // Dummy value for calculating cooling zone air flow for heating only systems
           // If not specified, and there is no vent air, sizing sim will crash
        20 
      else
      if( ( IfValidAnd( BaseSysNum = 5 ) .OR. 
            IfValidAnd( BaseSysNum = 6 ) ) .AND.
          IsExtZn = 0 )
      then // Core zones of VAV baseline are sized for 15F deltaT
        15 
      else
      if( IfValidAnd( BaseSysNum = 1 ) .AND. 
          IfValidAnd( Proj:ResBaseSysChange > 0 ) )
      then // Was 15F for 2019.1.2 release, changed back to 20 for 1.3, see ticket 2691/3212
        20
      else // 20F for all single zone systems, see GC issue 857
        20 
      endif endif endif
    else if( zp:HtgOnlySysArea > 0 )
    then // Dummy value for calculating cooling zone air flow for heating only systems
         // If not specified, and there is no vent air, sizing sim will crash
      20 
    else ClgDsgnSupAirTempDiff
    endif endif
  SIZING : T24N
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) )
    then
      if( zp:HtgOnlySysArea > 0 .OR. HasNoClg > 0 )
      then // Dummy value for calculating cooling zone air flow for heating only systems
           // If not specified, and there is no vent air, sizing sim will crash
        20 
      else
      if( ( IfValidAnd( BaseSysNum = 5 ) .OR.
            IfValidAnd( BaseSysNum = 6 ) ) .AND.
          IsExtZn = 0 )
      then // Core zones of VAV baseline are sized for 15F deltaT
        15 
      else
      if( IfValidAnd( BaseSysNum = 10) .OR.
          IfValidAnd( BaseSysNum = 11) )
      then // computer room baseline are sized for 15F deltaT for economizer requirement, see ticket 3320
        15
      else
      if( IfValidAnd( BaseSysNum = 1 ) .AND. 
          IfValidAnd( Proj:ResBaseSysChange > 0 ) )
      then // Was 15F for 2019.1.2 release, changed back to 20 for 1.3, see ticket 2691/3212
        20
      else // 20F for all single zone systems, see GC issue 857
        20 
      endif endif endif endif
    else if( zp:HtgOnlySysArea > 0 )
    then // Dummy value for calculating cooling zone air flow for heating only systems
         // If not specified, and there is no vent air, sizing sim will crash
      20 
    else ClgDsgnSupAirTempDiff
    endif endif
  SIZING : S901G ECBC
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) )
    then 
      if( zp:HtgOnlySysArea > 0 .OR. HasNoClg > 0 )
      then UNDEFINED
      else 20
      endif
    else if( zp:HtgOnlySysArea > 0 )
    then UNDEFINED      
    else ClgDsgnSupAirTempDiff
    endif endif
  ANNUAL
    z:ClgDsgnSupAirTempDiff
ENDRULE
RULE ThrmlZn:ClgDsgnSupAirTemp
  DESCRIPTION
    "The design cooling supply air temperature for sizing zone/system air flows." 
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Cooling Design Supply Air Temperature, and is used for sizing with
     ! - Zone Cooling Design Supply Air Temperature Input Method = SupplyAirTemperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.3
  INPUTCLASS 
    Default
  MINIMUM
    50
  COMMONMINIMUM
    54
  COMMONMAXIMUM
    66
  MAXIMUM
    75
  DEFAULT : T24N
    if( IsCond > 0 .AND. LocalCompAssigned( PriAirCondgSysRef[1] ) )
    then
      if( IfValidAnd( HasNoClg > 0 ) )
      then UNDEFINED
      else if( IsRes .OR. IsResCommon )
      then 58
      else if( CompRmArea > 0 )
      then 60
      else if( LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType( "ZnSys" ) )
      then 55
      else if( PriAirCondgSysRef[1]:IsMultiZnSys .AND. IsExtZn = 0 )
      then 60
      else 55
      endif endif endif endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond > 0 )
    then
      if( HasClg = 0 )
      then UNDEFINED
      else ValidOr( ClgTstatSchMinVal, 75) - ValidOr( ClgDsgnSupAirTempDiff, 0 )
      endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       LocalStatus( ClgDsgnSupAirTemp ) = 0 )
;   then 
;     PostError("The CoolingDesignSupplyAirTemperature for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.")
;   else UNCHANGED
;   endif
  SIZING : T24N_2013 T24N_2016 T24N_2019
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
        LocalStatus( PredominantFuncGrp ) > 0 )
    then
      if( zp:HtgOnlySysArea > 0 .OR. HasNoClg > 0 )
      then UNDEFINED      
      else if( CompRmAreaNew > 0 .OR. CompRmAreaAltered > 0 )
      then 60
      else DsgnTStatSetpt:MinClgSetpt("SchGrpName", PredominantFuncGrp) -
           ClgDsgnSupAirTempDiff
      endif endif
    else if( zp:HtgOnlySysArea > 0 )
    then UNDEFINED    
    else ClgDsgnSupAirTemp
    endif endif
  SIZING : T24N
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
        LocalStatus( PredominantFuncGrp ) > 0 )
    then
      if( zp:HtgOnlySysArea > 0 .OR. HasNoClg > 0 )
      then UNDEFINED      
      else
      if( CompRmAreaNew > 0 .OR. CompRmAreaAltered > 0 )
      then 65 // computer room baseline are sized for 65F for economizer requirement, see ticket 3320
      else
      if( FuncGrpArea24Hr > 0 )
      then DsgnTStatSetpt:MinClgSetpt("SchGrpName", "AllOn")
      else DsgnTStatSetpt:MinClgSetpt("SchGrpName", PredominantFuncGrp) -
           ClgDsgnSupAirTempDiff
      endif endif endif
    else if( zp:HtgOnlySysArea > 0 )
    then UNDEFINED    
    else ClgDsgnSupAirTemp
    endif endif
  SIZING : S901G ECBC
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) )
    then
      if( zp:HtgOnlySysArea > 0 )
      then UNDEFINED 
      else ValidOr( ClgTstatSchMinVal, 75) - ValidOr( ClgDsgnSupAirTempDiff, 0 )
      endif
    else if( zp:HtgOnlySysArea > 0 )
    then UNDEFINED      
    else ClgDsgnSupAirTemp
    endif endif
  ANNUAL 
    z:ClgDsgnSupAirTemp
ENDRULE

// ==================== ResOtherZn COOLING =====================================
RULE ResOtherZn:ClgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling condition.  
     Applicable to sizing runs only."
// Calculated from z:ClgDsgnFlowSim. This value reflects zone multipliers, 
// so SDD reported value is divided by ThrmlZn:Mult
  INPUTCLASS 
    Prescribed
  SIZING
    if( IsCond > 0 )
    then
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then ClgDsgnFlow
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  ANNUAL  
    if( IsCond > 0 )
    then
      if( ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
          IfValidAnd( z:ClgDsgnFlowSim >= 0 ) )
      then z:ClgDsgnFlowSim / Mult
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then z:ClgDsgnFlow
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ResOtherZn:ClgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling conditions,
     For simulation only, includes zone multipliers."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // CSE Report: * - ZB-TRMLSZG.CSV -> ClgMxFlw
    // This simulation value includes zone multipliers 
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then UNDEFINED
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( ClgDsgnFlow > 0 ) )
      then ClgDsgnFlow * Mult
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE


// ==================== HEATING ================================================
RULE ThrmlZn:HtgDsgnSizingFac
  DESCRIPTION
    "The factor used to adjust the design heating air flow. Applicable to sizing
     runs only."
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Heating Sizing Factor."
  INPUTCLASS 
    Optional
  MINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  DEFAULT
    if( IsCond > 0 )
    then
      if( IfValidAnd( Proj:HVACAutoSizing > 0 ) .AND. 
          LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then PriAirCondgSysRef[1]:HtgSizingRat
      else 1.0
      endif
    else UNDEFINED
    endif
  SIZING
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then 1.0
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND. 
          LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then PriAirCondgSysRef[1]:HtgSizingRat
      else 1.0
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then 1.0
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) )
      then z:HtgDsgnSizingFac
      else 1.0
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:HtgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition.  
     Applicable to sizing runs only."  
// Calculated from z:HtgDsgnFlowSim. This value reflects zone multipliers, 
// so SDD reported value is divided by ThrmlZn:Mult
  INPUTCLASS 
    Optional
  SIZING
    if( IsCond > 0 )
    then
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then HtgDsgnFlow
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  ANNUAL  
    if( IsCond > 0 )
    then
      if( ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
          IfValidAnd( z:HtgDsgnFlowSim >= 0 ) )
      then z:HtgDsgnFlowSim / Mult
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then z:HtgDsgnFlow
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:HtgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition,
     for simulation."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // E+ Report: HVAC Sizing Summary -> Zone Heating -> Calculated Design Air Flow
    // This simulation value includes zone multipliers
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then UNDEFINED
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then HtgDsgnFlow * Mult
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:HtgDsgnSupAirTempDiff
  DESCRIPTION
    "The temperature difference between the heating supply air temperature
     and room air temperature for sizing zone/system airflows."
  HELP
    "This value, combined with the design zone temperature setpoint, is used to 
     calculated the design supply air temperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.4
  INPUTCLASS 
    Default
  DEFAULT : T24N
    if( IsCond > 0 )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then ValidOr( HtgDsgnSupAirTemp, 105 ) - ValidOr( HtgTstatSchMaxVal, 70 )
      else if( IsRes .OR. IsResCommon )
      then 27
      else 25
      endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond > 0 )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then ValidOr( HtgDsgnSupAirTemp, 105 ) - ValidOr( HtgTstatSchMaxVal, 70 )
      else 20
      endif
    else UNDEFINED
    endif
  SIZING : T24N
    if( IsCond > 0 .AND. IfValidAnd( BaseSysNum > 0 ) .AND.
        LocalStatus( PredominantFuncGrp ) > 0 )
    then
      if( FuncGrpArea24Hr > 0 )
      then 95 - DsgnTStatSetpt:MinClgSetpt("SchGrpName", "AllOn")
      else 95 - DsgnTStatSetpt:MaxHtgSetpt("SchGrpName", PredominantFuncGrp)
      endif
    else HtgDsgnSupAirTempDiff
    endif
  SIZING : S901G ECBC
    if( IsCond > 0 .AND. ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) )   
    then
      if( HtgOnlySysArea > 0 )
      then 105 - ValidOr( HtgTstatSchMaxVal, 70 )
      else 20
      endif
    else HtgDsgnSupAirTempDiff
    endif
  ANNUAL
    z:HtgDsgnSupAirTempDiff
ENDRULE
RULE ThrmlZn:HtgDsgnSupAirTemp
  DESCRIPTION
    "The design heating supply air temperature for sizing zone/system air flows." 
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Heating Design Supply Air Temperature, and is used for sizing with
     ! - Zone Heating Design Supply Air Temperature Input Method = SupplyAirTemperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.4
  INPUTCLASS 
    Default
  MINIMUM
    60
  COMMONMINIMUM
    74
  COMMONMAXIMUM
    106
  MAXIMUM
    120
  DEFAULT : T24N
    if( IsCond > 0 )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then 95 // Change to 105F?
      else 95 
      endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond > 0 )
    then 
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then 105
      else ValidOr( HtgTstatSchMaxVal, 70 ) + ValidOr( HtgDsgnSupAirTempDiff, 20 )
      endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       LocalStatus( HtgDsgnSupAirTemp ) = 0 )
;   then 
;     PostError("The HeatingDesignSupplyAirTemperature for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.")
;   else UNCHANGED
;   endif
  SIZING : T24N
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then
        if( HtgOnlySysArea > 0 )
        then 95 // Change to 105F?
        else 95
        endif
      else HtgDsgnSupAirTemp
      endif
    else UNDEFINED
    endif
  SIZING : S901G ECBC
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then
        if( HtgOnlySysArea > 0 )
        then 105
        else ValidOr( HtgTstatSchMaxVal, 70 ) + ValidOr( HtgDsgnSupAirTempDiff, 20 )
        endif
      else HtgDsgnSupAirTemp
      endif
    else UNDEFINED
    endif
  ANNUAL
    z:HtgDsgnSupAirTemp 
ENDRULE
RULE ThrmlZn:HtgDsgnMaxFlowFrac
  DESCRIPTION
    "The maximum supply air flow fraction during heating for sizing zone reheat
     coils capacity" 
  HELP
    "This property impacts the behavior for EnergyPlus Sizing:Zone. 
     A zero or UNDEFINED value results in Sizing:Zone !- Heating Design Air Flow Method 
     to be DesignDay.  A non-zero value results in DesignDayWithLimit, which should only 
     be used for VAV terminals and SZVAV."
  REFERENCE 
    NACM Section 5.6.5.1
  INPUTCLASS 
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0.15
  COMMONMAXIMUM
    0.51
  MAXIMUM
    1.0
  DEFAULT
    if( IsCond > 0 .AND. 
        LocalCompAssigned( TrmlUnitRef ) .AND.
        LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then
      if( TrmlUnitRef:Type = "VAVReheatBox" )
      then
        if( TrmlUnitRef:ReheatCtrlMthd = "DualMaximum" )
        then 
          if( IfValidAnd( TrmlUnitRef:PriAirFlowMax > 0 ) .AND.
              IfValidAnd( TrmlUnitRef:HtgAirFlowMax > 0 ) )
          then Max(0.5, TrmlUnitRef:HtgAirFlowMax/TrmlUnitRef:PriAirFlowMax)
          else 0.5
          endif
        else 
        if( IfValidAnd( TrmlUnitRef:PriAirFlowMax > 0 ) .AND.
            IfValidAnd( TrmlUnitRef:HtgAirFlowMax > 0 ) ) 
        then Max(0.2, TrmlUnitRef:HtgAirFlowMax/TrmlUnitRef:PriAirFlowMax)
        else 0.2
        endif endif  
      else if( TrmlUnitRef:Type = "ParallelFanBox" )
      then 0.5 // For 90.1
      else if( PriAirCondgSysRef[1]:Type = "SZVAVAC" .OR.
               PriAirCondgSysRef[1]:Type = "SZVAVHP" .OR.
               PriAirCondgSysRef[1]:Type = "SZVAVDFHP" )
      then 0.5 // SZVAV control currently limited to 50%
      else 0.0 // All other cases assume max flow in heating is 100%. 
               // Setting to 0 results in E+ Sizing:Zone !- Heating Design Air Flow Method
               // to be DesignDay
      endif endif endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( LocalCompAssigned( TrmlUnitRef ) = 0 )
;   then UNCHANGED
;   else
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       TrmlUnitRef:Type = "VAVReheatBox" .AND.
;       LocalStatus( HtgDsgnMaxFlowFrac ) = 0 )
;   then 
;     PostError("The HeatingDesignMaxFlowFracton for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.", Name)
;   else UNCHANGED
;   endif endif
  SIZING
    if( IsCond > 0 .AND. LocalCompAssigned( TrmlUnitRef ) )
    then
      if( LocalCompAssigned( PropVentOnlyAirSysRef ) > 0 )
      then 0.0 // VentOnly AirSys created by rules
               // Setting to 0 results in E+ Sizing:Zone !- Heating Design Air Flow Method
               // to be DesignDay
      else
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then
        if( TrmlUnitRef:Type = "VAVReheatBox" )
        then 
          if( TrmlUnitRef:ReheatCtrlMthd = "DualMaximum" )          
          then 0.5
          else 0.2
          endif
        else if( TrmlUnitRef:Type = "ParallelFanBox" )
        then 0.5 // For 90.1
        else if( PriAirCondgSysRef[1]:Type = "SZVAVAC" .OR.
                 PriAirCondgSysRef[1]:Type = "SZVAVHP" .OR.
                 PriAirCondgSysRef[1]:Type = "SZVAVDFHP" )
        then 0.5 // SZVAV control currently limited to 50%
        else 0.0 // All other cases assume max flow in heating is 100%. 
                 // Setting to 0 results in E+ Sizing:Zone !- Heating Design Air Flow Method
                 // to be DesignDay
        endif endif endif
      else HtgDsgnMaxFlowFrac
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( IsCond > 0 .AND. LocalCompAssigned( TrmlUnitRef ) )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then
        if( TrmlUnitRef:Type = "VAVReheatBox" )
        then 
          if( TrmlUnitRef:ReheatCtrlMthd = "DualMaximum" )          
          then 0.5
          else 0.2
          endif
        else if( TrmlUnitRef:Type = "ParallelFanBox" )
        then 0.5 // For 90.1
        else if( PriAirCondgSysRef[1]:Type = "SZVAVAC" .OR.
                 PriAirCondgSysRef[1]:Type = "SZVAVHP" .OR.
                 PriAirCondgSysRef[1]:Type = "SZVAVDFHP" )
        then 0.5 // SZVAV control currently limited to 50%
        else 0.0 // All other cases assume max flow in heating is 100%. 
                 // Setting to 0 results in E+ Sizing:Zone !- Heating Design Air Flow Method
                 // to be DesignDay
        endif endif endif
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE

// ==================== ResOtherZn HEATING =====================================
RULE ResOtherZn:HtgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition.  
     Applicable to sizing runs only."  
// Calculated from z:HtgDsgnFlowSim. This value reflects zone multipliers, 
// so SDD reported value is divided by ThrmlZn:Mult
  INPUTCLASS 
    Optional
  SIZING
    if( IsCond > 0 )
    then
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then HtgDsgnFlow
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  ANNUAL  
    if( IsCond > 0 )
    then
      if( ( BaseSysNum > 0 .OR. AddClgToProp > 0 ) .AND.
          IfValidAnd( z:HtgDsgnFlowSim >= 0 ) )
      then z:HtgDsgnFlowSim / Mult
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then z:HtgDsgnFlow
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE
RULE ResOtherZn:HtgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition,
     for simulation."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // CSE Report: * - ZB-TRMLSZG.CSV -> HtgMxFlw
    // This simulation value includes zone multipliers 
  ANNUAL
    if( IsCond > 0 )
    then
      if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
      then UNDEFINED
      else
      if( IfValidAnd( u:Proj:HVACAutoSizing > 0 ) .AND.
          IfValidAnd( HtgDsgnFlow > 0 ) )
      then HtgDsgnFlow * Mult
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
ENDRULE

// ==================== DOAS ===================================================
RULE ThrmlZn:SizeForDOAS
  DESCRIPTION
    "A flag that indicates zone equipment should be sized for a DOAS
     supplying ventilation air to the zone."
  HELP
    "This property translates to EnergyPlus Sizing:Zone,
     Field: Account for Dedicated Outdoor Air System."
  INPUTCLASS
    Optional
  DEFAULT
    if( IsCond > 0 .AND.
        IfValidAnd( UniqueVentSysAssigned > 0 ) .AND.
        IfValidAnd( PropVentSysIsExh = 0 ) )
    then 1
    else UNDEFINED
    endif
  SIZING
    if( IfValidAnd( VentSysRef:IsUnitaryHRRVentSys > 0 ) .AND.
        IfValidAnd( ResVentEquipChkReq > 0 ) .AND. 
        IfValidAnd( ResVentEquipIsAccessible = 0 ) )
    then // System is unitary ventilation system and is not accessible, Standard = Proposed
      SizeForDOAS
    else
    if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
    then
      if( VentSysRef:BaseSysNum > 100 .AND.
          VentSysRef:BaseSysNum < 200 )
      then 1 
      else UNDEFINED
      endif
    else
    if( IsCond > 0 )
    then SizeForDOAS
    else UNDEFINED
    endif endif endif
  ANNUAL
    if( IfValidAnd( VentSysRef:IsUnitaryHRRVentSys > 0 ) .AND.
        IfValidAnd( ResVentEquipChkReq > 0 ) .AND. 
        IfValidAnd( ResVentEquipIsAccessible = 0 ) )
    then // System is unitary ventilation system and is not accessible, Standard = Proposed
      z:SizeForDOAS
    else
    if( BaseSysNum > 0 .OR. AddClgToProp > 0 )
    then
      if( VentSysRef:BaseSysNum > 100 .AND.
          VentSysRef:BaseSysNum < 200 )
      then 1 
      else UNDEFINED
      endif
    else z:SizeForDOAS
    endif endif
ENDRULE
RULE ThrmlZn:SizeForDOASCtrl
  DESCRIPTION
    "If the zone equipment is sized for a DOAS, the type of control that
     should be assumed for sizing.

     Neutral: DOAS supplies air at a neutral temperature condition.

     NeutralDehumidifed: DOAS supplies air at a neutral, dehumidified 
     condition

     Cold: DOAS supplies air that will cool the zone.

     For each control option, high/low setpoints are specified by 
     SizeForDOASTempLow and SizeForDOASTempHi."
  HELP
    "This property translates to EnergyPlus Sizing:Zone,
     Field: Dedicated Outdoor Air System Control Strategy."
  INPUTCLASS
    Optional
  OPTION
    Neutral
    NeutralDehumidifed
    Cold
  DEFAULT
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then "Neutral"
    else UNDEFINED
    endif
  SIZING
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then SizeForDOASCtrl
    else UNDEFINED
    endif
  ANNUAL
    z:SizeForDOASCtrl
ENDRULE
RULE ThrmlZn:SizeForDOASTempLow
  DESCRIPTION
    "If the zone equipment is sized for a DOAS, this is the low
     temperature for the design control strategy."
  HELP
    "This property translates to EnergyPlus Sizing:Zone,
     Field: Dedicated Outdoor Air Low Temperatue Setpoint for Design" 
  INPUTCLASS
    Optional
  UNITS
    °F
  COMMONMINIMUM
    50
  COMMONMAXIMUM
    90
  DEFAULT
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then
      if( IfValidAnd( SizeForDOASCtrl = "Neutral" ) )
      then 50
      else
      if( IfValidAnd( SizeForDOASCtrl = "NeutralDehumidified" ) )
      then 58
      else
      if( IfValidAnd( SizeForDOASCtrl = "Cold" ) )
      then 55
      else UNDEFINED
      endif endif endif
    else UNDEFINED
    endif
  SIZING
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then SizeForDOASTempLow
    else UNDEFINED
    endif
  ANNUAL
    z:SizeForDOASTempLow
ENDRULE
RULE ThrmlZn:SizeForDOASTempHi
  DESCRIPTION
    "If the zone equipment is sized for a DOAS, this is the high
     temperature for the design control strategy."
  HELP
    "This property translates to EnergyPlus Sizing:Zone,
     Field: Dedicated Outdoor Air High Temperatue Setpoint for Design" 
  INPUTCLASS
    Optional
  UNITS
    °F
  COMMONMINIMUM
    50
  COMMONMAXIMUM
    90
  DEFAULT
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then
      if( IfValidAnd( SizeForDOASCtrl = "Neutral" ) )
      then 75
      else
      if( IfValidAnd( SizeForDOASCtrl = "NeutralDehumidified" ) )
      then 70
      else
      if( IfValidAnd( SizeForDOASCtrl = "Cold" ) )
      then 60
      else UNDEFINED
      endif endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( IfValidAnd( SizeForDOAS > 0 ) .AND.
        IfValidAnd( SizeForDOASTempHi = SizeForDOASTempLow ) )
    then 
      PostError("SizeForDOAS has been selected for AirSystem '%s',
                 and SizeForDOASTempHi is set to the same value
                 as SizeForDOASTempLow. This not allowed.", Name)
    else UNCHANGED
    endif 
  SIZING
    if( IfValidAnd( SizeForDOAS > 0 ) )
    then SizeForDOASTempHi
    else UNDEFINED
    endif
  ANNUAL
    z:SizeForDOASTempHi
ENDRULE


// ** debugging rule evaluation duration **
RULE NEW Proj:RuleEvalDuration
  DEFAULT 
    LogDuration( "            time to evaluate Thermal Zone DEFAULT rules:  %.3f seconds" )
  SIZING_PROPOSED 
    LogDuration( "            time to evaluate Thermal Zone SIZING_PROPOSED rules:  %.3f seconds" )
  SIZING_BASELINE
    LogDuration( "            time to evaluate Thermal Zone SIZING_BASELINE rules:  %.3f seconds" )
  ANNUAL_PROPOSED
    LogDuration( "            time to evaluate Thermal Zone ANNUAL_PROPOSED rules:  %.3f seconds" )
  ANNUAL_BASELINE
    LogDuration( "            time to evaluate Thermal Zone ANNUAL_BASELINE rules:  %.3f seconds" )
ENDRULE

