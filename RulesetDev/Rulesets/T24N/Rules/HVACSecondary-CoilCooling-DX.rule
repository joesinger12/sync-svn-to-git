// HVAC Secondary Systems - Cooling Coils - DX
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:
//  Section 5.7.5.1 - General
//      Cooling Source
//      Total Cooling Capacity
//      Sensible Cooling Capacity
//  Section 5.7.5.2 - Direct Expansion
//      Condenser Type



// ---------- Section 5.7.5.2 - Direct Expansion -------------------------------

// ********** DX Cooling Coil Efficiency Rounding *****************************
RULE NEW CoilClg:EERSEERRndFact
  DATATYPE
    Float
  LONGFORM
    EERSEERRoundingFactor
  DESCRIPTION
    "The smallest factor of the rounded direct expansion (DX) cooling coil 
     efficiency per the applicable AHRI testing standard."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "DirectExpansion" )
    then
      if( ParentComp = "ZnSys" )
      then // Part of an ZoneSystem  
        if ( SysType = "PTAC" .OR. SysType = "PTHP" )
        then 0.1
        else if ( SysType = "SPVAC" .OR. SysType = "SPVHP" )
        then 0.05
        else if ( SysType = "WSHP" )
        then 0.05
        else if ( SysType = "VRF" )
        then UNDEFINED // RndFact defined in VRFSys.rule
        else if( ZnSys:SubType = "CRAC" )
        then 0.01
// Older rule based on AHRI STANDARD 210/240-2008 for 1-phase, 
// which referenced rounding requirements in subpart B 430.23(m)(3)(i) of Title 10 of the CFR
;       else if ( IfValidAnd( ParentValid( ClgCap ) < 65000 ) )
;       then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65kBtu/hr
;         if( ZnSys:SubType = "Packaged1Phase" .OR.
;             ZnSys:SubType = "Split1Phase" )
;         then 0.025
;         else 0.05
;         endif
// New rule based on // Revised rounding based on AHRI 210/240-2017, which defines rounding in 6.1.2
        else if( IfValidAnd( ZnSys:ClgCap < 65000 ) )
        then 0.05 
        else // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65kBtu/hr
          0.1
        endif endif endif endif endif endif
      else if( ParentComp = "AirSys" )
      then // Part of an AirSystem  
        if ( SysType = "SPVAC" .OR. SysType = "SPVHP" )
        then 0.05
        else if( AirSys:SubType = "CRAC" )
        then 0.01
// Older rule based on AHRI STANDARD 210/240-2008 for 1-phase, 
// which referenced rounding requirements in subpart B 430.23(m)(3)(i) of Title 10 of the CFR
;       else if ( IfValidAnd( Parent2Valid( ClgCap ) < 65000 ) )
;       then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65kBtu/hr
;         if( AirSys:SubType = "Packaged1Phase" .OR.
;             AirSys:SubType = "Split1Phase" )
;         then 0.025
;         else 0.05
;         endif
// New rule based on // Revised rounding based on AHRI 210/240-2017, which defines rounding in 6.1.2
        else if( IfValidAnd( AirSys:ClgCap < 65000 ) )
        then 0.05
        else  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65kBtu/hr
          0.1
        endif endif endif
      else // catch all
        0.01  
      endif endif
    else  
      UNDEFINED
    endif
  ANNUAL
    if( Type = "DirectExpansion" )
    then
      if( ParentComp = "ZnSys" )
      then // Part of an ZoneSystem  
        if ( SysType = "PTAC" .OR. SysType = "PTHP" )
        then 0.1
        else if ( SysType = "SPVAC" .OR. SysType = "SPVHP" )
        then 0.05
        else if ( SysType = "WSHP" )
        then 0.05
        else if ( SysType = "VRF" )
        then UNDEFINED // RndFact defined in VRFSys.rule
        else if( ZnSys:SubType = "CRAC" )
        then 0.01
// Older rule based on AHRI STANDARD 210/240-2008 for 1-phase, 
// which referenced rounding requirements in subpart B 430.23(m)(3)(i) of Title 10 of the CFR
;       else if ( IfValidAnd( ParentValid( ClgCap ) < 65000 ) )
;       then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65kBtu/hr
;         if( ZnSys:SubType = "Packaged1Phase" .OR.
;             ZnSys:SubType = "Split1Phase" )
;         then 0.025
;         else 0.05
;         endif
// New rule based on // Revised rounding based on AHRI 210/240-2017, which defines rounding in 6.1.2
        else if( IfValidAnd( ZnSys:ClgCap < 65000 ) )
        then 0.05 
        else // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65kBtu/hr
          0.1
        endif endif endif endif endif endif
      else if( ParentComp = "AirSys" )
      then // Part of an AirSystem  
        if ( SysType = "SPVAC" .OR. SysType = "SPVHP" )
        then 0.05
        else if( AirSys:SubType = "CRAC" )
        then 0.01
// Older rule based on AHRI STANDARD 210/240-2008 for 1-phase, 
// which referenced rounding requirements in subpart B 430.23(m)(3)(i) of Title 10 of the CFR
;       else if ( IfValidAnd( Parent2Valid( ClgCap ) < 65000 ) )
;       then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65kBtu/hr
;         if( AirSys:SubType = "Packaged1Phase" .OR.
;             AirSys:SubType = "Split1Phase" )
;         then 0.025
;         else 0.05
;         endif
// New rule based on // Revised rounding based on AHRI 210/240-2017, which defines rounding in 6.1.2
        else if( IfValidAnd( AirSys:ClgCap < 65000 ) )
        then 0.05
        else // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65kBtu/hr
          0.1
        endif endif endif
      else // catch all
        0.01  
      endif endif
    else  
      UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW CoilClg:EERSEERRndScl
  DATATYPE
    Integer
  LONGFORM
    EERSEERRoundingScale
  DESCRIPTION
    "The scale or number of digits to the right of the decimal place in the
     rounded direct expansion (DX) cooling coil efficiency per the applicable 
     AHRI testing standard."
  INPUTCLASS
    NotInput    
  DEFAULT
    if( IfValidAnd( EERSEERRndFact = 0.1 ) )
    then 1
    else
    if( IfValidAnd( EERSEERRndFact = 0.05 ) .OR.
        IfValidAnd( EERSEERRndFact = 0.01 ) )
    then 2
    else
    if( IfValidAnd( EERSEERRndFact = 0.025 ) )
    then 3
    else UNDEFINED
    endif endif endif
  ANNUAL
    if( IfValidAnd( EERSEERRndFact = 0.1 ) )
    then 1
    else
    if( IfValidAnd( EERSEERRndFact = 0.05 ) .OR.
        IfValidAnd( EERSEERRndFact = 0.01 ) )
    then 2
    else
    if( IfValidAnd( EERSEERRndFact = 0.025 ) )
    then 3
    else UNDEFINED
    endif endif endif
ENDRULE

// Set a flag indicating the standard design system is SEER2
// -----------------------------------------------------------------------------
RULE NEW AirSys:StdIsSEER2
  DATATYPE
    Integer
  LONGFORM
    StandardIsSEER2
  DESCRIPTION
    "A flag that indicates SEER2/EER2/HSPF2 efficiency ratings should be used for the standard design."
  INPUTCLASS 
    NotInput
  ANNUAL : T24N_2019
    0 // No std design AirSys uses SEER2 in 2019 
  ANNUAL : T24N
    if( BaseSysNum > 200 )
    then 1 // Baseline Res Common Area is AirSys split 1-phase system <65kBtu/hr, use SEER2
    else 0
    endif
ENDRULE  
RULE NEW ZnSys:StdIsSEER2
  DATATYPE
    Integer
  LONGFORM
    StandardIsSEER2
  DESCRIPTION
    "A flag that indicates SEER2/EER2/HSPF2 efficiency ratings should be used for the standard design."
  INPUTCLASS 
    NotInput
  ANNUAL : T24N_2019
    if( BaseSysNum = 1 )
    then
      if( IfValidAnd( CtrlZnRef:StdIsSEER2 > 0 ) )
      then 1
      else 0
      endif
    else 0
    endif
  ANNUAL : T24N
    if( BaseSysNum = 1 )
    then 1  // Baseline Hotel/Motel res system is ZnSys packaged 1-phase system <65kBtu/hr, use SEER2
    else 0
    endif
ENDRULE  
RULE NEW CoilClg:StdIsSEER2
  DATATYPE
    Integer
  LONGFORM
    StandardIsSEER2
  DESCRIPTION
    "A flag that indicates SEER2/EER2/HSPF2 efficiency ratings should be used for the standard design."
  INPUTCLASS 
    NotInput
  ANNUAL
    if( ParentComp = "AirSys" )
    then AirSys:StdIsSEER2
    else
    if( ParentComp = "ZnSys" )
    then ZnSys:StdIsSEER2
    else 0
    endif endif
ENDRULE  

// ********** Direct Expansion Cooling Minimum Efficiencies ******************************
// CodeMin for SEER
RULE NEW CoilClg:CodeMinSEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumSEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. IfValidAnd( AirSys:SEER2IsValid > 0 ) )
        then UNDEFINED // Uses SEER2
        else
        if( SysType = "SPVAC" .OR. SysType ="SPVHP" )
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          UNDEFINED
        else 
        if( IfValidAnd( AirSys:UnitaryCat != "NA" ) .AND.
            CapTotNetRtd < 65000 .AND. 
            ValidOr( Parent2( SubType ), "undef" ) != "undef" ) // work-around for not having Parent2Status
        then
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd )
        else UNDEFINED
        endif endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // Part of a ZoneSystem
        if (SysType = "SPVAC" .OR. SysType ="SPVHP" )
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
        UNDEFINED
        else 
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. IfValidAnd( ZnSys:SEER2IsValid > 0 ) )
        then UNDEFINED
        else
        if( IfValidAnd( ZnSys:UnitaryCat != "NA" ) .AND.
            CapTotNetRtd < 65000 .AND. 
            ValidOr( Parent( SubType ), "undef" ) != "undef" ) // work-around for not having Parent2Status
        then
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", ZnSys:UnitaryCat,
                                     "SubCategory", ZnSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd )
        else UNDEFINED
        endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else UNCHANGED
    endif 
  ANNUAL : T24N_2019
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( BaseSysNum = 1 )
      then 
        if( ZnSys:StdIsSEER2 > 0 )
        then UNDEFINED // Baseline uses SEER2/EER2
        else // Hotel/Motel or HRR, look up requirement
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", ZnSys:UnitaryCat,
                                     "SubCategory", ZnSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
        endif
      else
      if( IfValidAnd( CapTotNetRtd < 65000 ) )
      then 
        if( ParentComp = "ZnSys" )
        then // ZnSys packaged 3-phase, uses SEER (PropNoClg)
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", ZnSys:UnitaryCat,
                                     "SubCategory", ZnSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd ) 
        else // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, uses SEER
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd ) 
        endif
      else UNDEFINED // Not used if net capacity >= 65 MBH
      endif endif
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( IfValidAnd( StdIsSEER2 > 0 ) )
      then // Baseline Hotel/Motel and Res Common Area is packaged 1-phase system < 65kBtu/hr, uses SEER2/EER2
        UNDEFINED
      else
      if( IfValidAnd( CapTotNetRtd < 65000 ) )
      then 
        if( ParentComp = "ZnSys" )
        then // ZnSys packaged 3-phase, uses SEER (PropNoClg)
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", ZnSys:UnitaryCat,
                                     "SubCategory", "Packaged3Phase",
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd ) 
        else // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, uses SEER
          UnitaryACHPMinEffReq:SEER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", "Packaged3Phase",
                                     "CondenserType", "Air",
                                     "SizeCategory", CapTotNetRtd ) 
        endif
      else UNDEFINED // Not used if net capacity >= 65 MBH
      endif endif
    else UNCHANGED
    endif 
ENDRULE
// CodeMin for SEER2
RULE NEW CoilClg:CodeMinSEER2
  DATATYPE
    Float
  LONGFORM
    CodeMinimumSEER2
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. AirSys:SEER2IsValid > 0 )
        then // User has choosen SEER2 metric
          if( AirSys:HSPF2IsValid > 0 )
          then // Is a HP
            UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                        "Category", "HP", 
                                        "SubCategory", AirSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd )
          else // Is AC
            UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                        "Category", "AC", 
                                        "SubCategory", AirSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          endif
        else UNDEFINED
        endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // User has choosen SEER2 metric
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. ZnSys:SEER2IsValid > 0 )
        then
          if( ZnSys:HSPF2IsValid > 0 )
          then // Is a HP
            UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                        "Category", "HP", 
                                        "SubCategory", ZnSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) // 14.3 // Split HP
          else // Is AC
            UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                        "Category", "AC", 
                                        "SubCategory", ZnSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          endif
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N_2019
    if( BaseSysNum = 1 .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ZnSys:StdIsSEER2 > 0 )
      then // Std System 1 uses SEER2
        UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                    "Category", ZnSys:UnitaryCat, 
                                    "SubCategory", ZnSys:SubType,
                                    "CondenserType", "Air", 
                                    "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
      else UNDEFINED
      endif
    else UNCHANGED
    endif 
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ParentComp = "ZnSys" )
      then 
        if( ZnSys:StdIsSEER2 > 0 )
        then // Baseline Hotel/Motel res system is ZnSys packaged 1-phase system < 65kBtu/hr, uses EER2a
          UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                      "Category", ZnSys:UnitaryCat, 
                                      "SubCategory", ZnSys:SubType,
                                      "CondenserType", "Air", 
                                      "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
        else UNDEFINED
        endif
      else
      if( ParentComp = "AirSys" )
      then 
        if( AirSys:StdIsSEER2 > 0 )
         // Baseline Res Common Area is assumed AirSys packaged 1-phase system < 65kBtu/hr, uses EER2a
        then
          UnitaryACHPMinEffReq:SEER2( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air", 
                                     "SizeCategory", Min( CapTotNetRtd, 64999 ) )
        else UNDEFINED // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, use SEER/EER
        endif
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

RULE NEW CoilClg:SEERToSEER2Rat
  DATATYPE
    Float
  LONGFORM
    SEERToSEER2Ratio
  DESCRIPTION
    "The ratio of SEER2/SEER for a given equipment type."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" )
    then // Part of an AirSystem
      if( AirSys:SEER2IsValid > 0 )
      then
        UnitaryACHPMinEffReq:SEERToSEER2Rat( "StandardsVersion", Proj:StdsVersion,
                                             "Category", AirSys:UnitaryCat,
                                             "SubCategory", AirSys:SubType,
                                             "CondenserType", "Air", 
                                             "SizeCategory", CapTotNetRtd )
      else UNDEFINED
      endif
    else // ------------------------------------------------------------------
    if( ParentComp = "ZnSys" )
    then
      if( ZnSys:SEER2IsValid > 0 )
      then
        UnitaryACHPMinEffReq:SEERToSEER2Rat( "StandardsVersion", Proj:StdsVersion,
                                             "Category", ZnSys:UnitaryCat,
                                             "SubCategory", ZnSys:SubType,
                                             "CondenserType", "Air", 
                                             "SizeCategory", CapTotNetRtd )
      else UNDEFINED
      endif
    else UNDEFINED
    endif endif
  ANNUAL : T24N_2019
    if( BaseSysNum = 1 .AND. IfValidAnd( CapTotNetRtd > 0 ) .AND. IfValidAnd( CodeMinSEER2 > 0 ) )
    then // Std System 1 uses SEER2
      UnitaryACHPMinEffReq:SEERToSEER2Rat( "StandardsVersion", Proj:StdsVersion,
                                           "Category", ZnSys:UnitaryCat, 
                                           "SubCategory", ZnSys:SubType,
                                           "CondenserType", "Air", 
                                           "SizeCategory", Min( CapTotNetRtd, 64999 ) )
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ParentComp = "ZnSys" )
      then 
        if( ZnSys:StdIsSEER2 > 0 )
        then // Baseline Hotel/Motel res system is ZnSys packaged 1-phase system < 65kBtu/hr, uses EER2a
          UnitaryACHPMinEffReq:SEERToSEER2Rat( "StandardsVersion", Proj:StdsVersion,
                                      "Category", ZnSys:UnitaryCat, 
                                      "SubCategory", ZnSys:SubType,
                                      "CondenserType", "Air", 
                                      "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
        else UNDEFINED
        endif
      else
      if( ParentComp = "AirSys" )
      then 
        if( AirSys:StdIsSEER2 > 0 )
         // Baseline Res Common Area is assumed AirSys packaged 1-phase system < 65kBtu/hr, uses EER2a
        then
          UnitaryACHPMinEffReq:SEERToSEER2Rat( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air", 
                                     "SizeCategory", Min( CapTotNetRtd, 64999 ) )
        else UNDEFINED // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, use SEER/EER
        endif
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

// Determine code minimum EER for systems with DX cooling coils
RULE NEW CoilClg:CodeMinEER2
  DATATYPE
    Float
  LONGFORM
    CodeMinimumEER2
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. AirSys:EER2IsValid > 0 )
        then // User has choosen SEER2 metric
          if( IfValidAnd( DXSEER2 >= 15.2 ) )
          then // Use EER2b
            UnitaryACHPMinEffReq:EER2b( "StandardsVersion", Proj:StdsVersion,
                                        "Category", AirSys:UnitaryCat,  
                                        "SubCategory", AirSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          else // Use EER2a
            UnitaryACHPMinEffReq:EER2a( "StandardsVersion", Proj:StdsVersion,
                                        "Category", AirSys:UnitaryCat,
                                        "SubCategory", AirSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          endif
        else UNDEFINED
        endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. ZnSys:EER2IsValid > 0 )
        then // User has choosen SEER2 metric
          if( IfValidAnd( DXSEER2 >= 15.2 ) )
          then // Use EER2b
            UnitaryACHPMinEffReq:EER2b( "StandardsVersion", Proj:StdsVersion,
                                        "Category", ZnSys:UnitaryCat,
                                        "SubCategory", ZnSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          else // Use EER2a
            UnitaryACHPMinEffReq:EER2a( "StandardsVersion", Proj:StdsVersion,
                                        "Category", ZnSys:UnitaryCat, 
                                        "SubCategory", ZnSys:SubType,
                                        "CondenserType", "Air", 
                                        "SizeCategory", CapTotNetRtd ) 
          endif
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N_2019
    if( BaseSysNum = 1 .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then  // Baseline Hotel/Motel res system is ZnSys packaged 1-phase system < 65kBtu/hr
      if( IfValidAnd( ZnSys:StdIsSEER2 > 0 ) )
      then // Std System 1 uses SEER2/EER2
        UnitaryACHPMinEffReq:EER2a( "StandardsVersion", Proj:StdsVersion,
                                    "Category", ZnSys:UnitaryCat, 
                                    "SubCategory", ZnSys:SubType,
                                    "CondenserType", "Air", 
                                    "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
      else UNDEFINED
      endif 
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( ParentComp = "ZnSys" )
      then 
        if( ZnSys:StdIsSEER2 > 0 .AND. ZnSys:UnitaryCat = "AC" )
        then // EER2 only applicable to 'AC' category
          UnitaryACHPMinEffReq:EER2a( "StandardsVersion", Proj:StdsVersion,
                                      "Category", ZnSys:UnitaryCat, 
                                      "SubCategory", ZnSys:SubType,
                                      "CondenserType", "Air", 
                                      "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
        else UNDEFINED
        endif
      else
      if( ParentComp = "AirSys" )
      then 
        if( AirSys:StdIsSEER2 > 0 .AND. AirSys:UnitaryCat = "AC" )
        then // EER2 only applicable to 'AC' category
          UnitaryACHPMinEffReq:EER2a( "StandardsVersion", Proj:StdsVersion,
                                     "Category", AirSys:UnitaryCat,
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air", 
                                     "SizeCategory", Min( CapTotNetRtd, 64999 ) )
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

RULE NEW CoilClg:CodeMinEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. IfValidAnd( AirSys:EER2IsValid > 0 ) )
        then UNDEFINED
        else
        if( SysType = "SPVAC" .OR. SysType = "SPVHP" )
        then // SPVAC/HP
          if( CapTotNetRtd >= 240000)
          then UNDEFINED // No requirement for > 240 kBtu/hr
          else // Look-up from table  
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", SysType,
              "SubCategory", "*",
              "CondenserType", "Air",
              "SizeCategory", CapTotNetRtd )
          endif
        else
        if( IfValidAnd( CndsrType = "Air" ) .AND.
            AirSys:UnitaryCat != "NA" .AND.
            Proj:StdsVersionYr >= 2015 .AND.
            AirSys:SubType != "Packaged3Phase" .AND.
            CapTotNetRtd < 65000 )
        then // <65 kBtu/hr and >= 2015 efficiency requirements
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd )
        else        
        if( IfValidAnd( CndsrType = "Air" ) .AND.
            AirSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Air-source >65 kBtu/hr
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else
        if( IfValidAnd( CndsrType = "WaterSource" ) .AND.
            AirSys:UnitaryCat != "NA" )
        then // Water-source
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", "WaterSource",
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else UNDEFINED
        endif endif endif endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" ) 
      then // Part of an ZoneSystem
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. IfValidAnd( ZnSys:EER2IsValid > 0 ) )
        then UNDEFINED
        else
        if( SysType = "SPVAC" .OR. SysType ="SPVHP" )
        then // SPVAC/SPVHP
          if( CapTotNetRtd >= 240000)
          then UNDEFINED // No requirement for > 240 kBtu/hr
          else  
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", SysType,
              "SubCategory", "*",
              "CondenserType", "Air",
              "SizeCategory", CapTotNetRtd )
          endif
        else
        if( ( SysType = "SZAC" .OR. SysType = "SZHP"  .OR. SysType = "SZDFHP" .OR.
              SysType = "MiniSplitAC" .OR. SysType = "MiniSplitHP" ) .AND.
            CapTotNetRtd < 65000 .AND.
            Proj:StdsVersionYr >= 2015 .AND.
            ZnSys:UnitaryCat != "NA" .AND.
            ZnSys:SubType != "Packaged3Phase" )
        then // <65 kBtu/hr and >= 2015 efficiency requirements
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", ZnSys:SubType,
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd )
        else
        if( ( SysType = "SZAC" .OR. SysType = "SZHP"  .OR. SysType = "SZDFHP" .OR.
              SysType = "MiniSplitAC" .OR. SysType = "MiniSplitHP" ) .AND.
            CapTotNetRtd >= 65000 .AND.
            IfValidAnd( CndsrType = "Air" ) .AND.
            ZnSys:UnitaryCat != "NA" )
        then // Air-source >65 kBtu/hr, including minisplits
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", ZnSys:SubType,
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        else 
        if( SysType = "PTAC" )
        then // PTAC
        // Rules currently only cover PTAC and PTHP in new constrution or newly 
        // conditioned buildings or additions.  Rules for PTAC/PTHP replacements 
        // not currently supported.
          if( Proj:StdsVersionYr >= 2016 )
          then   
            14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
          else 
            13.8 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )            
          endif
        else
        if( SysType = "PTHP" )
        then // PTHP 
          14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
        else
        if( SysType = "WSHP" .AND. 
            IfValidAnd( CndsrType = "WaterSource" ) .AND.
            ZnSys:UnitaryCat != "NA" )
        then // Water-source HPs
          if( CapTotNetRtd < 65000 )
          then // Water-source, <65 kBtu/hr
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", ZnSys:UnitaryCat,
              "SubCategory", ZnSys:SubType,
              "CondenserType", "WaterSource",
              "SizeCategory", CapTotNetRtd )
          else
          if( CapTotNetRtd < 760000 )
          then // Water-source, <760 kBtu/hr
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", ZnSys:UnitaryCat,
              "SubCategory", ZnSys:SubType,
              "CondenserType", "WaterSource",
              "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
          else UNDEFINED
          endif endif
        else UNDEFINED
        endif endif endif endif endif
        endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else UNCHANGED
    endif 
  ANNUAL : T24N_2019
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then // Is baseline system
      if( BaseSysNum = 1 )
      then
        if( ZnSys:StdIsSEER2 > 0 )
        then UNDEFINED // Baseline uses SEER2/EER2
        else // Hotel/Motel or HRR, look up requirement
          UnitaryACHPMinEffReq:EER( "StandardsVersion", Proj:StdsVersion,
                                     "Category", ZnSys:UnitaryCat,
                                     "SubCategory", ZnSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", Min( CapTotNetRtd, 64999 ) ) 
        endif
      else
      if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then UNDEFINED // See DXEER for SEER -> EER conversion
      else // Look-up minimum efficiency based on baseline rated net capacity 
      if( IfValidAnd( AirSys:SubType = "CRAC" ) .OR. IfValidAnd( AirSys:UnitaryCat = "HP" ) )
      then // Look-up table UnitaryACHPMinEffReq includes 0.2 deduction for HP; CRAC has no heating
        UnitaryACHPMinEffReq:EER( "StandardsVersion", Proj:StdsVersion,
                                  "Category", AirSys:UnitaryCat,
                                  "SubCategory", AirSys:SubType,
                                  "SizeCategory", CapTotNetRtd )
      else // All other BaseSys w/ DX Clg have heating other than electric resistance heat
        UnitaryACHPMinEffReq:EER( "StandardsVersion", Proj:StdsVersion,
                                  "Category", AirSys:UnitaryCat,
                                  "SubCategory", AirSys:SubType,
                                  "SizeCategory", CapTotNetRtd ) - 0.2
      endif endif endif
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then // Is baseline system
      if( IfValidAnd( StdIsSEER2 > 0 ) )
      then // Baseline uses SEER2/EER2
        UNDEFINED
      else
      if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then UNDEFINED // See DXEER for SEER -> EER conversion
      else // Look-up minimum efficiency based on baseline rated net capacity 
      if( IfValidAnd( AirSys:SubType = "CRAC" ) .OR. IfValidAnd( AirSys:UnitaryCat = "HP" ) )
      then // Look-up table UnitaryACHPMinEffReq includes 0.2 deduction for HP; CRAC has no heating
        UnitaryACHPMinEffReq:EER( "StandardsVersion", Proj:StdsVersion,
                                  "Category", AirSys:UnitaryCat,
                                  "SubCategory", AirSys:SubType,
                                  "SizeCategory", CapTotNetRtd )
      else // All other BaseSys w/ DX Clg have heating other than electric resistance heat
        UnitaryACHPMinEffReq:EER( "StandardsVersion", Proj:StdsVersion,
                                  "Category", AirSys:UnitaryCat,
                                  "SubCategory", AirSys:SubType,
                                  "SizeCategory", CapTotNetRtd ) - 0.2
      endif endif endif
    else UNCHANGED
    endif
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( BaseSysNum = 1 )
      then // Is PTAC
        13.8 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
      else if( BaseSysNum = 2 )
      then // Is PTHP
        14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 ) 
      else if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then // Use SEER -> EER equation
        ( -0.0194 * DXSEER**2 ) + ( 1.0864 * DXSEER )
      else if( AirSys:BaseSysNum = 4 )
      then // Is SZHP, look-up minimum efficiency from 6.8.1B
        StdHPClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd)
      else if( AirSys:BaseSysNum = 3 )
      then // Is SZAC, look-up minimum efficiency from 6.8.1A, include 0.2 EER deduction
        StdDXClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd) - 0.2
      else // Look-up minimum efficiency from 6.8.1A, assuming no heating source
        StdDXClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd)
      endif endif endif endif endif
    else UNCHANGED
    endif
ENDRULE

// Determine code minimum IEER for systems with DX cooling coils
RULE NEW CoilClg:CodeMinIEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumIEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        // Cooling coil is in an AirSys it is subject to unitary equipment 
        // efficiency requirements of T24 Table 110.2-A and 110.2-B
        if(SysType = "SPVAC" .OR. SysType ="SPVHP")
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          UNDEFINED
        else     
        if( IfValidAnd( CndsrType = "Air" ) .AND.
            AirSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", "*",
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else UNDEFINED
        endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" ) 
      then // Part of an ZoneSystem
        if( SysType = "SPVAC" .OR. SysType ="SPVHP" )
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          UNDEFINED
        else 
        if( ( SysType = "SZAC" .OR. SysType = "SZHP"  .OR. SysType = "SZDFHP" .OR.
              SysType = "MiniSplitAC" .OR. SysType = "MiniSplitHP" ) .AND.
            ZnSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", "*",
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        else
        if( SysType = "WSHP" .AND. 
            ZnSys:UnitaryCat = "AC" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", "AC",
            "SubCategory", "*",
            "CondenserType", "WaterSource",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        else UNDEFINED
        endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else CodeMinIEER
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED // Not applicable to baseline
    else z:CodeMinIEER
    endif 
ENDRULE
// -----------------------------------------------------------------------------
// Determine code minimum EER of systems with separate condensing units (split systems only) 
RULE NEW CoilClg:CodeMinEERCndsr
  DATATYPE
    Float
  LONGFORM
    CodeMinEERCondenser
  DESCRIPTION
    "The efficiency of the direct expansion (DX) condensing unit 
     (compressor + condenser fan only) at AHRI rated conditions."
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. 
        ParentComp = "AirSys" .AND. 
        IfValidAnd( CapTotNetRtd > 0 ) )
      // If the cooling coil is in an AirSys with a remote condensing unit (split),
      // it is subject to also subject to the Condensing Unit efficiency 
      // requirements of T24 Table 110.2-A
    then
      if( AirSys:UnitaryCat = "AC" .AND. 
          AirSys:SubType = "Split3Phase" .AND.
          CapTotNetRtd >= 135000 )
      then
        // Condensing unit minimum EER applies
        switch ( CndsrType )
          case "Air"                 : 10.5
          case "EvaporativelyCooled" : 13.5
          case "WaterSource"         : 13.5
          default : UNCHANGED
        endswitch
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else CodeMinEERCndsr
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED // Not applicable to baseline
    else z:CodeMinEERCndsr
    endif 
ENDRULE

RULE NEW CoilClg:EERToEER2Rat
  DATATYPE
    Float
  LONGFORM
    EERToEER2Ratio
  DESCRIPTION
    "The ratio of EER2/EER for a given equipment type."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" )
    then // Part of an AirSystem
      if( AirSys:SEER2IsValid > 0 )
      then 0.96 // Assumption for all equipment
      else UNDEFINED
      endif
    else // ------------------------------------------------------------------
    if( ParentComp = "ZnSys" )
    then
      if( ZnSys:SEER2IsValid > 0 )
      then 0.96 // Assumption for all equipment
      else UNDEFINED
      endif
    else UNDEFINED
    endif endif
  ANNUAL : T24N_2019
    if( BaseSysNum = 1 .AND. IfValidAnd( CodeMinEER2 > 0 ) )
    then 0.96
    else UNCHANGED
    endif 
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( CodeMinEER2 > 0 ) )
      then 0.96 // Baseline uses SEER2/EER2
      else UNDEFINED
      endif
    else UNCHANGED
    endif 
ENDRULE

RULE NEW CoilClg:ShowEER
  DATATYPE
    Integer
  LONGFORM
    ShowEER
  DESCRIPTION
    "For managing screens."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( CodeMinEER > 0 ) .OR. IfValidAnd( CodeMinSEER > 0 ) ) 
    then 1
    else 0
    endif
ENDRULE


// ********** Direct Expansion Cooling Efficiencies **********************
RULE CoilClg:DXSEER2
  DESCRIPTION
    "The Seasonal Energy Efficiency Ratio (SEER2) is a term used to describe the 
     seasonal performance of a DX cooling system. It is determined in accordance with 
     AHRI standards." 
  INPUTCLASS 
   CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    20
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 .AND. IfValidAnd( CodeMinSEER2 > 0 ) )
    then // For PROPOSED AutoEffInput only 
      CodeMinSEER2
    else UNDEFINED
    endif
  REPORTPRECISION
    1
  CHECKCODE
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. 
        IfValidAnd( DXSEER2 > 0 ) = 0 )
    then PostError("DX coil '%s' must be specified with AHRI rated SEER2", Name)
    else 
    if( IfValidAnd( DXSEER2 < CodeMinSEER2 ) )
    then PostError("SEER2 of coil '%s' is less than the code minimum required efficiency 
                    of SEER2-%.1f. Select the bypass of efficiency check at the coil
                    if this is not applicable.", Name, CodeMinSEER2)
    else UNCHANGED 
    endif endif endif
  SIZING
    if( BaseSysNum > 0 .OR. Type != "DirectExpansion" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else if( IfValidAnd( CodeMinSEER2 > 0 ) ) 
    then // User-equipment is SEER2-rated
      DXSEER2
    else UNDEFINED // Purge from model if not used
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system
      if( IfValidAnd( CodeMinSEER2 > 0 ) )
      then CodeMinSEER2
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilClg:DXSEER
  DESCRIPTION
    "The Seasonal Energy Efficiency Ratio (SEER) is a term used to describe the 
     seasonal performance of a DX cooling system. It is determined in accordance with 
     AHRI standards." 
  HELP
    "This is a required input for air-source DX cooling coils in packaged/split
     units where EER is not entered and the net capacity is <65  MBH."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
   CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    20
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 ) 
    then  // For PROPOSED AutoEffInput only 
      if( IfValidAnd( CodeMinSEER > 0 ) )
      then // Is SEER rated
        CodeMinSEER
      else 
      if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. IfValidAnd( SEERToSEER2Rat > 0 ) )
      then // Is SEER2 rated, calculate SEER for EER calc
        CodeMinSEER2 / SEERToSEER2Rat
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  REPORTPRECISION
    1
  CHECKCODE
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinSEER > 0 ) .AND. 
        IfValidAnd( DXSEER > 0 ) = 0 )
    then PostError("DX coil '%s' must be specified with AHRI rated SEER", Name)
    else 
    if( IfValidAnd( DXSEER < CodeMinSEER ) )
    then PostError("SEER of coil '%s' is less than the code minimum required efficiency 
                    of SEER-%.1f. Select the bypass of efficiency check at the coil
                    if this is not applicable.", Name, CodeMinSEER)
    else UNCHANGED 
    endif endif endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else
    if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. IfValidAnd( SEERToSEER2Rat > 0 ) )
    then // User-equipment is SEER2-rated
      DXSEER2 / SEERToSEER2Rat
    else
    if( IfValidAnd( CodeMinSEER > 0 ) ) 
    then // User-equipment is SEER-rated
      DXSEER
    else UNDEFINED // Purge from model if not used
    endif endif endif
  ANNUAL : T24N_2016
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system 
      if( IfValidAnd( CapTotNetRtd < 65000 ) )
      then 13.0 // Baseline is assumed 3Phase
      else UNDEFINED // Not used if net capacity >= 65 MBH
      endif
    else z:DXSEER
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system
      if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. IfValidAnd( SEERToSEER2Rat > 0 ) )
      then // Baseline uses SEER2
        CodeMinSEER2 / SEERToSEER2Rat
      else
      if( IfValidAnd( CodeMinSEER > 0 ) )
      then CodeMinSEER
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Rounded DX SEER 
RULE NEW CoilClg:DXSEERRnd
  DATATYPE
    Float
  LONGFORM
    DXSEERRound
  DESCRIPTION
    "Rounded DX SEER, based on AHRI test procedure."
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  DEFAULT
    if( IfValidAnd( EERSEERRndFact > 0 ) .AND.
        IfValidAnd( DXSEER > 0 ) )
    then
      int( ( DXSEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
  SIZING
    if( IfValidAnd( EERSEERRndFact > 0 ) .AND.
        IfValidAnd( DXSEER > 0 ) )
    then
      int( ( DXSEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
  ANNUAL
    if( IfValidAnd( EERSEERRndFact > 0 ) .AND.
        IfValidAnd( DXSEER > 0 ) )
    then
      int( ( DXSEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
ENDRULE

RULE CoilClg:DXEER2
  DESCRIPTION
    "The Energy Efficiency Ratio (EER2) is a term used to describe the 
     seasonal performance of a DX cooling system. It is determined in accordance with 
     AHRI standards." 
  INPUTCLASS 
   CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    20
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 )
    then // For PROPOSED AutoEffInput only 
      if( IfValidAnd( CodeMinEER2 > 0 ) )
      then // Has a code minimum EER2
        CodeMinEER2
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  REPORTPRECISION
    1
  CHECKCODE
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinEER2 > 0 ) .AND. IfValidAnd( DXEER2 > 0 ) = 0 )
    then
      PostError("DX coil '%s' must be specified with AHRI rated EER2", Name)
    else 
    if( IfValidAnd( CodeMinEER2 > 0 ) .AND. 
        IfValidAnd( DXEER2 < CodeMinEER2 ) )
    then
      PostError("EER2 of coil '%s' is less than the code minimum required efficiency 
                 of EER2-%.1f. Select the bypass of efficiency check at the coil
                 if this is not applicable.", Name, CodeMinEER2)
    else UNCHANGED 
    endif endif endif
  SIZING
    if( BaseSysNum > 0 .OR. Type != "DirectExpansion" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else
    if( IfValidAnd( CodeMinEER2 > 0 ) .OR. IfValidAnd( CodeMinSEER2 > 0 ) )       
    then // User-equipment is EER2-rated
      DXEER2
    else UNDEFINED // Purge from model if not used
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system
      if( IfValidAnd( CodeMinEER2 > 0 ) )
      then CodeMinEER2
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE
// -----------------------------------------------------------------------------
// DX EER 
RULE NEW CoilClg:UserDefEER
  DATATYPE
    Float
  LONGFORM
    UserDefinedEER
  DESCRIPTION
    "The value of user entered EER or EER2 for a SEER/SEER2-rated DX cooling coil."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "DirectExpansion" )
    then 
      if( IfValidAnd( CodeMinSEER > 0 ) .AND. IfValidAnd( DXEER > 0 ) )
      then DXEER // SEER rated and user has entered EER
      else 
      if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. IfValidAnd( DXEER2 > 0 ) )
      then DXEER2 // SEER2 rated and user has entered EER2
      else 0
      endif endif
   else 0
   endif 
ENDRULE

RULE CoilClg:DXEER
  DESCRIPTION
    "The efficiency of a direct expansion (DX) cooling system at AHRI rated 
     conditions."
  HELP
    "This is a required input for DX cooling coils in PTAC/PTHP, WSHP, and other
     packaged/split units >= 65  MBH cooling capacity."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    18  
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" )
    then
      if( Proj:AutoEffInput = 1 )
      then // For PROPOSED AutoEffInput only
        if( IfValidAnd( CodeMinEER > 0 ) )
        then // Default to CodeMinEER
          CodeMinEER
        else UNDEFINED
        endif
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( ( IfValidAnd( CodeMinEER > 0 ) .OR. IfValidAnd( CodeMinEERCndsr > 0 ) )
        .AND. IfValidAnd( DXEER > 0 ) = 0 )
    then 
       PostError("DX coil '%s' must be specified with AHRI rated EER", Name)
    else
    if( IfValidAnd( CodeMinEER > 0 ) .AND. 
        IfValidAnd( DXEER < CodeMinEER ) )
    then 
      PostError("Air conditioner efficiency (EER) of coil '%s' is less than the code 
                 minimum required efficiency of EER-%.1f. Select the bypass of
                 efficiency check at the coil if this is not applicable.", 
                 Name, CodeMinEER)
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
      if( IfValidAnd( CodeMinEER > 0 ) .AND. 
          IfValidAnd( DXEER > 0 ) = 0 )
      then 
         PostError("DX coil ''%s' must be specified with AHRI rated EER", Name)
      else UNCHANGED 
      endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else 
    if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. 
        IfValidAnd( DXSEERRnd > 0 ) .AND.
        IfValidAnd( DXEER2 > 0 ) = 0 )
    then // User has selected SEER2 but not entered EER2, use default equation to calculate EER 
      Min( 13, ( -0.0194 * DXSEERRnd**2 ) + ( 1.0864 * DXSEERRnd ) )
    else 
    if( IfValidAnd( CodeMinSEER > 0 ) .AND. 
        IfValidAnd( DXSEERRnd > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) = 0 )   
    then // User has selected SEER but not entered EER, use default equation to calculate EER 
      Min( 13, ( -0.0194 * DXSEERRnd**2 ) + ( 1.0864 * DXSEERRnd ) )
    else 
    if( IfValidAnd( CodeMinSEER2 > 0 ) .AND. 
        IfValidAnd( DXEER2 > 0 ) .AND.
        IfValidAnd( EERToEER2Rat > 0 ) )   
    then // User has selected SEER2 and entered EER2, use ratio to calculate EER 
       DXEER2 / EERToEER2Rat
    else // Use user-specified EER
      UNCHANGED
    endif endif endif endif
  ANNUAL : T24N_2016
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then // Use SEER -> EER equation
        ( -0.0194 * DXSEERRnd**2 ) + ( 1.0864 * DXSEERRnd )
      else // Look-up minimum efficiency based on baseline rated net capacity 
        StdDXClgEfficiencyT24N:EER("SizeCategory", CapTotNetRtd)
      endif
    else z:DXEER
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system
      if( IfValidAnd( CodeMinEER > 0 ) )
      then // Baseline uses EER
        CodeMinEER
      else
      if( IfValidAnd( CodeMinEER2 > 0 ) .AND. IfValidAnd( EERToEER2Rat > 0 ) )
      then // Baseline uses EER2
        CodeMinEER2 / EERToEER2Rat
      else
      if( IfValidAnd( CodeMinSEER > 0 ) .OR. IfValidAnd( CodeMinSEER2 > 0 ) )
      then // Baseline is SEER or SEER2-rated, use SEER -> EER equation
        ( -0.0194 * DXSEERRnd**2 ) + ( 1.0864 * DXSEERRnd )
      else
      if( IfValidAnd( CodeMinEER > 0 ) )
      then CodeMinEER
      else UNDEFINED
      endif endif endif endif
    else UNCHANGED
    endif
ENDRULE
// DX EER Round
RULE NEW CoilClg:DXEERRnd
  DATATYPE
    Float
  LONGFORM
    DXEERRound
  DESCRIPTION
    "Rounded DX EER, based on AHRI test procedure."
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  DEFAULT
    if( IfValidAnd( EERSEERRndFact > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) )
    then
      int( ( DXEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
  SIZING
    if( IfValidAnd( EERSEERRndFact > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) )
    then
      int( ( DXEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
  ANNUAL
    if( IfValidAnd( EERSEERRndFact > 0 )  .AND.
        IfValidAnd( DXEER > 0 ) )
    then // When DXEER and EERSEERRndFact are defined
      int( ( DXEER / EERSEERRndFact ) + 0.5 ) * EERSEERRndFact
    else
      UNDEFINED
    endif
ENDRULE
// DX IEER 
RULE CoilClg:DXIEER
  DESCRIPTION
    "The integrated part-load efficiency of a direct expansion (DX) cooling system at AHRI rated 
     conditions."
  HELP
    "This is a required input for air- and water-source DX cooling coils in systems with >= 65MBH
     cooling capacity."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    18  
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" )
    then
      if( Proj:AutoEffInput = 1 .AND. IfValidAnd( CodeMinIEER > 0 ) )
      then // For PROPOSED AutoEffInput only
        CodeMinIEER
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinIEER > 0 ) .AND. 
        IfValidAnd( DXIEER > 0 ) = 0 )
    then 
       PostError("DX coil '%s' must be specified with AHRI rated IEER", Name)
    else
    if( IfValidAnd( DXIEER < CodeMinIEER ) )
    then 
      PostError("Air conditioner integrated efficiency (IEER) of coil '%s' is less than the code 
                 minimum required efficiency of IEER-%.1f. Select the bypass of
                 efficiency check at the coil if this is not applicable.", 
                 Name, CodeMinIEER)
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    if( IfValidAnd( CodeMinIEER > 0 ) .AND. 
        IfValidAnd( DXIEER > 0 ) = 0 )
    then 
       PostError("DX coil ''%s' must be specified with AHRI rated IEER", Name)
    else UNCHANGED 
    endif
  SIZING
    if( BaseSysNum > 0 .OR. Type != "DirectExpansion" )
    then
      // Not used for non-DX coils or ZnSys DX coils, and 
      // AHRI rated EER unknown for baseline systems 
      UNDEFINED
    else // Use user-specified EER
      DXIEER
    endif
  ANNUAL
    z:DXIEER
ENDRULE

// -----------------------------------------------------------------------------
// T24N_2016 DX cooling efficiency
TABLE StdDXClgEfficiencyT24N
// *** EER values INCLUDE 0.2 deduction indicated in Table 110.2-A footnote b: for
// units with a heating section other than electric resistance heat.
    SizeCategory    EER    IEER
//  <65000          Defined in DXEER rule
    <135000         11.0   11.4
    <240000         10.8   11.2
    <760000         9.8    10.1
    >=760000        9.5    9.8
ENDTABLE

// -----------------------------------------------------------------------------
// S901G DX cooling efficiency
// Table 6.8.1A
TABLE StdDXClgEfficiencyS901G
// EERs DO NOT include 0.2 deduction from required EERs for units with a heating
    SizeCategory    EER    
//  <65000          Defined in DXEER rule
    <135000         11.2   
    <240000         11.0   
    <760000         10.0 
    >=760000        9.7  
ENDTABLE

// S901G HP cooling efficiency
// Table 6.8.1B
TABLE StdHPClgEfficiencyS901G
// *** EER values INCLUDE 0.2 deduction from required EERs for units with a heating section 
// other than electric resistance heat.
    SizeCategory    EER    
//  <65000          Defined in DXEER rule
    <135000         10.8 
    <240000         10.4   
    >=240000        9.3  
ENDTABLE   

// ********** Direct Expansion Cooling Efficiencies ******************************
// -----------------------------------------------------------------------------
// Adjust rated EER to remove fan heat at AHRI rating condtion
RULE CoilClg:DXEERAdj
  DESCRIPTION
    "The cooling efficiency of a direct expansion (DX) cooling system at AHRI 
     rated conditions, adjusted to remove fan energy and pump energy.


      DXEERAdj = ( Rated net capacity (Btu/hr) + Rated fan heat (Btu/hr) ) /
                 ( Rated input power (W) - rated fan power (W) - rated pump power (W) )"
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h-W
  DEFAULT
    // Calculated in DEFAULT rules for CHECKCODE rule 
    if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      CapTotGrossRtd /  
      ( ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    // Check Table 110.2-A minimum efficiency for condensing units 
    // Condensing unit EER = EERAdj
    if( IfValidAnd( DXEERAdj < CodeMinEERCndsr ) )
    then 
      PostError("Condensing unit efficiency of coil '%s' is less than 
                 the code minimum required EER-%.1f. Select the bypass of
                 efficiency check at the coil if this is not applicable.", 
                 Name, CodeMinEERCndsr)
    else UNCHANGED 
    endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    // Condensing unit EER = EERAdj
    if( IfValidAnd( CodeMinEERCndsr > 0 ) .AND. 
        ParentComponentType() != "ZnSys" )
      then
      if( LocalStatus( DXEERAdj ) = 0 )
      then
        PostError("DX coil '%s' must be specified with AHRI rated EER", Name)
      else UNCHANGED 
      endif   
    else UNCHANGED
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
      // Not used for non-DX coils and 
      // AHRI rated EER unknown for baseline systems 
    then UNDEFINED
    else
    if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
        IfValidAnd( DXEERRnd > 0 ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      // DXEERAdj = ( Net capacity (Btu/hr) + Fan heat (Btu/hr) ) /
      //            ( Rated power (W) - fan power (W) - pump power (W) )
      // where the fan heat/power is the power associated with overcoming the
      // internal static and coil pressure drop ( for water-source equipment )
      // ( CapTotNetRtd + FanHtRtd ) /
      // ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
      // CapTotGrossRtd = CapTotNetRtd + FanHtRtd
      CapTotGrossRtd /  
      ( ( CapTotNetRtd / DXEERRnd ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
    else 0
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
          IfValidAnd( DXEERRnd > 0 ) .AND.
          IfValidAnd( CapTotNetRtd > 0 ) )
      then
        CapTotGrossRtd / ; CapTotGrossRtd = CapTotNetRtd + FanHtRtd
        ( ( CapTotNetRtd / DXEERRnd ) - ( FanHtRtd / 3.412 ) )
      else 0
      endif
    else z:DXEERAdj
    endif  
ENDRULE
 
// -----------------------------------------------------------------------------
// Convert EERAdj to EIR for simulation purposes
RULE CoilClg:DXEIR
  DESCRIPTION
    "The cooling efficiency of a direct expansion (DX) cooling system, described
     for simulation purposes.  Enery Input Ratio (EIR) is the inverse of the COP."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    NotInput
  SIZING
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then 1 / 3.0  // Default cooling efficiency assumed for baseline sizing run
    else if( IfValidAnd( DXEERAdj > 0 ) )
    then 1 / ( DXEERAdj / 3.412 )
//    then
//      if( ParentComp = "AirSys" )
//      then 
//        if( IfValidAnd( Parent2Valid( DuctLeakage ) > 0 ) )
//       then // For now, increase EIR by 6.5% if DuctLeakage > 0
//          1 / ( DXEERAdj / 3.412 ) * 1.065       
//        else // No adjustment
//          1 / ( DXEERAdj / 3.412 )
//        endif
//      else if( ParentComp = "ZnSys" )
//      then 
//        if( IfValidAnd( ParentValid( DuctLeakage ) > 0 ) )
//       then // For now, increase EIR by 6.5% if DuctLeakage > 0
//          1 / ( DXEERAdj / 3.412 ) * 1.065       
//        else // No adjustment
//          1 / ( DXEERAdj / 3.412 )
//        endif
//      else 1 / ( DXEERAdj / 3.412 )
//      endif endif
    else UNDEFINED
    endif endif
  ANNUAL 
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( DXEERAdj > 0 ) )
      then 1 / ( DXEERAdj / 3.412 )
      else UNDEFINED
      endif 
    else z:DXEIR
    endif
ENDRULE

// -----------------------------------------------------------------------------
// ********** Direct Expansion Cooling Efficiency Temperature Adjustment Curve *
RULE CoilClg:DXEIR_fTempCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil and condenser conditions."
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS
    Prescribed  
  SIZING
    if( Type = "DirectExpansion" ) 
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) )
      then UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      // RuleLibrary(QuadplLin, "CoilClgWSDXEIRRatio_fTwbRatioTwtRatioCFMRatioGPMRatioSI")
      else
      if( ServesResZn > 0 )
      then // Use IP curve for CSE
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbIP") 
      else
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else
      if( IfValidAnd( CapTotNetRtd < 65000 ) .AND. 
         ( IfValidAnd( CodeMinSEER > 0 ) .OR. IfValidAnd( CodeMinSEER2 > 0 ) ) )
      then // Assign curve map object. Dependent values calculated based on EER/SEER in SEEREIR_fTCurve.rule
        RuleLibrary(CrvMapDblVar, "CoilClgDXSEEREIR_fTwbToadbSI", 1)
        // Old rule assigning 6 discrete curves based on EER/SEER
        ;if( IfValidAnd( DXEER >= 13 ) )
        ;then // EER >= 13, use EER13 set of curves
        ;  if( IfValidAnd( DXSEER >= 20 ) )
        ;  then // SEER >= 20
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER20EIRRatio_fTwbToadbSI")
        ;  else if( IfValidAnd( DXSEER >= 18 ) )
        ;  then // SEER >= 18
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER18EIRRatio_fTwbToadbSI")
        ;  else // SEER < 18
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER16EIRRatio_fTwbToadbSI")
        ;  endif endif
        ;// EER < 13, use EER11 set of curves
        ;else if( IfValidAnd( DXSEER >= 17 ) )
        ;then // SEER >= 17
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER17EIRRatio_fTwbToadbSI")
        ;else if( IfValidAnd( DXSEER >= 15 ) )
        ;then // SEER >= 15
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER15EIRRatio_fTwbToadbSI")
        ;else // SEER < 15
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER13EIRRatio_fTwbToadbSI")
        ;endif endif endif
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N_2016
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else if( CapTotNetRtd < 65000 )
      then // Assign curve map object for standard design SEER-13.0/EER-10.846
        RuleLibrary(CrvMapDblVar, "CoilClgDXStdSEER13EIR_fTwbToadbSI")
        // Old rule assigning 6 discrete curves based on EER/SEER
        ;if( IfValidAnd( DXEER >= 13 ) )
        ;then // EER >= 13, use EER13 set of curves
        ;  if( IfValidAnd( DXSEER >= 20 ) )
        ;  then // SEER >= 20
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER20EIRRatio_fTwbToadbSI")
        ;  else if( IfValidAnd( DXSEER >= 18 ) )
        ;  then // SEER >= 18
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER18EIRRatio_fTwbToadbSI")
        ;  else // SEER < 18
        ;    RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER16EIRRatio_fTwbToadbSI")
        ;  endif endif
        ;// EER < 13, use EER11 set of curves
        ;else if( IfValidAnd( DXSEER >= 17 ) )
        ;then // SEER >= 17
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER17EIRRatio_fTwbToadbSI")
        ;else if( IfValidAnd( DXSEER >= 15 ) )
        ;then // SEER >= 15
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER15EIRRatio_fTwbToadbSI")
        ;else // SEER < 15
        ;  RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER13EIRRatio_fTwbToadbSI")
        ;endif endif endif
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif
    else if( Type = "DirectExpansion" )
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) )
      then UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      //RuleLibrary(QuadplLin, "CoilClgWSDXEIRRatio_fTwbRatioTwtRatioCFMRatioGPMRatioSI")
      else z:DXEIR_fTempCrvRef
      endif
    else UNDEFINED
    endif endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) )
      then UNDEFINED
      else
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else
      if( ServesResZn > 0 )
      then // Use IP curve for systems simulated in CSE (AirSys, BaseSysNum = 201
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbIP") 
      else
      if( LocalCompAssigned( DXEIR_fTempCrvRef ) = ComponentType( "CrvDblQuad" ) .AND.
          IfValidAnd( CapTotNetRtd < 65000 ) .AND. 
          ( IfValidAnd( CodeMinSEER > 0 ) .OR. IfValidAnd( CodeMinSEER2 > 0 ) ) )
      then // Is SEER rated, update to SEER curve map. Dependent values calculated based on EER/SEER in SEEREIR_fTCurve.rule
        RuleLibrary(CrvMapDblVar, "CoilClgDXSEEREIR_fTwbToadbSI", 1)
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif endif endif
    else UNCHANGED
    endif
  ANNUAL : S901G
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else
      if( CapTotNetRtd < 65000 )
      then // Assign curve map object for standard design SEER-13.0/EER-10.846
        RuleLibrary(CrvMapDblVar, "CoilClgDXStdSEER13EIR_fTwbToadbSI")
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif
    else
    if( Type = "DirectExpansion" )
    then
      if( CndsrType = "WaterSource" )
      then UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      //RuleLibrary(QuadplLin, "CoilClgWSDXEIRRatio_fTwbRatioTwtRatioCFMRatioGPMRatioSI")
      else UNCHANGED
      endif
    else UNDEFINED
    endif endif
ENDRULE
// Reset DXEIR_fTempCrvRef if system is SPVAC/HP without Economizer
RULE CoilClg:DXEIR_fTempCrvRef
  SIZING
    if( Type = "DirectExpansion" .AND. ParentComp = "AirSys" )
    then
      if( ( SysType = "SPVAC" .OR. SysType = "SPVHP" ) .AND.
          AirSys:HasAirEcono = 0 )
      then // AirSys SPV does not have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI" )
      else UNCHANGED
      endif
    else
    if( Type = "DirectExpansion" .AND. ParentComp = "ZnSys" )
    then
      if( SysType = "SPVAC" .OR. SysType = "SPVHP" )
      then // ZnSys SPV cannot have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI" )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
  ANNUAL
    UNCHANGED // Baseline does not use SPVAC/HP
ENDRULE


// -----------------------------------------------------------------------------
// ********** Direct Expansion Part-Load Efficiency Adjustment Curve ***********
RULE CoilClg:DXEIR_fPLFCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     part-load factor.  This curve type is specific to EnergyPlus."
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "DirectExpansion" ) 
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) )
      then UNDEFINED 
      else
      if( ServesResZn > 0 )
      then UNDEFINED // Not used for CSE, see DXEIR_fPLRCrvRef below
      else
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvCubic, "CoilClgPTACEIRRatio_fQFrac")
      else
      if( IfValidAnd( LocalValid( CapTotNetRtd ) < 65000 ) ) then
        RuleLibrary(CrvCubic, "CoilClgDXSEEREIRRatio_fQFrac") 
      else RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fQFrac") 
      endif endif endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) )
      then UNDEFINED
      else
      if( ServesResZn > 0 )
      then UNDEFINED // Not used for CSE, see DXEIR_fPLRCrvRef below
      else
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvCubic, "CoilClgPTACEIRRatio_fQFrac")
      else
      if( IfValidAnd( LocalValid( CapTotNetRtd ) < 65000 ) )
      then
        RuleLibrary(CrvCubic, "CoilClgDXSEEREIRRatio_fQFrac") 
      else
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fQFrac") 
      endif endif endif endif
    else
    if( Type = "DirectExpansion" .AND. CndsrType = "WaterSource" )
    then
      UNDEFINED 
    else z:DXEIR_fPLFCrvRef
    endif endif
ENDRULE
// Reset DXEIR_fPLFCrvRef if system is Single Packaged Unit without Economizer
RULE CoilClg:DXEIR_fPLFCrvRef
  SIZING
    if( Type = "DirectExpansion" .AND. ParentComp = "AirSys" )
    then
      if( ( SysType = "SPVAC" .OR. SysType = "SPVHP" ) .AND.
          AirSys:HasAirEcono = 0 )
      then // AirSys SPV does not have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fQFrac" )
      else UNCHANGED
      endif
    else
    if( Type = "DirectExpansion" .AND. ParentComp = "ZnSys" )
    then
      if( SysType = "SPVAC" .OR. SysType = "SPVHP" )
      then // ZnSys SPV cannot have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fQFrac" )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
  ANNUAL
    UNCHANGED // Baseline does not use SPVAC/HP
ENDRULE
// For CSE
RULE CoilClg:DXEIR_fPLRCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     part-load ratio.  This curve type is specific to CSE."
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "DirectExpansion" .AND. ServesResZn > 0 )
    then // Ise IP curve for CSE
      RuleLibrary(CrvCubic, "CoilClgDXEIRRatio_fQRatio") 
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" .AND. ServesResZn > 0 )
    then RuleLibrary(CrvCubic, "CoilClgDXEIRRatio_fQRatio") 
    else UNCHANGED
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Cooling efficiency as a function of indoor coil coil flow.  Applicable to EnergyPlus
RULE CoilClg:DXEIR_fFlowCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil flow. This curve type is specific to EnergyPlus."
  INPUTCLASS
    Prescribed  
  SIZING
    if( Type = "DirectExpansion" )
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) .OR. ServesResZn > 0 )
      then UNDEFINED 
      else
      if( IfValidAnd( NumClgStages > 1 ) )
      then RuleLibrary(CrvCubic, "CoilClgDXDblEIRRatio_fCFMRatio")
      else RuleLibrary(CrvQuad, "CoilClgDXSnglEIRRatio_fCFMRatio")
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( CndsrType = "WaterSource" ) .OR. ServesResZn > 0 )
      then UNDEFINED
      else 
      if( IfValidAnd( NumClgStages > 1 ) )
      then RuleLibrary(CrvCubic, "CoilClgDXDblEIRRatio_fCFMRatio")
      else RuleLibrary(CrvQuad, "CoilClgDXSnglEIRRatio_fCFMRatio")
      endif endif
    else UNCHANGED
    endif
ENDRULE


// ********** Crankcase Heater Capacity ****************************************
RULE CoilClg:DXCrankcaseHtrCap
  DESCRIPTION
    "The capacity of the electric resistance crankcase heater."
  INPUTCLASS 
    Optional
  UNITS 
    Btu/hr
  DEFAULT
    if( Type = "DirectExpansion" .AND. 
        IfValidAnd( ServesResZn > 0 ) .AND.
        IfValidAnd( CndsrType = "Air" ) )
    then 40 * 3.412 // 40 Watts per BW
    else 0
    endif
  ANNUAL
    if( Type = "DirectExpansion" )
    then 
      if( BaseSysNum > 0 .AND. ServesResZn > 0 )
      then 40 * 3.412 // 40 Watts per BW
      else UNCHANGED
      endif
    else 0
    endif
ENDRULE

// ********** Crankcase Heater Shutoff Temperature ****************************
RULE CoilClg:DXCrankcaseCtrlTemp
  DESCRIPTION
    "The outdoor air dry-bulb temperature above which the crank case heater is 
     not permitted to operate."
  HELP
    "Only applicable if Type = 'DirectExpansion'"
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  COMMONMINIMUM
    32
  COMMONMAXIMUM
    50 
  UNITS 
    F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( DXCrankcaseHtrCap > 0 ) )
    then 50
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "DirectExpansion" .AND. IfValidAnd( DXCrankcaseHtrCap > 0 ) )
    then 
      if( BaseSysNum > 0 .AND. ServesResZn > 0 )
      then 50 // 50F per BW
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilClg:DXCrankcaseHtrCapSim
  DESCRIPTION
    "The capacity of the electric resistance crankcase heater, for simulation."
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/hr
  ANNUAL
    if( IfValidAnd( DXCrankcaseHtrCap > 0 ) )
    then DXCrankcaseHtrCap * SysCnt
    else 0
    endif
ENDRULE


// ********** Heat Rejection for Water-cooled Condenser ************************
RULE NEW CoilClg:HtRejLdSim
  DATATYPE
    Float
  LONGFORM
    HeatRejectionLoadSimulated
  DESCRIPTION
    "The amount of heat rejected by the WSHP cooling coil at the peak rated output."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CndsrType = "WaterSource" ) .AND.
        CapTotGrossRtdSim > 0 )
    then CapTotGrossRtdSim * ( 1 + ( 3.412 / ValidOr( DXEERAdj, 12 ) ) )
    else 0
    endif
ENDRULE


// added simply to handle backward compatibility for now
RULE CoilClg:AuxPwr
//  DESCRIPTION
//    "The ..."
//  REFERENCE 
//    NACM Section ...
  PREVIOUSNAMES
    AuxilliaryPower
ENDRULE
