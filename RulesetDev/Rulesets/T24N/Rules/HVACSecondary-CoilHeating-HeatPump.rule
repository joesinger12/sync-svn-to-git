// HVAC Secondary Systems - Heating Coils - Heat Pumps
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:
//  Section 5.7.6.5 - Electric Heat Pumps
//      Electric Heat Pump Heating Efficiency
//      Electric Heat Pump Heating Efficiency Adjustment Curve




// ---------- Section 5.7.6.5 - Electric Heat Pump -----------------------------

// ********** Electric Heat Pump Heating Efficiency (HSPF) *********************
RULE NEW CoilHtg:HSPFCOPRndFact
  DATATYPE
    Float
  LONGFORM
    HSPFCOPRoundingFactor
  DESCRIPTION
    "The smallest factor of the rounded direct expansion (DX) heating coil 
     efficiency per the applicable AHRI testing standard."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "HeatPump" )
    then
      if( ParentComp = "ZnSys" )
      then // Part of an ZoneSystem  
        if ( SysType = "PTHP" )
        then 0.1
        else if ( SysType = "SPVHP" )
        then 0.02
        else if ( SysType = "WSHP" )
        then 0.05
        else if ( SysType = "VRF" )
        then UNDEFINED // RndFact defined in VRFSys.rule
        else if( ZnSys:SubType = "CRAC" )
        then 0.01
        else if ( IfValidAnd( ParentValid( ClgCap ) < 65000 ) )
        then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65,000
          if( ZnSys:SubType = "Packaged1Phase" .OR.
              ZnSys:SubType = "Split1Phase" )
          then 0.025
          else 0.05
          endif
        else  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65,000
          0.01
        endif endif endif endif endif endif
      else if( ParentComp = "AirSys" )
      then // Part of an AirSystem  
        if ( SysType = "SPVHP" )
        then 0.02
        else if( AirSys:SubType = "CRAC" )
        then 0.01
        else if ( IfValidAnd( Parent2Valid( ClgCap ) < 65000 ) )
        then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65,000
          if( AirSys:SubType = "Packaged1Phase" .OR.
              AirSys:SubType = "Split1Phase" )
          then 0.025
          else 0.05
          endif
        else  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65,000
          0.01
        endif endif endif
      else  // catch all
        0.01
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "HeatPump" )
    then
      if( ParentComp = "ZnSys" )
      then // Part of an ZoneSystem  
        if ( SysType = "PTHP" )
        then 0.1
        else if ( SysType = "SPVHP" )
        then 0.02
        else if ( SysType = "WSHP" )
        then 0.05
        else if ( SysType = "VRF" )
        then UNDEFINED // RndFact defined in VRFSys.rule
        else if( ZnSys:SubType = "CRAC" )
        then 0.01
        else if ( IfValidAnd( ParentValid( ClgCap ) < 65000 ) )
        then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65,000
          if( ZnSys:SubType = "Packaged1Phase" .OR.
              ZnSys:SubType = "Split1Phase" )
          then 0.025
          else 0.05
          endif
        else  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65,000
          0.01
        endif endif endif endif endif endif
      else if( ParentComp = "AirSys" )
      then // Part of an AirSystem  
        if ( SysType = "SPVHP" )
        then 0.02
        else if( AirSys:SubType = "CRAC" )
        then 0.01
        else if ( IfValidAnd( Parent2Valid( ClgCap ) < 65000 ) )
        then  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, < 65,000
          if( AirSys:SubType = "Packaged1Phase" .OR.
              AirSys:SubType = "Split1Phase" )
          then 0.025
          else 0.05
          endif
        else  // PVAV, SZAC, SZHP, SZVAVAC, SZVZVHP, MiniSplitHP, MiniSplitAC, >= 65,000
          0.01
        endif endif endif
      else  // catch all
        0.01
      endif endif
    else UNDEFINED
    endif        
ENDRULE
// -----------------------------------------------------------------------------
RULE NEW CoilHtg:HSPFCOPRndScl
  DATATYPE
    Integer
  LONGFORM
    HSPFCOPRoundingScale
  DESCRIPTION
    "The scale (number of digits to the right of the decimal place) in the
     rounded direct expansion (DX) heating coil efficiency per the applicable 
     AHRI testing standard."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( HSPFCOPRndFact = 0.1 ) )
    then 1
    else
    if( IfValidAnd( HSPFCOPRndFact = 0.01 ) .OR.
        IfValidAnd( HSPFCOPRndFact = 0.02 ) .OR. 
        IfValidAnd( HSPFCOPRndFact = 0.05 ) )
    then 2
    else
    if( IfValidAnd( HSPFCOPRndFact = 0.025 ) )
    then 3
    else UNDEFINED
    endif endif endif
  ANNUAL
    if( IfValidAnd( HSPFCOPRndFact = 0.1 ) )
    then 1
    else
    if( IfValidAnd( HSPFCOPRndFact = 0.01 ) .OR.
        IfValidAnd( HSPFCOPRndFact = 0.02 ) .OR. 
        IfValidAnd( HSPFCOPRndFact = 0.05 ) )
    then 2
    else
    if( IfValidAnd( HSPFCOPRndFact = 0.025 ) )
    then 3
    else UNDEFINED
    endif endif endif
ENDRULE

// ********** Electric Heat Pump Heating Minimum Efficiencies *********************
// CodeMin for HeatPump HSPF
RULE NEW CoilHtg:CodeMinHSPF
  DATATYPE
    Float
  LONGFORM
    CodeMinimumHSPF
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( SysClgCap > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. AirSys:HSPF2IsValid > 0 )
        then UNDEFINED
        else
        if(  SysType ="SPVHP" )
        then // efficiency for SPVHP is described in terms of COP
          UNDEFINED
        else
        if( IfValidAnd( AirSys:UnitaryCat = "HP" ) .AND.
            IfValidAnd( SysClgCap < 65000 ) .AND. 
            IfValidAnd( AirSys:SubType != "NA" ) )
        then
          UnitaryACHPMinEffReq:HSPF( "StandardsVersion", Proj:StdsVersion,
                                     "Category", "HP",
                                     "SubCategory", AirSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", SysClgCap )
        else UNDEFINED
        endif endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // Part of a ZoneSystem
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. ZnSys:HSPF2IsValid > 0 )
        then UNDEFINED
        else
        if( SysType ="SPVHP" )
        then // efficiency for SPVHP is described in terms of COP
          UNDEFINED
        else
        if( IfValidAnd( ZnSys:UnitaryCat = "HP" ) .AND.
            IfValidAnd( SysClgCap < 65000 ) .AND. 
            IfValidAnd( ZnSys:SubType != "NA" ) )
        then
          UnitaryACHPMinEffReq:HSPF( "StandardsVersion", Proj:StdsVersion,
                                     "Category", "HP",
                                     "SubCategory", ZnSys:SubType,
                                     "CondenserType", "Air",
                                     "SizeCategory", SysClgCap )
        else UNDEFINED
        endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else UNCHANGED
    endif 
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then
      if( BaseSysNum = 201 )
      then // Res Common Area is packaged 1-phase system < 65kBtu/hr, uses SEER2
        UNDEFINED
      else
      if( IfValidAnd( SysClgCap < 65000 ) .AND. ParentComp = "AirSys" )
      then // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, uses SEER
        UnitaryACHPMinEffReq:HSPF( "StandardsVersion", Proj:StdsVersion,
                                   "Category", "HP",
                                   "SubCategory", AirSys:SubType,
                                   "CondenserType", "Air",
                                   "SizeCategory", SysClgCap ) 
      else UNDEFINED // Not used if net capacity >= 65 MBH
      endif endif
    else UNCHANGED
    endif 
ENDRULE
// -----------------------------------------------------------------------------
// CodeMin for HeatPump HSPF2
RULE NEW CoilHtg:CodeMinHSPF2
  DATATYPE
    Float
  LONGFORM
    CodeMinimumHSPF2
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( SysClgCap > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( IfValidAnd( AirSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. AirSys:HSPF2IsValid > 0 )
        then
          UnitaryACHPMinEffReq:HSPF2( "StandardsVersion", Proj:StdsVersion,
                                      "Category", "HP", 
                                      "SubCategory", AirSys:SubType,
                                      "CondenserType", "Air", 
                                      "SizeCategory", SysClgCap )
        else UNDEFINED
        endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then
        if( IfValidAnd( ZnSys:EffMetric = "SEER2/EER2/HSPF2" ) .AND. ZnSys:HSPF2IsValid > 0 )
        then
          UnitaryACHPMinEffReq:HSPF2( "StandardsVersion", Proj:StdsVersion,
                                      "Category", "HP", 
                                      "SubCategory", ZnSys:SubType,
                                      "CondenserType", "Air", 
                                      "SizeCategory", SysClgCap )
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then
      if( BaseSysNum = 201 .AND. ParentComp = "AirSys" )
       // Baseline Res Common Area is assumed AirSys packaged 1-phase system < 65kBtu/hr, use SEER2
      then
        UnitaryACHPMinEffReq:HSPF2( "StandardsVersion", Proj:StdsVersion,
                                    "Category", "HP",
                                    "SubCategory", AirSys:SubType,
                                    "CondenserType", "Air", 
                                    "SizeCategory", 64999 )
      else UNDEFINED // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, use SEER
      endif
    else UNCHANGED
    endif 
ENDRULE

RULE NEW CoilHtg:HSPFToHSPF2Rat
  DATATYPE
    Float
  LONGFORM
    HSPFToHSPF2Ratio
  DESCRIPTION
    "The ratio of HSPF2/HSPF for a given equipment type."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" )
    then // Part of an AirSystem
      if( AirSys:HSPF2IsValid > 0 )
      then
        UnitaryACHPMinEffReq:HSPFToHSPF2Rat( "StandardsVersion", Proj:StdsVersion,
                                             "Category", "HP",
                                             "SubCategory", AirSys:SubType,
                                             "CondenserType", "Air", 
                                             "SizeCategory", CapTotNetRtd )
;       if( AirSys:SubType = "Split1Phase" .OR. AirSys:SubType = "Split3Phase" )
;       then 0.85 // Split AC or HP
;       else 0.84 // Packaged AC or HP
;       endif
      else UNDEFINED
      endif
    else // ------------------------------------------------------------------
    if( ParentComp = "ZnSys" )
    then
      if( ZnSys:HSPF2IsValid > 0 )
      then
        UnitaryACHPMinEffReq:HSPFToHSPF2Rat( "StandardsVersion", Proj:StdsVersion,
                                             "Category", "HP",
                                             "SubCategory", ZnSys:SubType,
                                             "CondenserType", "Air", 
                                             "SizeCategory", CapTotNetRtd )
;       if( AirSys:SubType = "Split1Phase" .OR. AirSys:SubType = "Split3Phase" )
;       then 0.85 // Split AC or HP
;       else 0.84 // Packaged AC or HP
;       endif
      else UNDEFINED
      endif
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then
      if( BaseSysNum = 201 .AND. ParentComp = "AirSys" )
       // Baseline Res Common Area is assumed AirSys packaged 1-phase system < 65kBtu/hr, use HSPF2
      then
        UnitaryACHPMinEffReq:HSPFToHSPF2Rat( "StandardsVersion", Proj:StdsVersion,
                                             "Category", "HP",
                                             "SubCategory", AirSys:SubType,
                                             "CondenserType", "Air", 
                                             "SizeCategory", 64999 )
      else UNDEFINED // All other Baseline NonRes <65kBtu/hr is assumed packaged 3-phase, use HSPF
      endif
    else UNCHANGED
    endif 
ENDRULE
// -----------------------------------------------------------------------------
// CodeMin for HeatPump COP
RULE NEW CoilHtg:CodeMinCOP
  DATATYPE
    Float
  LONGFORM
    CodeMinimumCOP
  INPUTCLASS
    NotInput
  UNITS 
    W/W
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( SysClgCap > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( SysType = "SPVHP" )
        then // SPVHP
          if( IfValidAnd( SysClgCap >= 240000 ) )
          then UNDEFINED // No requirement above 240 kBtu/hr
          else // Look-up from table
            UnitaryACHPMinEffReq:COP47(
              "StandardsVersion", Proj:StdsVersion, 
              "Category", SysType,
              "SubCategory", "*",
              "CondenserType", "Air",
              "SizeCategory", SysClgCap )          
          endif
        else
        if( IfValidAnd( SysClgCap >= 65000 ) .AND.
            IfValidAnd( CndsrType = "Air" ) .AND.
            AirSys:UnitaryCat = "HP" )
        then // Air-source, >65 kBtu/hr cooling
          UnitaryACHPMinEffReq:COP47(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", "Air",
            "SizeCategory", SysClgCap )
        else
        if( IfValidAnd( CndsrType = "WaterSource" ) .AND.
            AirSys:UnitaryCat = "HP" )
        then // Water-source
          UnitaryACHPMinEffReq:COP47(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", "WaterSource",
            "SizeCategory", SysClgCap )
        else UNDEFINED
        endif endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // Part of a ZoneSystem
        if( SysType = "SPVHP" )
        then // SPVHP
          if( IfValidAnd( SysClgCap >= 240000 ) )
          then UNDEFINED // No requirement above 240 kBtu/hr
          else // Look-up from table
            UnitaryACHPMinEffReq:COP47(
              "StandardsVersion", Proj:StdsVersion,
              "Category", SysType,
              "SubCategory", "*",
              "CondenserType", "Air",
              "SizeCategory", SysClgCap )          
          endif
        else
        if( ( SysType = "SZHP" .OR. SysType = "SZDFHP" .OR.
              SysType = "MiniSplitHP" ) .AND. 
            IfValidAnd( SysClgCap >= 65000 ) )
        then // >65 kBtu/hr cooling, including minisplits
          3.3 
        else
        if( SysType = "PTHP" .AND. SysClgCap > 0 )
        then // PTHP
          // Rules currently only cover PTAC and PTHP in new constrution or newly 
          // conditioned buildings or additions.  Rules for PTHP replacements 
          // not currently supported.
          3.2 - (0.026 * ( Min( 15000, Max( 7000, SysClgCap ) ) ) / 1000 )
        else
        if( SysType = "WSHP" .AND. IfValidAnd( SysClgCap > 0) )
        then // Water-source heat pumps
          if( SysClgCap < 135000 ) 
          then
            if( Proj:StdsVersionYr >= 2016 )
            then 4.3
            else 4.2
            endif
          else
          if( SysClgCap >= 135000 .AND. SysClgCap < 240000 )
          then 2.9
          else UNDEFINED
          endif endif
        else UNDEFINED
        endif endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( SysClgCap > 0 ) )
    then // Is baseline system
      if( IfValidAnd( SysClgCap < 65000 ) ) 
      then UNDEFINED // See HtPumpCOP for HSPF -> COP conversion
      else // Look-up minimum efficiency based on baseline rated net capacity 
        UnitaryACHPMinEffReq:COP47( "StandardsVersion", Proj:StdsVersion,
                                    "Category", AirSys:UnitaryCat,
                                    "SubCategory", AirSys:SubType,
                                    "SizeCategory", SysClgCap )
      endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// T24N_2016 HP heating efficiency
TABLE StdHPHtgEfficiency
    SizeCategory    COP47   
//  <65000          Based on HSPF -> COP equation
    <135000         3.2  
    <240000         2.8     
    >=240000        2.8  
ENDTABLE  


// ********** Electric Heat Pump Heating Efficiencies **********************
RULE CoilHtg:HtPumpHSPF2
  DESCRIPTION
    "The Heating Season Performance Factor2 (HSPF2) is an indicator of expected, 
     average seasonal heat pump efficiency. It is determined in accordance with 
     AHRI Standards." 
  HELP
    "This input applies to air-source HeatPump heating coils in packaged/split units
     with net cooling capacity <65 MBH."
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    5.0
  COMMONMAXIMUM
    13.0
  MAXIMUM
    15.0
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 .AND. 
        IfValidAnd( CodeMinHSPF2 > 0 ) )   
    then // For PROPOSED AutoEffInput only 
      CodeMinHSPF2
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 ) 
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then 
      UNCHANGED
    else
    if( IfValidAnd( CodeMinHSPF2 > 0 ) .AND. 
        IfValidAnd( HtPumpHSPF2 > 0 ) = 0 )
    then
      PostError("Heat pump coil '%s' must be specified with AHRI rated HSPF2.", Name )
    else 
    if( IfValidAnd( HtPumpHSPF2 < CodeMinHSPF2 ) )
    then 
      PostError("HSPF of coil '%s' is less than the code minimum required 
                 efficiency of HSPF2-%.1f. Select the bypass of efficiency
                 check at the coil if this is not applicable.",
                 Name, CodeMinHSPF2 )
    else UNCHANGED 
    endif endif endif
  SIZING
    if( BaseSysNum > 0 .OR. Type != "HeatPump" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else if( IfValidAnd( CodeMinHSPF2 > 0 ) ) 
    then // User-equipment is HSPF2-rated
      CodeMinHSPF2
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then // Is baseline system
      if( IfValidAnd( CodeMinHSPF2 > 0 ) )
      then CodeMinHSPF2
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:HtPumpHSPF
  DESCRIPTION
    "The Heating Season Performance Factor (HSPF) is an indicator of expected, 
     average seasonal heat pump efficiency. It is determined in accordance with 
     AHRI Standards." 
  HELP
    "This input applies to air-source HeatPump heating coils in packaged/split units
     with net cooling capacity <65 MBH."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    5.0
  COMMONMAXIMUM
    13.0
  MAXIMUM
    15.0
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 ) 
    then  // For PROPOSED AutoEffInput only 
      if( IfValidAnd( CodeMinHSPF > 0 ) )
      then // Is HSPF rated
        CodeMinHSPF
      else 
      if( IfValidAnd( CodeMinHSPF2 > 0 ) .AND. IfValidAnd( HSPFToHSPF2Rat > 0 ) )
      then // Is HSPF2 rated, calculate HSPF for COP calc
        CodeMinHSPF2 / HSPFToHSPF2Rat
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 ) 
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then 
      UNCHANGED
    else
    if( IfValidAnd( CodeMinHSPF > 0 ) .AND. 
        IfValidAnd( HtPumpHSPF > 0 ) = 0 )
    then
      PostError("Heat pump coil '%s' must be specified with AHRI rated HSPF.", Name )
    else 
    if( IfValidAnd( HtPumpHSPF < CodeMinHSPF ) )
    then 
      PostError("HSPF of coil '%s' is less than the code minimum required 
                 efficiency of HSPF-%.1f. Select the bypass of efficiency
                 check at the coil if this is not applicable.",
                 Name, CodeMinHSPF )
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    if( IfValidAnd( CodeMinHSPF > 0 ) .AND. 
        IfValidAnd( HtPumpHSPF > 0 ) = 0 )
    then
      PostError("Heat pump coil '%s' must be specified with AHRI rated HSPF.", Name )
    else UNCHANGED 
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "HeatPump" )
    then // Not used for non-HP coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else
    if( IfValidAnd( CodeMinHSPF2 > 0 ) .AND. IfValidAnd( HSPFToHSPF2Rat > 0 ) )
    then // User-equipment is HSPF2-rated
      HtPumpHSPF2 / HSPFToHSPF2Rat
    else
    if( IfValidAnd( CodeMinHSPF > 0 ) ) 
    then // User-equipment is HSPF-rated
      HtPumpHSPF
    else UNDEFINED
    endif endif endif
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then
      if( ParentComp = "ZnSys" )
      then UNDEFINED
      else if( IfValidAnd( SysClgCap < 65000 ) )
      then 7.7 // Baseline is assumed 3Phase
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then // Is baseline system
      if( IfValidAnd( CodeMinHSPF2 > 0 ) .AND. IfValidAnd( HSPFToHSPF2Rat > 0 ) )
      then // Baseline uses SEER2
        CodeMinHSPF2 / HSPFToHSPF2Rat
      else
      if( IfValidAnd( CodeMinHSPF > 0 ) )
      then CodeMinHSPF
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Rounded HP HSPF 
RULE NEW CoilHtg:HtPumpHSPFRnd
  DATATYPE
    Float
  LONGFORM
    HeatPumpHSPFRound
  DESCRIPTION
    "Round heating coil HSPF value based on AHRI testing standard"
  INPUTCLASS
    NotInput
  UNITS
    Btu/h-W
  DEFAULT
    if( IfValidAnd( HSPFCOPRndFact > 0 ) .AND.
        IfValidAnd( HtPumpHSPF > 0 ) )
    then
      int( ( HtPumpHSPF / HSPFCOPRndFact ) + 0.5 ) * HSPFCOPRndFact
    else
      UNDEFINED
    endif
  SIZING
    if( IfValidAnd( HSPFCOPRndFact > 0 ) .AND.
        IfValidAnd( HtPumpHSPF > 0 ) )
    then
      int( ( HtPumpHSPF / HSPFCOPRndFact ) + 0.5 ) * HSPFCOPRndFact
    else
      UNDEFINED
    endif
  ANNUAL
    if( IfValidAnd( HSPFCOPRndFact > 0 ) .AND.
        IfValidAnd( HtPumpHSPF > 0 ) )
    then
      int( ( HtPumpHSPF / HSPFCOPRndFact ) + 0.5 ) * HSPFCOPRndFact
    else
      UNDEFINED
    endif
ENDRULE      

// -----------------------------------------------------------------------------
;; DEBUGGING
;RULE CoilHtg:Action
;  SIZING
;    PostMessageToLog( "      b4 CoilHtg:HtPumpCOP Sizing rule for '%s':  BaseSysNum %g  /  Type %s  /  UserTypeToRestore %s", Name, ValidOr( BaseSysNum, -999 ), Type, ValidOr( UserTypeToRestore, "undef" ) )
;ENDRULE
RULE CoilHtg:HtPumpCOP
  DESCRIPTION
    "The heating efficiency of a heat pump at AHRI rated conditions."
  HELP
    "This is a required input for HeatPump heating coils in PTHP, WSHP, and other
     packaged/split units >= 65 MBH cooling capacity. For air-source heat pumps,
     this value should be the COP at 47°F outdoor dry bulb temperature."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    2.0
  COMMONMAXIMUM
    5.0
  UNITS 
    W/W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "HeatPump" )
    then
      if( Proj:AutoEffInput = 1 ) 
      then // For PROPOSED AutoEffInput only  
        if( IfValidAnd( CodeMinCOP > 0 ) )
        then // Code minimum COP based on capacity
          CodeMinCOP
        else UNDEFINED
        endif
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinCOP > 0 ) .AND. 
        IfValidAnd( HtPumpCOP > 0 ) = 0 )
    then 
      PostError("Heat pump coil '%s' must be specified with AHRI rated COP", Name)
    else 
    if( IfValidAnd( HtPumpCOP < CodeMinCOP ) )
    then 
      PostError("COP of coil '%s' is less than the code minimum required 
                 efficiency of COP-%.1f. Select the bypass of
                 efficiency check at the coil if this is not applicable.",
                 Name, CodeMinCOP)
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    if( IfValidAnd( CodeMinCOP > 0 ) .AND. 
        IfValidAnd( HtPumpCOP > 0 ) = 0 )
    then 
      PostError("Heat pump coil '%s' must be specified with AHRI rated COP", Name)
    else UNCHANGED 
    endif 
  SIZING
    if( BaseSysNum > 200 .AND. (Type = "HeatPump" .OR. IfValidAnd( UserTypeToRestore = "HeatPump" )) )
    then
      // default for sizing run purposes (for Res baseline systems only)
      3.0
    else
    if( BaseSysNum > 0 .OR. Type != "HeatPump" )
    then // Not used for non-HP coils, and AHRI rated COP unknown for baseline systems 
      UNDEFINED
    else
    if( IfValidAnd( HtPumpHSPFRnd > 0 ) .AND. IfValidAnd( HtPumpCOP > 0 ) = 0 )
    then
      // User has provided HSPF but not COP, so use default equation to calculate COP
      ( 0.2778 * HtPumpHSPFRnd ) + 0.9667
    else UNCHANGED
    endif endif endif
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( SysClgCap > 0 ) )
    then
      if( ParentComp = "ZnSys" )
      then // Is PTHP
        3.2 - (0.026 * ( Min( 15000, Max( 7000, SysClgCap ) ) ) / 1000 )
      else if( IfValidAnd( SysClgCap < 65000 ) ) 
      then // Use HSPF -> COP equation
        ( 0.2778 * HtPumpHSPF ) + 0.9667
      else // Look-up minimum efficiency based on baseline rated net capacity 
        StdHPHtgEfficiency:COP47( "SizeCategory", SysClgCap )
      endif endif
    else UNCHANGED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( SysClgCap > 0 ) )
    then // Is baseline system
      if( IfValidAnd( CodeMinHSPF > 0 ) .OR. IfValidAnd( CodeMinHSPF2 > 0 ) )
      then // Use HSPF -> COP equation
        ( 0.2778 * HtPumpHSPFRnd ) + 0.9667
      else
      if( IfValidAnd( CodeMinCOP > 0 ) )
      then CodeMinCOP
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE
// -----------------------------------------------------------------------------
RULE NEW CoilHtg:HtPumpCOPRnd
  DATATYPE
    Float
  LONGFORM
    HeatPumpCOPRound
  DESCRIPTION
    "Round heating coil COP value based on AHRID testing standard"
  INPUTCLASS
    NotInput
  SIZING
    if( IfValidAnd( HSPFCOPRndFact > 0 ) .AND.
        IfValidAnd( HtPumpCOP > 0 ) )
    then
      int( ( HtPumpCOP / HSPFCOPRndFact ) + 0.5 ) * HSPFCOPRndFact
    else
      UNDEFINED
    endif
  ANNUAL
    if( IfValidAnd( HSPFCOPRndFact > 0 ) .AND.
        IfValidAnd( HtPumpCOP > 0 ) )
    then
      int( ( HtPumpCOP / HSPFCOPRndFact ) + 0.5 ) * HSPFCOPRndFact
    else
      UNDEFINED
    endif
ENDRULE      

//   -----------------------------------------------------------------------------
// Adjust rated COP to remove fan heat at AHRI rating condtion
;; DEBUGGING
;RULE CoilHtg:Action
;  ANNUAL
;    PostMessageToLog( "      b4 CoilHtg:HtPumpCOPAdj Annual rule for '%s':  Type %s  /  BaseSysNum %g  /  CapTotGrossRtd %g  /  HtPumpCOPRnd %g  /  CapTotNetRtd %g", 
;                                     Name, Type, ValidOr( BaseSysNum, 0 ), ValidOr( CapTotGrossRtd, -999 ), ValidOr( HtPumpCOPRnd, -999 ), ValidOr( CapTotNetRtd, -999 ) )
;ENDRULE
RULE CoilHtg:HtPumpCOPAdj
  DESCRIPTION
    "The heating efficiency of a  heat pump heating system at AHRI 
     rated conditions, adjusted to remove fan energy."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    NotInput
  UNITS 
    W/W
  SIZING
    if( BaseSysNum > 0 .OR. Type != "HeatPump" )
    then UNDEFINED
    else 
    if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
        IfValidAnd( HtPumpCOPRnd > 0 ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      // HtPumpCOPAdj = ( Net capacity (Btu/hr) - Fan heat (Btu/hr) ) /
      //                ( Rated power (W) - fan power (W) - pump power (W) )
      // where the fan heat/power is the power associated with overcoming the
      // internal static and coil pressure drop ( for water-source equipment )
      // ( CapTotNetRtd - FanHtRtd ) /
      // ( CapTotNetRtd / HtPumpCOP ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
      // CapTotGrossRtd = CapTotNetRtd - FanHtRtd
      ( CapTotGrossRtd / 3.412 ) /  ; CapTotGrossRtd = CapTotNetRtd - FanHtRtd
      ( ( CapTotNetRtd / ( HtPumpCOPRnd * 3.412 ) ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
    else 0
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then 
      if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
          IfValidAnd( HtPumpCOPRnd > 0 ) .AND.
          IfValidAnd( CapTotNetRtd > 0 ) )
      then
        ( CapTotGrossRtd / 3.412 ) / ; CapTotGrossRtd = CapTotNetRtd - FanHtRtd
        ( ( CapTotNetRtd / ( HtPumpCOPRnd * 3.412 ) ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
      else if (IfValidAnd( HtPumpCOPRnd > 0 ))
      then  HtPumpCOPRnd   // handles case where autosized cap(s) are 0 - preventing later eval error
      else  0
      endif endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Convert COP to EIR for simulation purposes
RULE CoilHtg:HtPumpEIR
  DESCRIPTION
    "The heating efficiency of a heat pump heating coil, described for simulation
      purposes. Enery Input Ratio (EIR) is the inverse of the COP."
  REFERENCE 
    NACM Section 5.7.6.5 
  INPUTCLASS 
    NotInput
  SIZING
    if( Type = "HeatPump" .AND. IfValidAnd( HtPumpCOPAdj > 0 ) )
    then
      1 / HtPumpCOPAdj
//      if( ParentComp = "AirSys" )
//      then
//        if( IfValidAnd( Parent2Valid( DuctLeakage ) > 0 ) )
//        then // For now, increase EIR by 4.86% if DuctLeakage > 0
//          1 / HtPumpCOPAdj * 1.0486        
//        else 
//          1 / HtPumpCOPAdj
//        endif
//      else if( ParentComp = "ZnSys" )
//      then
//        if( IfValidAnd( ParentValid( DuctLeakage ) > 0 ) )
//        then // For now, increase EIR by 4.86% if DuctLeakage > 0
//          1 / HtPumpCOPAdj * 1.0486        
//        else 
//          1 / HtPumpCOPAdj
//        endif
//      else  1 / HtPumpCOPAdj   
//      endif endif
    else UNDEFINED
    endif
  ANNUAL 
    if( Type = "HeatPump" .AND. IfValidAnd( HtPumpCOPAdj > 0 ) )
    then 1 / HtPumpCOPAdj
    else UNDEFINED
    endif
ENDRULE


// ********** Heat Pump Heating Efficiency Temperature Adjustment Curve ********
RULE CoilHtg:HtPumpEIR_fTempCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil and condenser conditions."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS
    Prescribed 
  SIZING
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then
      if( ServesResZn > 0 )
      then RuleLibrary(CrvCubic, "CoilHtgHPEIRRatio_fToadbIP") 
      else RuleLibrary(CrvCubic, "CoilHtgHPEIRRatio_fToadbSI")
      endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then 
      if( ServesResZn > 0 )
      then RuleLibrary(CrvCubic, "CoilHtgHPEIRRatio_fToadbIP") 
      else RuleLibrary(CrvCubic, "CoilHtgHPEIRRatio_fToadbSI")
      endif
    else UNCHANGED
    endif
ENDRULE

;; DEBUGGING
;RULE CoilHtg:Action
;  ANNUAL
;    PostMessageToLog( "      b4 CoilHtg:HtPumpCOP17Adj Annual rule for '%s':  Type %s  /  HtPumpEIR %g", Name, Type, ValidOr( HtPumpEIR, -999 ) )
;ENDRULE
RULE NEW CoilHtg:HtPumpCOP17Adj
  DATATYPE
    Float
  LONGFORM
    HeatPumpCOP17Adjusted
  DESCRIPTION
    "The heating efficiency of aan air-source heat pump at 17°F, adjusted to remove fan energy."
  SIZING
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        LocalCompAssigned( HtPumpEIR_fTempCrvRef ) = ComponentType( "CrvCubic" ) )
    then
      1 / ( HtPumpEIR * ( HtPumpEIR_fTempCrvRef:Coef1 + 
                        ( HtPumpEIR_fTempCrvRef:Coef2 * 17 ) +
                        ( HtPumpEIR_fTempCrvRef:Coef3 * pow( 17, 2 ) ) +                         
                        ( HtPumpEIR_fTempCrvRef:Coef4 * pow( 17, 3 ) ) ) )       
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        LocalCompAssigned( HtPumpEIR_fTempCrvRef ) = ComponentType( "CrvCubic" ) )
    then
      1 / ( HtPumpEIR * ( HtPumpEIR_fTempCrvRef:Coef1 + 
                        ( HtPumpEIR_fTempCrvRef:Coef2 * 17 ) +
                        ( HtPumpEIR_fTempCrvRef:Coef3 * pow( 17, 2 ) ) +                         
                        ( HtPumpEIR_fTempCrvRef:Coef4 * pow( 17, 3 ) ) ) )       
    else UNDEFINED
    endif
ENDRULE


// ********** Heat Pump Part-Load Efficiency Adjustment Curve ******************
RULE CoilHtg:HtPumpEIR_fPLFCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     part-load factor.  This curve type is specific to EnergyPlus."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS
    Prescribed 
  SIZING
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then
      if( ServesResZn > 0 )
      then UNDEFINED
      else RuleLibrary(CrvQuad, "CoilHtgHPEIRRatio_fQFrac")
      endif
    else UNDEFINED
    endif
  ANNUAL 
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then 
      if( ServesResZn > 0 )
      then UNDEFINED 
      else RuleLibrary(CrvQuad, "CoilHtgHPEIRRatio_fQFrac")
      endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Heating efficiency as a function of indoor coil coil flow.
RULE CoilHtg:HtPumpEIR_fFlowCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil flow. This curve type is specific to EnergyPlus."
  INPUTCLASS
    Prescribed 
  SIZING
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then
      if( ServesResZn > 0 )
      then UNDEFINED
      else RuleLibrary(CrvQuad, "CoilHtgHPEIRRatio_fCFMRatio")
      endif
    else UNDEFINED
    endif
  ANNUAL 
    if( BaseSysNum > 0 .AND. Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then 
      if( ServesResZn > 0 )
      then UNDEFINED 
      else RuleLibrary(CrvQuad, "CoilHtgHPEIRRatio_fCFMRatio")
      endif
    else UNCHANGED
    endif
ENDRULE


// ********** Heat Pump Compressor Minimum Operating Temp **********************
RULE CoilHtg:HtPumpCprsrLockoutTemp
  DESCRIPTION
    "The outside dry-bulb temperature below which the compressor is shut-off."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  COMMONMINIMUM
    -10.0
  COMMONMAXIMUM
    45.0
  UNITS 
    °F
  REPORTPRECISION
    0
  DEFAULT : T24N_2016 T24N_2016 S901G
    if( Type = "HeatPump" )
    then
      if( CndsrType = "Air" )
      then 
        if( ParentComp = "ZnSys" )
        then
          if( SysType = "PTHP" )
          then 35
          else 17
          endif
        else 17
        endif
      else if( CndsrType = "WaterSource" )
      then -50
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  DEFAULT : T24N
    if( Type = "HeatPump" )
    then
      if( CndsrType = "Air" )
      then 
        if( ParentComp = "ZnSys" )
        then
          if( SysType = "PTHP" )
          then 35 // For PTHP
          else 17
          endif
        else
        if( ServesResZn > 0 )
        then 0 // ResAirSys, per BW
        else 17 // NRes AirSys
        endif endif
      else if( CndsrType = "WaterSource" )
      then -50
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKCODE : S901G
    if( BypassCheckCode > 0 )
    then UNCHANGED
    else
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 1 ) .AND.
        IfValidAnd( HtPumpCprsrLockoutTemp > 40 ) )
    then 
      PostWarning("The compressor lockout temperature for heat pump coil '%s'
                   is %.1f degF. Supplemental heating is not allowed above 40degF,
                   so unmet heating hours may occur.", Name, HtPumpCprsrLockoutTemp)
    else UNCHANGED
    endif endif
  CHECKSIM
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        LocalCompAssigned( HtPumpSuppCoilHtgRef ) = 0 .AND.
        IfValidAnd( HtPumpCprsrLockoutTemp < 40 ) )
    then 
      if( ParentComp = "ZnSys" )
      then // Is ZnSys, add exceptions to this check
        if( ZnSys:Type = "MiniSplit" )
        then UNCHANGED // No HtPumpSuppCoil is expected
        else 
          PostWarning("A supplemental heating coil for heat pump coil '%s' is not
                       defined and heat pump operation is specified to disable below %.1f degF.
                       If unmet heating hours occur, a supplemental heating coil or 
                       other additional heating source should be provided.",
                       Name, HtPumpCprsrLockoutTemp)
        endif
      else
        PostWarning("A supplemental heating coil for heat pump coil '%s' is not
                     defined and heat pump operation is specified to disable below %.1f degF.
                     If unmet heating hours occur, a supplemental heating coil or 
                     other additional heating source should be provided.",
                     Name, HtPumpCprsrLockoutTemp)
      endif
    else UNCHANGED
    endif 
  ANNUAL : T24N_2016 T24N_2019
    if( Type = "HeatPump" )
    then 
      if( BaseSysNum > 0 )
      then
        if( SysType = "PTHP" )
        then 35
        else 17
        endif
      else UNCHANGED
      endif
    else UNDEFINED
    endif
  ANNUAL : T24N
// See ticket 3370 for 2022 heat pump controls
    if( Type = "HeatPump" )
    then 
      if( BaseSysNum > 0 )
      then
        if( SysType = "PTHP" )
        then 35
        else
        if( ParentComp = "AirSys" )
        then
          if( ServesResZn > 0 )
          then 0 // ResAirSys, revised to 0 per BW
          else
          if( IfValidAnd( AirSys:BaseSysNumOpt = "c" ) )
          then 45 // Is a dual-fuel (DF) heat pump
          else 17 // Same as previous
          endif endif
        else 17
        endif endif
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE

// ********** Heat Pump Compressor Enable Temp **********************
RULE CoilHtg:HtPumpCprsrEnableTemp
  DESCRIPTION
    "The outside dry-bulb temperature at which the compressor turns back on
     if it was previously shut off by HtPumpCprsrLockoutTemp."
  INPUTCLASS 
    Default
  COMMONMINIMUM
    -10.0
  COMMONMAXIMUM
    45.0
  UNITS 
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "HeatPump" )
    then
      if( CndsrType = "Air" )
      then // Set to same temp as lockout
        HtPumpCprsrLockoutTemp
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "HeatPump" )
    then
      if( CndsrType = "Air" )
      then // Set to same temp as lockout
        HtPumpCprsrLockoutTemp
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** Electric Supplemental Heating Coil *******************************
RULE NEW CoilHtg:IsHtPumpSuppCoilHtg
  DATATYPE
    Integer
  LONGFORM
    IsHeatPumpSupplementalCoilHeating
  DESCRIPTION
    "The a flag that indicates the CoilHeating object is supplemental heating for
     the heat pump."
  INPUTCLASS
    NotInput
  ANNUAL
    if( CountRefs( CoilHtg:HtPumpSuppCoilHtgRef) > 0 )
    then 1
    else 0
    endif
ENDRULE
 
RULE CoilHtg:HtPumpSuppCoilHtgRef
  DESCRIPTION
    "The name of the CoilHeating object that provide supplemental heating for
     the heat pump."
  HELP
    "The valid CoilHeating Types are Resistance and Furnace."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Optional
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED
    else
    if( ParentComp = "ZnSys" .AND.
        Type = "HeatPump" .AND. 
        LocalCompAssigned( HtPumpSuppCoilHtgRef ) )
    then // ZnSys
      if( ZnSys:Type = "PTHP" .AND.
          IfValidAnd( CndsrType = "Air" ) .AND.
          HtPumpSuppCoilHtgRef:Type != "Resistance" .AND. 
          HtPumpSuppCoilHtgRef:Type != "Furnace" )
      then 
        PostError("The supplemental heat coil, '%s', for PTHP system '%s' must be Type =
                   'Resistance' or 'Furnace'.", Name, Parent( Name ))
      else
      if( ZnSys:Type = "WSHP" .AND.
          IfValidAnd( CndsrType = "WaterSource" ) .AND.
          HtPumpSuppCoilHtgRef:Type != "Resistance" .AND. 
          HtPumpSuppCoilHtgRef:Type != "HotWater" .AND.
          HtPumpSuppCoilHtgRef:Type != "Furnace" )
      then 
        PostError("The supplemental heat coil, '%s', for WSHP system '%s' must be Type =
                   'Resistance', 'Furnace', or 'HotWater'.", Name, Parent( Name ))
      else UNCHANGED
      endif endif
    else // AirSys
    if( LocalCompAssigned( HtPumpSuppCoilHtgRef ) )
    then
      if( Type != "HeatPump" )
      then
        PostError("CoilHeating '%s' is not a HeatPump, but a supplemental heating
                   coil is defined. Coil '%s' must deleted from the system.",
                   Name, HtPumpSuppCoilHtgRef:Name )
      else
      if( Type = "HeatPump" .AND. ServesResZn > 0 .AND. 
          HtPumpSuppCoilHtgRef:Type != "Resistance" .AND. 
          HtPumpSuppCoilHtgRef:Type != "Furnace" )
      then
        PostError("CoilHeating '%s' is a HeatPump, and its supplemental heating coil
                   '%s' is Type = '%s'. Only 'Resistance' and 'Furnace' supplemental coils
                   are currently supported for AirSystems serving Residential zones.",
                   Name, HtPumpSuppCoilHtgRef:Name, HtPumpSuppCoilHtgRef:Type )
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif endif
  ANNUAL : T24N
    if( BaseSysNum = 7 .AND. Type = "Furnace" )
    then // Supplemental coil not needed for std systems with furnace
      UNDEFINED
    else UNCHANGED
    endif
ENDRULE

// T24N_2022 Delete supplemental CoilHtg object if system is changed to SZAC
RULE CoilHtg:Action
  ANNUAL : T24N
    if( BaseSysNum = 7 .AND. SysType = "SZAC" .AND.
        IfValidAnd( IsHtPumpSuppCoilHtg > 0 ) )
    then // System is SZAC, delete HtPumpSuppCoilHtg object
      DeleteComp()
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:HtPumpSuppTemp
  DESCRIPTION
    "The outside air dry-bulb temperature below which the heat pump supplemental 
     heating is enabled to operate."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  COMMONMINIMUM
    32.0
  COMMONMAXIMUM
    40.0
  UNITS 
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "HeatPump" .AND. LocalCompAssigned( HtPumpSuppCoilHtgRef ) > 0 )
    then 40.0     
    else UNDEFINED
    endif 
  CHECKCODE : T24N
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 1 ) .AND.
        LocalCompAssigned( HtPumpSuppCoilHtgRef ) > 0 .AND.
        IfValidAnd( HtPumpCprsrLockoutTemp > HtPumpSuppTemp ) )
    then
      if( ServesResZn > 0 )
      then UNCHANGED // Modeled in CSE
      else 
        PostWarning("The supplemental heating temperature operation (shut-off) limit for 
                     heat pump coil '%s' is greater than the compressor lockout
                     temperature.", Name)
      endif
    else UNCHANGED
    endif 
  CHECKCODE : S901G
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 1 ) .AND.
        LocalCompAssigned( HtPumpSuppCoilHtgRef ) > 0 .AND.
        IfValidAnd( HtPumpCprsrLockoutTemp > 40 ) )
    then 
      PostError("The supplemental heating temperature operation (shut-off) limit for
                 heat pump coil '%s' is greater than 40degF.", Name)
    else UNCHANGED
    endif
  CHECKSIM
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        ParentComp = "AirSys" .AND.
        IfValidAnd( CapTotNetRtd > 1 ) .AND.
        LocalCompAssigned( HtPumpSuppCoilHtgRef ) > 0 )
    then
      if( ServesResZn > 0 )
      then UNCHANGED // Modeled in CSE
      else 
        PostWarning("The heat pump coil '%s' is part of an AirSystem, and therefore
                     the supplemental heating temperature operation (shut-off) limit
                     is not simulated. The supplemental heating coil will be used
                     anytime the heat pump cannot meet the zone load or supply air
                     temperature setpoint.", Name)
      endif
    else UNCHANGED
    endif      
  ANNUAL 
    if( Type = "HeatPump" .AND. LocalCompAssigned( HtPumpSuppCoilHtgRef ) > 0 )
    then 
      if( BaseSysNum > 0 )
      then Max( ValidOr( HtPumpCprsrEnableTemp, 0 ), 40 )
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** Coil Defrost ********************************************
RULE CoilHtg:HtPumpDefHtSrc
  DESCRIPTION
    "The defrost heating source for an air-cooled heat pump."
  HELP
    "Only applicable if Type = 'HeatPump' and CondenserType = 'Air'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  OPTION
    Electric
    HotGas
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then "HotGas"
    else UNDEFINED
    endif
  ANNUAL 
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) )
    then 
      if( BaseSysNum > 0 )
      then "HotGas"
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** Coil Defrost Capacity ********************************************
RULE CoilHtg:HtPumpDefHtrCap
  DESCRIPTION
    "The capacity of the electric resistance defrost heater."
  HELP
    "Only applicable if Type = 'HeatPump', CondenserType = 'Air', and
    HeatPumpDefrostHeatSource = 'Electric'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    CondRequired
  UNITS 
    Btu/h
  REPORTPRECISION
    -3
  DEFAULT
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND. 
        IfValidAnd( HtPumpDefHtSrc = "Electric" ) .AND.
        Proj:AutoHardSize = 1 )
    then // For PROPOSED AutoHardSizing only
      1.25 * ValidOr( SysClgCap, 0 )
    else UNDEFINED
    endif
  ANNUAL 
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND. 
        IfValidAnd( HtPumpDefHtSrc = "Electric" ) )
    then HtPumpDefHtrCap
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:HtPumpDefHtrCapSim
  DESCRIPTION
    "The capacity of the electric resistance defrost heater, for simulation."
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h
  ANNUAL 
    if( LocalStatus ( HtPumpDefHtrCap ) > 0 )
    then HtPumpDefHtrCap * SysCnt
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Defrost control method
RULE CoilHtg:HtPumpDefCtrl
  DESCRIPTION
    "The defrost control mechanism for an air-cooled heat pump.  If TimedCycle, 
     the prescribed time period is 3.5 minutes."
  HELP
    "Only applicable if Type = 'HeatPump' and CondenserType = 'Air'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  OPTION
    OnDemand
    TimedCycle
  DEFAULT
    if( Type = "HeatPump" .AND. 
        IfValidAnd( CndsrType = "Air" ) .AND.
        LocalStatus( HtPumpDefHtSrc ) > 0 )
    then "TimedCycle"
    else UNDEFINED
    endif
  ANNUAL 
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) ) 
    then
      if( BaseSysNum > 0 )
      then "TimedCycle"
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Defrost limit temperature
RULE CoilHtg:HtPumpDefCtrlTemp
  DESCRIPTION
    "The outside dry-bulb temperature below which the coil defrost is allowed to 
     operate."
  HELP
    "Only applicable if Type = 'HeatPump' and CondenserType = 'Air'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        LocalStatus( HtPumpDefHtSrc ) > 0 ) 
    then 40
    else UNDEFINED
    endif
  ANNUAL 
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        LocalStatus( HtPumpDefHtSrc ) > 0 ) 
    then 40
    else UNDEFINED
    endif
ENDRULE


// ********** Crankcase Heater Capacity ****************************************
RULE CoilHtg:HtPumpCrankcaseHtrCap
  DESCRIPTION
    "The capacity of the electric resistance crankcase heater."
  HELP
    "Only applicable if Type = 'HeatPump' and CondenserType = 'Air'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Optional
  UNITS 
    Btu/h
  DEFAULT
    if( Type = "HeatPump" )
    then 
      if( LocalCompAssigned( CoilClgRef ) )
      then // Synchronize w/ DX coil
        CoilClgRef:DXCrankcaseHtrCap
      else UNCHANGED
      endif
    else 0
    endif
  ANNUAL
    if( Type = "HeatPump" )
    then 
      if( LocalCompAssigned( CoilClgRef ) )
      then // Synchronize w/ DX coil
        CoilClgRef:DXCrankcaseHtrCap
      else UNCHANGED
      endif
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:HtPumpCrankcaseHtrCapSim
  DESCRIPTION
    "The capacity of the electric resistance cranckcase heater, for simulation."
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h
  ANNUAL 
    if( IfValidAnd( HtPumpCrankcaseHtrCap > 0 ) )
    then HtPumpCrankcaseHtrCap * SysCnt
    else UNDEFINED
    endif
ENDRULE


// ********** Crankcase Heater Shutoff Temperature ****************************
RULE CoilHtg:HtPumpCrankcaseCtrlTemp
  DESCRIPTION
    "The outdoor air dry-bulb temperature above which the crank case heater is 
     not permitted to operate."
  HELP
    "Only applicable if Type = 'HeatPump'."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default
  COMMONMINIMUM
    32
  COMMONMAXIMUM
    50 
  UNITS 
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( HtPumpCrankcaseHtrCap > 0 ) )
    then
      if( LocalCompAssigned( CoilClgRef ) )
      then // Synchronize w/ DX coil
        CoilClgRef:DXCrankcaseCtrlTemp
      else 50
      endif
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "HeatPump" .AND. IfValidAnd( HtPumpCrankcaseHtrCap > 0 ) )
    then
      if( LocalCompAssigned( CoilClgRef ) )
      then // Synchronize w/ DX coil
        CoilClgRef:DXCrankcaseCtrlTemp
      else UNCHANGED
      endif
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
// For reporting/QC purposes
RULE NEW CoilHtg:SuppHtgCap
  DATATYPE
    Float
  LONGFORM
    SupplementalCoilHeatingCapacity
  DESCRIPTION
    "The capacity of the heat pump supplemental heaing coils"
  UNITS 
    Btu/h
  INPUTCLASS 
    NotInput         
  DEFAULT
    if( CountRefs(CoilHtg:HtPumpSuppCoilHtgRef) > 0 )
    then CapTotNetRtd
    else 0
    endif
  ANNUAL  
    if( CountRefs(CoilHtg:HtPumpSuppCoilHtgRef) > 0 )
    then CapTotNetRtd
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE AirSys:SuppHtgCap
  DESCRIPTION
    "The capacity of the heat pump supplemental heaing coils"   
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h
  REPORTPRECISION
    -3
  DEFAULT
    SumChildren( CoilHtg:SuppHtgCap )
  ANNUAL  
    SumChildren( CoilHtg:SuppHtgCap )
ENDRULE

// -----------------------------------------------------------------------------
RULE ZnSys:SuppHtgCap
  DESCRIPTION
    "The capacity of the heat pump supplemental heaing coils"   
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h
  REPORTPRECISION
    -3
  DEFAULT
    SumChildren( CoilHtg:SuppHtgCap )
  ANNUAL  
    SumChildren( CoilHtg:SuppHtgCap )
ENDRULE


// ********** System Heating Capacity (Net) ***********************************
// =========================== AirSystem =======================================
RULE AirSys:HtgCap
  DESCRIPTION
    "The net heating capacity of the AirSystem."
  HELP
    "This is the sum of all child CoilHeating component (net) heating capacities,
     minus the capacity of heat pump supplementary coils."
  INPUTCLASS 
    NotInput     
  REPORTPRECISION
    -3
  DEFAULT
    if( LocalStatus( SuppHtgCap ) > 0 )
    then SumChildren( CoilHtg:CapTotNetRtd ) - SuppHtgCap  
    else SumChildren( CoilHtg:CapTotNetRtd )
    endif  
  CHECKSIM
    if( IfValidAnd( AirSys:BypassCheckSim > 0 ) )
    then UNCHANGED
    else
    if( ( Type = "SZHP" .OR. Type = "SZDFHP" .OR.
          Type = "SZVAVHP" .OR. Type = "SZVAVDFHP" .OR. AirSys:Type = "SZVAVDFHP" ) .AND.
        SumChildren( AirSeg:HtPumpCap ) <= 0 )
    then
      PostError("AirSystem '%s' is defined as Type = '%s', but it does not have
                 heating coil of Type = 'HeatPump' heating source, or that coil
                 has 0 heating capacity. Revise the system Type to be SZAC or
                 SZVAVAC if a non-HP system is desired.",
                 Name, Type) 
    else
    if( IsCondgSys > 0 .AND. HtgCap = 0 .AND. 
        Type != "DOASCV" .AND.
        Type != "DOASVAV" )
    then
      PostWarning("AirSystem '%s' is defined as Type = '%s', but has either no
                   heating coils, or zero heating capacity.", Name, Type)
    else UNCHANGED
    endif endif endif
  ANNUAL
    if( LocalStatus( SuppHtgCap ) > 0 )
    then SumChildren( CoilHtg:CapTotNetRtd ) - SuppHtgCap  
    else SumChildren( CoilHtg:CapTotNetRtd )
    endif 
ENDRULE
// =========================== ZoneSystem ======================================
RULE ZnSys:HtgCap
  DESCRIPTION
    "The net heating capacity of the AirSystem."
  HELP
    "This is the sum of all child CoilHeating component (net) heating capacities,
     minus the capacity of heat pump supplementary coils."
  INPUTCLASS
    NotInput
  REPORTPRECISION
    -3
  DEFAULT
    if( LocalStatus( SuppHtgCap ) > 0 )
    then SumChildren( CoilHtg:CapTotNetRtd ) - SuppHtgCap  
    else SumChildren( CoilHtg:CapTotNetRtd )
    endif 
  CHECKSIM
    if( IsCondgSys = 1 .AND. HtgCap = 0 .AND. 
        ( Type != "PassiveBeam" .AND. Type != "MiniSplitAC" .AND. Type != "Radiant") )
    then
      PostWarning("ZoneSystem '%s' is defined as Type = '%s', but has either no
                   heating coils, or zero heating capacity.", Name, Type)
    else UNCHANGED
    endif 
  ANNUAL
    if( LocalStatus( SuppHtgCap ) > 0 )
    then SumChildren( CoilHtg:CapTotNetRtd ) - SuppHtgCap  
    else SumChildren( CoilHtg:CapTotNetRtd )
    endif 
ENDRULE

// ********** Heat Extraction for Water-cooled Condenser ************************
RULE NEW CoilHtg:HtExtractionLdSim
  DATATYPE
    Float
  LONGFORM
    HeatExtractionLoadSimulated
  DESCRIPTION
    "The amount of heat extracted by the WSHP heating coil at the peak rated output."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "WaterSource" ) .AND. 
        CapTotGrossRtdSim > 0 )
    then CapTotGrossRtdSim * ( 1 - ( 1 / ValidOr( HtPumpCOPAdj, 3 ) ) )
    else 0
    endif
ENDRULE
