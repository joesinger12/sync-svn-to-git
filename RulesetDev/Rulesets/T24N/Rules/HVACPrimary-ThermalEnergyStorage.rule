// HVAC Primary Systems - Thermal Energy Storage
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALifORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN if
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------

//  This rule file addresses the following building descriptors:
//  Section 5.X.X - Thermal Energy Storage
//      Thermal Energy Storage Type
//      Thermal Energy Storage Discharge Priority
//      Thermal Energy Storage Operation Mode Schedule
//      Thermal Energy Storage Rated Capacity
//      Thermal Energy Storage Design Flow Rate
//      Thermal Energy Storage Tank Location
//      Thermal Energy Storage Tank Location Zone Name
//      Thermal Energy Storage Tank Shape
//      Thermal Energy Storage Tank Volume
//      Thermal Energy Storage Tank Height
//      Thermal Energy Storage Tank Length to Width Ratio
//      Thermal Energy Storage Tank Perimeter
//      Thermal Energy Storage Tank R-Value
//      Thermal Energy Storage Tank U-Factor
//      Thermal Energy Storage Tank Setpoint Temperature
//      Thermal Energy Storage Tank Discharge Schedule Reference
//      Thermal Energy Storage Tank Charge Schedule Reference
//      Thermal Energy Storage Tank Inverse Discharge Schedule Reference
//      ..

// -----------------------------------------------------------------------------
// ********** Status *********************************************************** 
RULE ThrmlEngyStor:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New'
;   New
;   Existing 
  DEFAULT
// Status defined from top-down
    if( Parent( IsNew ) ) then "New" else "Existing" endif
  CHECKCODE
    if( ( Proj:IsNewMech .AND. Status != "New" ) .OR.
        ( Proj:IsAlt = 0 .AND. Status = "Altered" ) )
    then
      PostWarning("Thermal Energy Storage '%s' has a Status of '%s', but Compliance Type is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else UNCHANGED
    endif
   SIZING
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else u:Status
    endif
  ANNUAL
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else z:Status
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ThrmlEngyStor:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  SIZING
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  ANNUAL
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ThrmlEngyStor:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    if( IsNew ) then 0 else 1 endif
ENDRULE


// -----------------------------------------------------------------------------
//        Thermal Energy Storage Discharge Inlet Fluid Segment
RULE ThrmlEngyStor:DischrgFluidSegInRef
  DESCRIPTION
    "The inlet fluid segment when the storage system is discharging.  
     This must be a chilled water return."
  INPUTCLASS
    Required
  CHECKSIM
    if( Parent( Type ) != "ChilledWater" )
    then 
      PostError( "Thermal energy storage systems may only be assigned to 
                  chilled water fluid systems. Thermal energy storage system
                  '%s' is assigned to a fluid system of type '%s'.  Please 
                  revise the inputs.", Name, Parent(Type) )
;    else if( DischrgFluidSegInRef:Type != "PrimaryReturn" )    // This comparison is not valid - throws an error
;    then 
;      PostError( "The discharge inlet fluid segment must be a primary return. 
;                  Thermal energy storage system '%s' has an incorrect type 
;                  assigned.  Please revise the inputs.", Name )
    else UNCHANGED
;    endif endif
    endif
  SIZING
    u:DischrgFluidSegInRef
  ANNUAL
    z:DischrgFluidSegInRef
ENDRULE


// -----------------------------------------------------------------------------
//        Thermal Energy Storage Discharge Outlet Fluid Segment
RULE ThrmlEngyStor:DischrgFluidSegOutRef
  DESCRIPTION
    "The outlet fluid segment when the storage system is discharging.  
     This must be a chilled water supply."
  INPUTCLASS
    Required
  CHECKSIM
    if( Parent( Type ) != "ChilledWater" )
    then 
      PostError( "Thermal energy storage systems may only be assigned to 
                  chilled water fluid systems. Thermal energy storage system
                  '%s' is assigned to a fluid system of type '%s'.  Please 
                  revise the inputs.", Name, Parent(Type) )
;    else if( DischrgFluidSegOutRef:Type != "PrimarySupply" )  
;    then 
;      PostError( "The discharge outlet fluid segment must be a primary supply. 
;                  Thermal energy storage system '%s' has an incorrect type 
;                  assigned.  Please revise the inputs.", Name )
    else UNCHANGED
;    endif endif
    endif
  SIZING
    u:DischrgFluidSegOutRef
  ANNUAL
    z:DischrgFluidSegOutRef
ENDRULE


// -----------------------------------------------------------------------------
//        Thermal Energy Storage Charge Inlet Fluid Segment
RULE ThrmlEngyStor:ChrgFluidSegInRef
  DESCRIPTION
    "The inlet fluid segment when the storage system is being charged.  
     This must be a chilled water supply, as the tank is a load on the 
     chilled water system during charging."
  INPUTCLASS
    Required
  DEFAULT
    u:DischrgFluidSegOutRef
  CHECKSIM
    if( Parent( Type ) != "ChilledWater" )
    then 
      PostError( "Thermal energy storage systems may only be assigned to 
                  chilled water fluid systems. Thermal energy storage system
                  '%s' is assigned to a fluid system of type '%s'.  Please 
                  revise the inputs.", Name, Parent(Type) )
;    else if( ChrgFluidSegInRef:Type != "PrimarySupply" )  
;    then 
;      PostError( "The charge inlet fluid segment must be a primary supply. 
;                  Thermal energy storage system '%s' has an incorrect type 
;                  assigned.  Please revise the inputs.", Name )
    else UNCHANGED
;    endif endif
    endif
  SIZING
    u:ChrgFluidSegInRef
  ANNUAL
    z:ChrgFluidSegInRef
ENDRULE


// -----------------------------------------------------------------------------
//        Thermal Energy Storage Charge Outlet Fluid Segment
RULE ThrmlEngyStor:ChrgFluidSegOutRef
  DESCRIPTION
    "The outlet fluid segment when the storage system is being charged.  
     This must be a chilled water return, as the tank is a load on the 
     chilled water system during charging."
  INPUTCLASS
    Required
  DEFAULT
    u:DischrgFluidSegInRef
  CHECKSIM
    if( Parent( Type ) != "ChilledWater" )
    then 
      PostError( "Thermal energy storage systems may only be assigned to 
                  chilled water fluid systems. Thermal energy storage system
                  '%s' is assigned to a fluid system of type '%s'.  Please 
                  revise the inputs.", Name, Parent(Type) )
;    else if( ChrgFluidSegOutRef:Type != "PrimaryReturn" )  
;    then 
;      PostError( "The charge outlet fluid segment must be a primary return. 
;                  Thermal energy storage system '%s' has an incorrect type 
;                  assigned.  Please revise the inputs.", Name )
    else UNCHANGED
;    endif endif
    endif
  SIZING
    u:ChrgFluidSegOutRef
  ANNUAL
    z:ChrgFluidSegOutRef
ENDRULE


// -----------------------------------------------------------------------------
//        Thermal Energy Storage System Type
RULE ThrmlEngyStor:Type
  DESCRIPTION
    "The type of thermal energy storage system being used."
  HELP
    "The type of energy storage system selected from a list: Chilled Water 
     is the only currently supported option."
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   ChilledWater
;   Ice
;   Other types to be defined?
  DEFAULT
    ChilledWater
  SIZING
    u:Type
  ANNUAL
    z:Type
ENDRULE


// -----------------------------------------------------------------------------
//        Discharge Priority
RULE ThrmlEngyStor:DischrgPriority
  DESCRIPTION
    "Shows whether the storage system or a chiller will operate first to meet
     cooling loads during the discharge period."
  HELP
    "Selecting storage priority will normally provide larger demand charge 
     savings but requires a larger storage system.  Chiller priority allows  
     use of a significantly smaller storage system but demand reduction will 
     be smaller."
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   Storage
;   Chiller
  DEFAULT
    Storage
  CHECKSIM
    if( LocalStatus( DischrgPriority ) < 1 )
    then
      PostError( "A discharge priority must be specified for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:DischrgPriority
  ANNUAL
    z:DischrgPriority
ENDRULE


// -----------------------------------------------------------------------------
//        Is Chiller Priority
RULE NEW ThrmlEngyStor:IsChlrPriority
  DATATYPE
    Integer
  LONGFORM
    IsChillerPriority
  DESCRIPTION
    "A flag set to 1 for a thermal energy storage system that had discharge 
     priority set to Chiller."
  INPUTCLASS
    NotInput
  DEFAULT
    if( DischrgPriority = "Chiller" )
    then 1
    else 0
    endif
  SIZING
    u:IsChlrPriority
  ANNUAL
    z:IsChlrPriority
ENDRULE


// -----------------------------------------------------------------------------
//        Rated Capacity
RULE ThrmlEngyStor:CapRtd
  DESCRIPTION
    "The design cooling capacity of the thermal energy storage system."
  HELP
    "The design cooling capacity will determine the flow rate through the 
     system using the parent fluid system's design delta T."
  INPUTCLASS
    Required
  MINIMUM 
    0
  UNITS 
    Btu/hr
  DEFAULT
    if( Proj:AutoHardSize = 1 .AND. IsChlrPriority = 0 )
    then
      MAX( 0, Parent( TotChlrCap ) )
    else
      UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( CapRtd ) < 1 )
    then
      PostError( "A capacity must be provided for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:CapRtd
  ANNUAL
    z:CapRtd
ENDRULE


// -----------------------------------------------------------------------------
//        Design Flow Rate
RULE ThrmlEngyStor:WtrFlowCap
  DESCRIPTION
    "The water flow rate through the thermal energy storage system that 
     provides the rated capacity when using the fluid system design delta T."
  HELP
    "The design cooling capacity will determine the flow rate through the 
     system using the parent fluid system's design delta T."
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    gpm
  DEFAULT
    if( LocalStatus( CapRtd ) > 0 )
    then
      //  1 Btu/lb H2O/F, 8.343 # H20/gallon at 50F, 60 min/hr
      CapRtd / ( 1 * Parent(DsgnSupWtrDelT) * 8.343 * 60 )
    else UNDEFINED
    endif
  SIZING
    u:WtrFlowCap
  ANNUAL
    z:WtrFlowCap
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Location
RULE ThrmlEngyStor:StorLctn
  DESCRIPTION
    "The location of the thermal energy storage tank - used for calculating 
     heat gains/losses."
  HELP
    "The location from the options of Indoor, Outdoor or Underground."
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   Indoor
;   Outdoor
;   Underground
  DEFAULT
    if( Proj:AutoHardSize = 1 )
    then
      Underground
    else UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( StorLctn ) < 1 )
    then
      PostError( "A storage location must be provided for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:StorLctn
  ANNUAL
    z:StorLctn
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Location
RULE ThrmlEngyStor:StorLctnSim
  DESCRIPTION
    "The location of the thermal energy storage tank - used for setting  
     location variable in simulation."
  HELP
    "The location from the options of Zone or Outdoor."
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   Zone
;   Outdoor
  SIZING
    if( StorLctn = "Indoor" )
    then "Zone"
    else "Outdoor"
    endif
  ANNUAL
    z:StorLctnSim
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Location Zone Name
RULE ThrmlEngyStor:StorZnRef
  DESCRIPTION
    "The name of the zone where the thermal energy storage tank is located."
  INPUTCLASS
    CondRequired
  CHECKSIM
    if( StorLctn = "Indoor" .AND. LocalStatus( StorZnRef ) < 4 )
    then
      PostError( "A zone name must be provided for 
                  thermal storage system '%s'.",Name )
    else UNCHANGED
    endif
  SIZING
    if( StorLctn = "Indoor" )
    then
      u:StorZnRef
    else UNDEFINED
    endif
  ANNUAL
    z:StorZnRef
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Shape
RULE ThrmlEngyStor:TankShape
  DESCRIPTION
    "The shape of the energy storage system tank."
  HELP
    "The tank shape from the options of VerticalCylinder, HorizontalCylinder 
     or Rectangular."
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   VerticalCylinder
;   HorizontalCylinder
;   Rectangular
  DEFAULT
    if( Proj:AutoHardSize = 1 )
    then
      Rectangular
    else UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( TankShape ) < 1 )
    then
      PostError( "A tank shape must be specified for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:TankShape
  ANNUAL
    z:TankShape
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Shape for simulation
RULE ThrmlEngyStor:TankShapeSim
  DESCRIPTION
    "The shape of the energy storage system tank."
  HELP
    "The tank shape from the options of VerticalCylinder, HorizontalCylinder 
     or Other."
  INPUTCLASS
    NotInput
; OPTION
; Defined in BEMEnums, shown here for reference
;   VerticalCylinder
;   HorizontalCylinder
;   Other
  SIZING
    if( TankShape = "Rectangular" )
    then
      "Other"
    else
      TankShape
    endif
  ANNUAL
    z:TankShapeSim
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Volume
RULE ThrmlEngyStor:StorCap
  DESCRIPTION
    "The volume of water held in the thermal energy storage system tank."
  HELP
    "The tank volume and the rated capacity will determine how long the 
     storage system can meet the load."
  INPUTCLASS
    Required
  MINIMUM 
    0
  UNITS
    gal
  DEFAULT
    if( LocalStatus( WtrFlowCap ) > 0 )
    then
      WtrFlowCap * 5 * 60      // Assume 5 hours of capacity, 60 min/hr
    else UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( StorCap ) < 1 )
    then
      PostError( "A tank volume must be provided for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:StorCap
  ANNUAL
    z:StorCap
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Height
RULE ThrmlEngyStor:TankHgt
  DESCRIPTION
    "The height of the storage system tank."
  HELP
    "For a horizontal cylinder, the height will be the diameter of the tank."
  INPUTCLASS
    Required
  MINIMUM 
    0
  COMMONMINIMUM
    6
  UNITS
    ft
  DEFAULT
    if( Proj:AutoHardSize = 1 .AND. LocalStatus( StorCap ) > 0 )
    then
      //  0.13368 ft3/gallon 
      if( StorCap * 0.13368 > 8000 )          // at 8000 ft3, assume 20x20x20 cube
      then 20
      else if( StorCap * 0.13368 > 1000 )     // at 1000 ft3, assume 10x10x10 cube
      then 10
      else 5
      endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( TankHgt ) < 1 )
    then
      PostError( "A tank height must be provided for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
//  SIZING
//    u:TankHgt
  SIZING
    if( TankShape = "HorizontalCylinder" )
    then 
      (StorCap / 7.48) / (3.142 * TankHgt**2 / 4)
    else 
      TankHgt
    endif
  ANNUAL
    z:TankHgt
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Height for Simulation
//RULE ThrmlEngyStor:TankHgtSim
//  DESCRIPTION
//    "The height of the storage system tank - used for setting  
//     tank height variable in simulation"
//  HELP
//    "For a horizontal cylinder, the height will be the width of the tank 
//     for simulation."
//  INPUTCLASS
//    NotInput
//  UNITS
//    ft
//  SIZING
//    if( TankShape = "HorizontalCylinder" )
//    then 
//      (StorCap / 7.48) / (3.142 * TankHgt**2 / 4)
//    else 
//      TankHgt
//    endif
//ENDRULE
//
//
// -----------------------------------------------------------------------------
//        Tank Length to Width Ratio
RULE ThrmlEngyStor:TankLenToWdRat
  DESCRIPTION
    "The length to width ratio of a horizontal tank - used to calculate 
     surface area for heat gain/loss calculations."
  HELP
    "The length to width ratio of a rectangular storage tank in plan view.  If 
     the tank is square, the length to width ratio is 1.  For a rectangular 
     tank, the ratio will be greater than 1."
  INPUTCLASS
    CondRequired
  MINIMUM 
    1
  DEFAULT
    if( Proj:AutoHardSize = 1 .AND. LocalStatus( CapRtd ) > 0 )
    then
      1
    else UNDEFINED
    endif
  CHECKSIM
    if( TankShape = "Rectangular" .AND. LocalStatus( TankLenToWdRat ) < 1 )
    then
      PostError( "A length to width ratio must be provided for rectangular
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    if( TankShape = "Rectangular" )
    then
      u:TankLenToWdRat
    else UNDEFINED
    endif
  ANNUAL
    z:TankLenToWdRat
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Perimeter
RULE ThrmlEngyStor:TankPerim
  DESCRIPTION
    "The perimeter length of a rectangular tank in the hoizontal plane."
  INPUTCLASS
    NotInput
  UNITS
    ft
  SIZING
    if( TankShape = "Rectangular" .AND. 
        LocalStatus( CapRtd ) > 0 )
    then
        // convert volume to area using height, then solve for width to 
        // determine perimeter, using 7.48 gal/ft³
        //  footprint area = StorCap / 7.48 / TankHgt
        //  footprint area = L x W = (L/W * W) * W = L/W * W2
        //  W = sqrt( Area / L/W )
        //  L = W * L/W
        //  perim = ( W + L ) * 2 
      sqrt( StorCap / 7.48 / TankHgt / TankLenToWdRat ) * (1 + TankLenToWdRat ) * 2
    else UNDEFINED
    endif
  ANNUAL
    z:TankPerim
ENDRULE


// -----------------------------------------------------------------------------
//        Tank R-Value
RULE ThrmlEngyStor:TankR
  DESCRIPTION
    "The insulation applied to the tank."
  INPUTCLASS
    Required
  UNITS
    hr-°F-ft2/Btu
  DEFAULT
    if( Proj:AutoEffInput = 1 )
    then
      8
    else UNDEFINED
    endif
  CHECKSIM
    if( LocalStatus( TankR ) < 1 )
    then
      PostError( "A tank insulation R-value must be provided for 
                  thermal storage system '%s'.", Name )
    else UNCHANGED
    endif
  SIZING
    u:TankR
  ANNUAL
    z:TankR
ENDRULE


// -----------------------------------------------------------------------------
//        Vertical Surface Area of Tank
RULE NEW ThrmlEngyStor:TankVertSurfArea
  DATATYPE
    Float
  LONGFORM
    TankVerticalSurfaceArea
  DESCRIPTION
    "The vertical surface area of the tank."
  HELP
    "For a horizontal cylinder tank, the vertical surface area of the tank 
     will be sum of 2/4 of circumference multiplied by the length of the tank 
     and surface areas of circular shaped surfaces."
  INPUTCLASS
    NotInput
  UNITS
    ft2
  SIZING
    if( TankShape = "Rectangular" )
    then
      TankPerim * TankHgt
    else if( TankShape = "VerticalCylinder" )
    then
      // convert volume to area, then solve for width to determine perimeter, 
      // using 7.48 gal/ft³
      3.142 * SQRT( 4 * ((StorCap / 7.48) / TankHgt) / 3.142 ) * TankHgt
    else
      2 * (3.142 * TankHgt**2 / 4) + 
      (3.142 * TankHgt)  * ((StorCap / 7.48) / (3.142 * TankHgt**2 / 4 )) * (2 / 4)
    endif endif
  ANNUAL
    z:TankVertSurfArea
ENDRULE


// -----------------------------------------------------------------------------
//        Horizontal Surface Area of Tank 
RULE NEW ThrmlEngyStor:TankHorSurfArea
  DATATYPE
    Float
  LONGFORM
    TankHorizontalSurfaceArea
  DESCRIPTION
    "The horizontal surface area of the tank."
  HELP
    "For a horizontal cylinder tank, the horizontal surface area of the tank 
     will be 1/4 of circumference multiplied by the length of the tank."
  INPUTCLASS
    NotInput
  UNITS
    ft2
  SIZING
    if( TankShape = "Rectangular" )
    then
      // convert volume to area, then solve for width to determine perimeter, 
      // using 7.48 gal/ft³
      SQRT( StorCap / 7.48 / TankHgt / TankLenToWdRat )
      * SQRT( StorCap / 7.48 / TankHgt / TankLenToWdRat ) * TankLenToWdRat
    else if( TankShape = "VerticalCylinder" )
    then
      (StorCap / 7.48) / TankHgt
    else
      (3.142 * TankHgt) * ( (StorCap / 7.48) / ( 3.142 * TankHgt**2 / 4 ) ) * (1 / 4)
    endif endif
  ANNUAL
    z:TankHorSurfArea
ENDRULE


// -----------------------------------------------------------------------------
//        Width of Horizontal Surface of Tank 
RULE NEW ThrmlEngyStor:TankWd
  DATATYPE
    Float
  LONGFORM
    TankWidth
  DESCRIPTION
    "The width of a horizontal surface of the tank in plan view. The width 
     is always shorter than the length of the tank."
  INPUTCLASS
    NotInput
  UNITS
    ft
  SIZING
    if( TankShape = "Rectangular" )
    then
      min( SQRT( StorCap / 7.48 / TankHgt / TankLenToWdRat ), 
      SQRT( StorCap / 7.48 / TankHgt / TankLenToWdRat ) * TankLenToWdRat )
    else if( TankShape = "VerticalCylinder" )
    then
      SQRT( 4 * ((StorCap / 7.48) / TankHgt) / 3.142 )
    else
      // The width of a horizontal surface of horizontal cylinder tank is 
      // a minimum value between TankHgt and the length of the tank 
      min( TankHgt, ((StorCap / 7.48) / (3.142 * TankHgt**2 / 4 )) )
    endif endif
  ANNUAL
    z:TankWd
ENDRULE


// -----------------------------------------------------------------------------
//        Number of Nodes in the Stratified Tank
RULE ThrmlEngyStor:TankNodeCnt
  DESCRIPTION
    "The number of stratified nodes in the storage tank."
  HELP
    "The storage tank is divided into multiple nodes of equal volume. The nodes
     are coupled by vertical conduction effects, internode fluid flow, and 
     temperature inversion mixing. There must be at least 5 nodes. The maximum 
     number of nodes is 10. The larger number of nodes may slow down 
     the simulation but provide more accurate result."
  INPUTCLASS
    Required
  DEFAULT
    5
  SIZING
    u:TankNodeCnt
  ANNUAL
    z:TankNodeCnt
ENDRULE


// -----------------------------------------------------------------------------
//        Tank U-Factor
RULE ThrmlEngyStor:TankUFac
  DESCRIPTION
    "The U-Factor of the storage system tank."
  HELP
    "The design cooling capacity will determine the flow rate through the 
     system using the parent fluid system's design delta T."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr-°F-ft2
  SIZING
    if( StorLctn = "Indoor" )
    then
      1 / ( TankR + 0.68 )
    else if( StorLctn = "Outdoor" )
    then
      1 / ( TankR + 0.25 )
    else if( StorLctn = "Underground" )
    then
  		// The value of soil thermal conductivity varies widely with soil type 
  		// and moisture contents. 
      // A typical soil thermal conductivity is 0.8 Btu/hr-ft-°F [ASHRAE, 2013]
      // For a horizontal cylinder tank, the thickness of dirt above the top 
      // of the tank is assumed to be 2 feet. For a vertical cylinder tank 
      // and a rectangular tank, the thickness of dirt above the tank is 
      // assumed to be 1 feet. 
      if( TankShape = "HorizontalCylinder" )
      then
	      ( (2 * 0.8 / 3.142 / TankHgt) * log(TankHgt + 2 * 0.8 * TankR / 3.142) * TankVertSurfArea
	      + (2 * 0.8 / 3.142 / TankWd) * ( log((TankWd / 2) + (TankHgt / 2) 
	      + (0.8 * TankR / 3.142)) - log((TankHgt / 2) + (0.8 * TankR / 3.142)) ) * TankHorSurfArea
	      + (1 / ((1 / 4.0) + (2 / 0.8) + TankR)) * TankHorSurfArea )
	      / ( TankVertSurfArea + (2 * TankHorSurfArea) )
	    else
	      ( (2 * 0.8 / 3.142 / TankHgt) * log(TankHgt + 2 * 0.8 * TankR / 3.142) * TankVertSurfArea
	      + (2 * 0.8 / 3.142 / TankWd) * ( log((TankWd / 2) + (TankHgt / 2) 
	      + (0.8 * TankR / 3.142)) - log((TankHgt / 2) + (0.8 * TankR / 3.142)) ) * TankHorSurfArea
	      + (1 / ((1 / 4.0) + (1 / 0.8) + TankR)) * TankHorSurfArea )
	      / ( TankVertSurfArea + (2 * TankHorSurfArea) )
	    endif
    else UNDEFINED
    endif endif endif
  ANNUAL
    z:TankUFac
ENDRULE


// -----------------------------------------------------------------------------
//        Tank Setpoint Temperature
RULE ThrmlEngyStor:TankSetptTemp
  DESCRIPTION
    "The design temperature of the water in the tank, based 3°F below the fluid 
     system design supply water temperature."
  HELP
    "While the system capacity is based on the fluid system temperatures, the 
     tank will be charged to a slightly cooler temperature."
  INPUTCLASS
    NotInput
  CHECKSIM
    if( ParentStatus( DsgnSupWtrTemp ) = 0 )
    then
      PostError( "Chilled water system '%s' does not have a valid design 
                  supply water temperature, which is required for thermal 
                  storage system '%s'.  Please check your inputs.", 
                  Parent(Name), Name )
    else UNCHANGED
    endif
  SIZING
    Parent( DsgnSupWtrTemp ) - 3
  ANNUAL
    z:TankSetptTemp
ENDRULE


// -----------------------------------------------------------------------------
//        Operation Mode Schedule
RULE ThrmlEngyStor:ModeSchRef
  DESCRIPTION
    "A schedule that secifies when the thermal energy storage system will be 
     in one of three modes - charging, discharging, or off."
  HELP
    "Charging is when chilled water is being added to the tank to oool it down, 
     typically occuring during the night.  Discharging is when chilled water 
     is being extracted from the tank for supply to the coils in the building 
     to meet cooling loads, typically occuring during periods of high demand 
     charges from the utility, such as summer weekday afternoons.  The system 
     will be off between these periods, such as winter days, weekends, weekday mornings 
     and weekday evenings."
  INPUTCLASS
    Required
  CHECKSIM
    if( LocalStatus( ModeSchRef ) < 1 )
    then
      PostError( "A schedule specifying when thermal energy storage 
                  system '%s' is to be charged and discharged must be 
                  provided.", Name )
    else UNCHANGED
    endif
  SIZING
    u:ModeSchRef
  ANNUAL
    z:ModeSchRef
ENDRULE

// -------------------------------------------------
RULE ThrmlEngyStor:TypeRpt
  DESCRIPTION
    "A text string describing the thermal energy storage type"
  INPUTCLASS 
    NotInput
  ANNUAL_PROPOSED
    if( Type = "ChilledWater" ) then
      "Thermal Storage Chilled Water"
    else
      "NA"
    endif 
  ANNUAL_BASELINE
    "NA"
ENDRULE
