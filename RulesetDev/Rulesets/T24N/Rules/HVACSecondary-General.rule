// AirSystem - General
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:
//  Section 5.7.1 - Basic System Information
//      System Type
//      Air Distribution Type
//      Total Cooling Capacity


// Indicate whether CHECKCODE rules should be bypassed
RULE NEW AirSys:BypassCheckCode
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates CHECKCODE rules should be bypassed for the system and thier child objects."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. 
        ( IfValidAnd( IsNew > 0 ) .OR. IfValidAnd( IsAltered > 0 ) ) ) )
    then 1 // CompType does not include Mechanical, system will be purged
    else
    if( IfValidAnd( IsExisting > 0 ) )
    then 1 // Is an existing system
    else
    if( ( CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) = SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) 
          .AND. SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) > 0 ) .OR.
        ( CountRefs( ThrmlZn:VentSysRef ) = SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) 
          .AND. SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) > 0 ) .OR. 
        ( CountRefs( ThrmlZn:ExhSysRef ) = SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HasUnknownHVAC ) 
        .AND. SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HasUnknownHVAC ) > 0 ))
    then 1 // System only serves zone(s) with HasUnknownHVAC
    else
    if( IfValidAnd( IsHlthCareSys > 0 ) )
    then 2 // Is a new healthcare system
    else 0
    endif endif endif endif
ENDRULE
RULE NEW ZnSys:BypassCheckCode
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates CHECKCODE rules should be bypassed for the system and thier child objects."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND.
        ( IfValidAnd( IsNew > 0 ) .OR. IfValidAnd( IsAltered > 0 ) ) ) )
    then 1 // CompType does not include Mechanical, system will be purged
    else
    if( IfValidAnd( IsExisting > 0 ) )
    then 1 // Is an existing system
    else
    if( LocalCompAssigned( CtrlZnRef ) )
    then 
      if( CtrlZnRef:HasUnknownHVAC = 1 )
      then 1 // System only serves zone(s) with HasUnknownHVAC
      else 0
      endif
    else
    if( IfValidAnd( IsHlthCareSys > 0 ) )
    then 2 // Is a new healthcare system
    else 0
    endif endif endif endif
ENDRULE

// Indicate whether CHECKSIM rules should be bypassed
RULE NEW AirSys:BypassCheckSim
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates CHECKSIM rules should be bypassed for the system and thier child objects."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. 
        ( IfValidAnd( IsNew > 0 ) .OR. IfValidAnd( IsAltered > 0 ) ) ) )
    then 1 // CompType does not include Mechanical, system will be purged
    else
    if( ( CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) = SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) 
          .AND. SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) > 0 ) .OR.
        ( CountRefs( ThrmlZn:VentSysRef ) = SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) 
          .AND. SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) > 0 ) .OR. 
        ( CountRefs( ThrmlZn:ExhSysRef ) = SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) 
        .AND. SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) > 0 ))
    then 1 // System only serves zone(s) with HasUnknownHVAC
    else 0
    endif endif
ENDRULE
RULE NEW ZnSys:BypassCheckSim
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates CHECKSIM rules should be bypassed for the system and thier child objects."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. 
        ( IfValidAnd( IsNew > 0 ) .OR. IfValidAnd( IsAltered > 0 ) ) ) )
    then 1 // CompType does not include Mechanical, system will be purged
    else
    if( LocalCompAssigned( CtrlZnRef ) )
    then 
      if( CtrlZnRef:HasUnknownHVAC = 1 )
      then 1 // System only serves zone(s) with HasUnknownHVAC
      else 0
      endif
    else 0
    endif endif
ENDRULE



// ********** System Type ******************************************************
// =========================== AirSystem =======================================
RULE AirSys:Type
  DESCRIPTION
    "A unique descriptor which identifies high level attributes of a HVAC system."
  REFERENCE 
    NACM Section 5.7.1 
  INPUTCLASS 
    Compulsory
// Enums defined in BEMEnums.txt since property referenced by Library object
//OPTION
// - specify -
// PVAV
// VAV
// SZAC
// SZHP
// SZDFHP
// SZVAVAC
// SZVAVHP
// SZVAVDFHP
// HV
// Exhaust
// SPVAC
// SPVHP
// DOASCV
// DOASVAV
// VRF
// DEFAULT
//  - specify -
  RESETS
    ResetTheFollowingWhenThisIsModified
      DuctLctn
      DuctIns
      HERSDuctReq
      HERSDuctTested
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if (ServesThrmlZn > 0 .AND. ServesResZn > 0)
    then 
      PostError("AirSystem '%s' serves both residential common areas
                 (ResOtherZn) and (nonres) ThermalZones, which is not
                 allowed.", Name)
    else
    if( SumRevRef(ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:Area) = 0 .AND.
        SumRevRef(ThrmlZn:VentSysRef, ThrmlZn:Area) = 0 .AND.
        SumRevRef(ThrmlZn:ExhSysRef, ThrmlZn:Area) = 0 .AND.
        ServesResZn = 0 )
    then
      PostError("AirSystem '%s' is not referenced as Primary, Ventilation,
                 or Exhaust system by any ThermalZones.", Name)
    else 
    if( ( ChildCount( CoilHtg ) + ChildCount( CoilClg ) ) > 0 .AND.
        IsExhSys > 0 )
    then 
      PostError("AirSystem '%s' is defined as Type = 'Exhaust', but it has
                 heating and/or cooling coils. This is currently not supported.", 
                 Name)
    else UNCHANGED
    endif endif endif endif
  SIZING  
// AirSys:Type for baseline systems defined in Library_HVAC file
  ANNUAL
    z:Type
ENDRULE

// =========================== ZoneSystem ======================================
RULE ZnSys:Type
  DESCRIPTION
    "A unique descriptor which identifies high level attributes of a HVAC system."
  REFERENCE 
    NACM Section 5.7.1 
  INPUTCLASS 
    Compulsory
// Enums defined in BEMEnums.txt since property referenced by Library object
//OPTION
// - specify -
// SZAC
// SZHP
// PTAC
// PTHP
// FPFC
// WSHP
// Baseboard
// SPVAC
// SPVHP
// PassiveBeam
// Furnace
// VRF
// Exhaust
// VentilationOnly
//DEFAULT
//  "- specify -"
  RESETS
    ResetTheFollowingWhenThisIsModified
      DuctLctn
      DuctIns
      HERSDuctReq
      HERSDuctTested
  CHECKCODE
    if( BypassCheckCode > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( ( Type = "SZAC" .OR. Type = "SZHP" .OR. Type = "SZDFHP" ) .AND. 
        IfValidAnd( ClgCap >= 135000 ) )
    then
      PostError("ZoneSystem '%s' is Type = SZAC or SZHP, and has a cooling capacity
                 >135,000 Btu/hr. This is currently not supported. Use an AirSystem
                 for large AC/HP systems.", Name)
    else
    if( ( Type = "MiniSplitAC" .OR. Type = "MiniSplitHP" ) .AND. 
        IfValidAnd( ClgCap >= 135000 ) )
    then
       PostError("ZoneSystem '%s' is Type = MiniSplitAC or MiniSplitHP, and has a cooling capacity
                 >135,000 Btu/hr. This is currently not supported.", Name)
    else UNCHANGED
    endif endif endif
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( IfValidAnd( Type = "- specify -" ) )
    then
      PostError("ZoneSystem '%s' must have a Type specified.", Name) 
    else
    if( SumRevRef(ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:Area) = 0 .AND.
        SumRevRef(ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:Area) = 0 .AND.
        SumRevRef(ThrmlZn:VentSysRef, ThrmlZn:Area) = 0 .AND.
        SumRevRef(ThrmlZn:ExhSysRef, ThrmlZn:Area) = 0 )
    then // Changed from Error to warning since it is OK to have systems in model that are not referenced
      PostWarning("ZoneSystem '%s' is not referenced as a Primary, Ventilation,
                 or Exhaust system by any ThermalZones.", Name)
    else
    if( ( CoilHtgCnt + CoilClgCnt ) > 0 .AND. 
        ( Type = "VentilationOnly" .OR. Type = "Exhaust" ) )
    then 
      PostError("ZoneSystem '%s' is defined as Type = 'Exhaust' but has
                 heating and/or cooling coils. This is currently not supported.", 
                 Name)
    else
    if( HtPumpCap = 0 .AND. 
        ( Type = "SZHP" .OR. 
          Type = "SZDFHP" .OR. 
          Type = "PTHP" .OR. 
          Type = "WSHP" .OR. 
          Type = "SPVHP" .OR.
          Type = "MiniSplitHP" 
        ) )
    then
      PostError("ZoneSystem '%s' is defined as Type = '%s' but lacks a heating 
                 coil of Type = 'HeatPump', or has 0 Btu/h HP capacity
                 For this system Type, a 'HeatPump' coil with non-zero
                 capacity must be defined.", Name, Type)
    else  
    if( DXCap = 0 .AND.
        Type != "Baseboard" .AND. 
        Type != "FPFC" .AND.
        Type != "PassiveBeam" .AND.
        Type != "Radiant" .AND.
        Type != "Furnace" .AND.
        Type != "VRF" .AND.
        Type != "VentilationOnly" .AND.
        Type != "Exhaust" )
    then
      PostError("ZoneSystem '%s' is defined as Type = '%s' but lacks a cooling
                 coil of Type = 'DirectExpansion', or has 0 Btu/h DX capacity.
                 For this system type, a 'DirectExpansion' coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( VRFNetClgCap = 0 .AND. Type = "VRF" )
    then
      PostError("ZoneSystem '%s' is defined as Type = 'VRF' but lacks a cooling
                 coil of Type = 'VRF', or has 0 Btu/h capacity.
                 For this system type, a 'VRF' cooling coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( VRFNetHtgCap = 0 .AND. Type = "VRF" )
    then
      PostError("ZoneSystem '%s' is defined as Type = 'VRF' but lacks a heating
                 coil of Type = 'VRF', or has 0 Btu/h capacity.
                 For this system type, a 'VRF' heating coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "HotWater" ) = 0 .AND.
        SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "Resistance" ) = 0 .AND.
        Type = "FPFC" )
    then 
      PostError("ZoneSystem '%s' is Type = 'FPFC' but lacks a heating coil of
                 Type = 'HotWater'  or has 0 Btu/h heating capacity.
                 For this system Type, a 'Resistance' or 'HotWater' coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "HotWater" ) = 0 .AND.
        SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "Resistance" ) = 0 .AND.
        Type = "Baseboard" )
    then 
      PostError("ZoneSystem '%s' is Type = 'Baseboard' but does no have a heating coil of
                 Type = 'HotWater' or 'Resistance, or has 0 Btu/h heating capacity.
                 For this system type, a 'Resistance' or 'HotWater' coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( SumChildrenIf( CoilClg:CapTotGrossRtd, CoilClg:Type = "ChilledWater" ) = 0 .AND.
        Type = "PassiveBeam" )  // FPFC removed from criteria, see ticket 2705
    then 
      PostError("ZoneSystem '%s' is Type = '%s' but lacks a cooling coil of 
                 Type = 'ChilledWater', or has 0 Btu/h cooling capacity.
                 For this system type, a 'ChilledWater' coil with non-zero
                 capacity must be defined.", Name, Type)
    else
    if( SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "HotWater" ) = 0 .AND.
        SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "Resistance" ) = 0 .AND.
        SumChildrenIf( CoilClg:CapTotGrossRtd, CoilClg:Type = "ChilledWater" ) = 0 .AND.
        Type = "Radiant")
    then 
      PostError("ZoneSystem '%s' is Type = 'Radiant' but does no have a heating coil of
                 Type = 'HotWater' or 'Resistance, a cooling coil of Type = 'ChilledWater'
                 or has 0 Btu/h heating or cooling capacity.
                 For this system type, a 'Resistance' or 'HotWater' or 'ChilledWater' coil 
                 with non-zero capacity must be defined.", Name, Type)
    else
    if( SumChildrenIf( CoilHtg:CapTotGrossRtd, CoilHtg:Type = "Resistance" ) > 0 .AND.
        SumChildrenIf( CoilClg:CapTotGrossRtd, CoilClg:Type = "ChilledWater" ) > 0 .AND.
        Type = "Radiant")
    then 
      PostError("ZoneSystem '%s' is Type = 'Radiant' but have a heating coil of
                 Type = 'Resistance' and a cooling coil of Type = 'ChilledWater'.
                 This is currently not supported.", Name, Type)
    else
    if( CoilHtgCnt = 0 .AND.
        Type != "PassiveBeam" .AND.
        Type != "Radiant" .AND. 
        Type != "VentilationOnly" .AND.
        Type != "Exhaust" )
    then 
      PostError("ZoneSystem '%s' is Type = '%s', but has no heating coil.
                 This is currently not supported. If the system has no heating coil,
                 model a 'Resistance' heating coil with 1 Btu/hr capacity,
                 or use the AirSystem object.", Name, Type)   
    else
    if( CoilClgCnt = 0 .AND. 
        Type != "Baseboard" .AND. 
        Type != "Furnace" .AND. 
        Type != "FPFC" .AND. 
        Type != "PassiveBeam" .AND. 
        Type != "Radiant" .AND.
        Type != "VentilationOnly" .AND.
        Type != "Exhaust" )
    then 
      PostError("ZoneSystem '%s' is Type = '%s', but has no cooling coil.
                 This is currently not supported. If the system has no cooling coil,
                 model the appropriate Type cooling coil with 1 Btu/hr capacity,
                 or use the AirSystem object.", Name, Type)
    else                 
    if( Type = "PassiveBeam" .AND. CoilHtgCnt > 0 )
    then 
      PostError("ZoneSystem '%s' is Type = '%s', but has been defined
                 with a child heating coil. This is currently 
                 not supported.", Name, Type)
    else
    if( ( Type = "Baseboard" .OR. Type = "Furnace" ) .AND. CoilClgCnt > 0 )
    then 
      PostError("ZoneSystem '%s' is Type = '%s', but has been defined
                 with a child cooling coil object. This is currently 
                 not supported.", Name, Type)   
    else    
    if( ( Type = "PassiveBeam" .OR. Type = "Radiant" .OR. Type = "Baseboard" .OR. Type = "Furnace" ) .AND.
         FanCnt > 0 )
    then
      if( Type = "Furnace" .AND. IfValidAnd( FurnaceType = "Fan" ) )
      then UNCHANGED    
      else
      if( Type = "Furnace" .AND. IfValidAnd( FurnaceType = "Gravity" ) )
      then
        PostError("ZoneSystem '%s' is Type = 'Furnace' and specified as 'Gravity'.
                   A fan should not be defined for this configuration.", Name)       
      else
        PostError("ZoneSystem '%s' is Type = '%s' but has a child Fan
                   object. This is currently not supported.", Name, Type) 
      endif endif
    else
    if( FanCnt = 0 .AND.
        Type != "Baseboard" .AND. 
        Type != "PassiveBeam" .AND.
        Type != "Radiant" .AND. 
        Type != "VentilationOnly" )
    then
      if( Type = "Furnace" )
      then
        if( IfValidAnd( FurnaceType = "Fan" ) )
        then 
          PostError("ZoneSystem '%s' is Type = 'Furnace' and specified as having a 'Fan',
                     but has no child Fan object.", Name)    
        else UNCHANGED
        endif
      else 
        PostError("ZoneSystem '%s' is Type = '%s', but has no child Fan object.", 
                  Name, Type)     
      endif
    else UNCHANGED
    endif endif endif endif endif
    endif endif endif endif endif 
    endif endif endif endif endif
    endif endif endif endif
  SIZING
// ZnSys:Type for baseline systems defined in Library_HVAC file
  ANNUAL
    z:Type
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is modeled as an exhaust system
RULE NEW AirSys:IsExhSys
  DATATYPE
    Integer
  LONGFORM
    IsExhaustSystem
  DESCRIPTION
    "An integer flag that indicates the system Type is 'Exhaust'."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Exhaust" )
    then 1
    else 0
    endif
  SIZING
    if( Type = "Exhaust" )
    then 1
    else 0
    endif
  ANNUAL
    z:IsExhSys
ENDRULE
RULE NEW ZnSys:IsExhSys
  DATATYPE
    Integer
  LONGFORM
    IsExhaustSystem
  DESCRIPTION
    "An integer flag that indicates the system Type is 'Exhaust'
     or is modelled as an exhaust fan."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Exhaust" .OR. 
        ( Type = "VentilationOnly" .AND. ExhSysType = "ExhaustOnly" ) )
    then 1
    else 0
    endif
  SIZING
    if( Type = "Exhaust" .OR. 
        ( Type = "VentilationOnly" .AND. ExhSysType = "ExhaustOnly" ) )
    then 1
    else 0
    endif
  ANNUAL
    z:IsExhSys
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is referenced as an exhaust system and serves and residential zone
RULE NEW AirSys:IsResExhSys
  DATATYPE
    Integer
  LONGFORM
    IsResidentialExhaustSystem
  DESCRIPTION
    "An integer flag that indicates the system Type is 'Exhaust' and serves a 
     highrise residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsExhSys > 0 .AND. CountRefs( ThrmlZn:ExhSysRef ) > 0 )
    then 
      if( SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:PropVentSysIsExh ) /
          CountRefs( ThrmlZn:ExhSysRef ) = 2 )
      then 1
      else 0
      endif
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsResExhSys
  DATATYPE
    Integer
  LONGFORM
    IsResidentialExhaustSystem
  DESCRIPTION
    "An integer flag that indicates the system Type is 'Exhaust' and serves a 
     highrise residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsExhSys > 0 .AND. CountRefs( ThrmlZn:ExhSysRef ) > 0 )
    then 
      if( SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:PropVentSysIsExh ) /
          CountRefs( ThrmlZn:ExhSysRef ) = 2 )
      then 1
      else 0
      endif
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a ventialtion for a high-rise residential zone (exhaust or otherwise)
RULE NEW AirSys:IsHRRVentSys
  DATATYPE
    Integer
  LONGFORM
    IsHighRiseResidentialVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is the ventilation system of a 
     high-rise residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsResExhSys > 0 .OR. 
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsHighRiseRes ) > 0 )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsHRRVentSys
  DATATYPE
    Integer
  LONGFORM
    IsHighRiseResidentialVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is the ventilation system of a 
     high-rise residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsResExhSys > 0 .OR. 
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsHighRiseRes ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a ventilation for a high-rise residential zone (exhaust or otherwise)
RULE NEW AirSys:IsUnitaryHRRVentSys
  DATATYPE
    Integer
  LONGFORM
    UnitaryHighRiseResidentialVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is a unitary ventilation system of a 
     high-rise residential zone (i.e. ventilation system only serves one dwelling unit)."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsHRRVentSys > 0 )
    then 
      if( IsResExhSys > 0 )
      then // Is exhaust system
        if( Cnt = SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:ResLivingUnitCntWithMult ) )
        then 1 // # of systems is 1:1 system to dwelling units
        else 0
        endif
      else // other systems
      if( Cnt = SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResLivingUnitCntWithMult ) )
      then 1 // # of systems is 1:1 system to dwelling units
      else 0
      endif endif
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsUnitaryHRRVentSys
  DATATYPE
    Integer
  LONGFORM
    UnitaryHighRiseResidentialVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is a unitary ventilation system of a 
     high-rise residential zone (i.e. ventilation system only serves one dwelling unit)."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsHRRVentSys > 0 )
    then 
      if( IsResExhSys > 0 )
      then // Is exhaust system
        if( Cnt = SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:ResLivingUnitCntWithMult ) )
        then 1 // # of systems is 1:1 system to dwelling units
        else 0
        endif
      else // other systems
      if( Cnt = SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResLivingUnitCntWithMult ) )
      then 1 // # of systems is 1:1 system to dwelling units
      else 0
      endif endif
    else 0
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Whether system serves a residential zone
RULE NEW AirSys:IsResSys
  DATATYPE
    Integer
  LONGFORM
    IsResidentialSystem
  DESCRIPTION
    "An integer flag that indicates the system is not 'Exhaust' and serves a 
     Residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsExhSys = 0 .AND. 
        ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:ResCondFlrAreaWithMult ) > 0 ) )
    then 1
    else 0
    endif
  ANNUAL
    if( IsExhSys = 0 .AND. 
        ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:ResCondFlrAreaWithMult ) > 0 ) )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsResSys
  DATATYPE
    Integer
  LONGFORM
    IsResidentialSystem
  DESCRIPTION
    "An integer flag that indicates the system is not 'Exhaust' and serves a 
     Residential ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( ( IsExhSys = 0 .OR. ( Type = "VentilationOnly" .AND. ExhSysType != "ExhaustOnly" ) ) .AND.
        ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:SimSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:SimSysRef[2], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsRes ) > 0 ) )
    then 1
    else 0
    endif
  ANNUAL
    if( ( IsExhSys = 0 .OR. ( Type = "VentilationOnly" .AND. ExhSysType != "ExhaustOnly" ) ) .AND.
        ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:SimSysRef[1], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:SimSysRef[2], ThrmlZn:IsRes ) > 0 .OR.
          SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsRes ) > 0 ) )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a ventilation only system
RULE NEW AirSys:IsVentOnlySys
  DATATYPE
    Integer
  LONGFORM
    IsVentilationOnlySystem
  DESCRIPTION
    "An integer flag that indicates the system is only referenced at
     ThermalZones as a VentSysRef."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:FlrArea ) = 0 .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:FlrArea ) > 0 )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsVentOnlySys
  DATATYPE
    Integer
  LONGFORM
    IsVentilationOnlySystem
  DESCRIPTION
    "An integer flag that indicates the system is only referenced at
     ThermalZones as a VentSysRef."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:FlrArea ) = 0 .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:FlrArea ) = 0 .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:FlrArea ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a balanced ventilation system
RULE NEW AirSys:IsBalancedVentSys
  DATATYPE
    Integer
  LONGFORM
    IsBalancedVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is a balanced ventilation system."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsVentOnlySys > 0 )
    then
      if( IfValidAnd( SupFanCap > 0 ) .AND.
          IfValidAnd( RetFanCap > 0 ) )
      then
// Ticket 3057: System is balanced if supply/return or exhaust are within specified tolerance
        if( ( SupFanCap - RetFanCap ) / SupFanCap > Proj:BalancedVentTolRes )
        then 0
        else 1
        endif 
      else
      if( IfValidAnd( SupFanCap > 0 ) .AND.
          IfValidAnd( ExhFanCap > 0 ) )
      then
        if( ( SupFanCap - ExhFanCap ) / SupFanCap > Proj:BalancedVentTolRes )
        then 0
        else 1
        endif 
      else 0
      endif endif
    else 0
    endif
ENDRULE
RULE NEW ZnSys:IsBalancedVentSys
  DATATYPE
    Integer
  LONGFORM
    IsBalancedVentilationSystem
  DESCRIPTION
    "An integer flag that indicates the system is a balanced ventilation system."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsVentOnlySys > 0 )
    then
      if( Type = "VentilationOnly" .AND. 
          ( IfValidAnd( ExhSysType = "Balanced" ) .OR.
            IfValidAnd( ExhSysType = "HeatRecovery" ) ) )
      then 1 // These two ExhSysTypes are inherently assumed to be balanced
      else 0
      endif
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a multi-zone system
RULE NEW AirSys:IsMultiZnSys
  DATATYPE
    Integer
  LONGFORM
    IsMultiZoneSystem
  DESCRIPTION
    "A flag that indicates if the AirSys:Type is classified as a 'multi-zone' 
     system for rules."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "PVAV" .OR. 
        Type = "VAV" .OR. 
        Type = "DOASCV" .OR. 
        Type = "DOASVAV" )
    then 1 
    else 0
    endif    
  SIZING
    if( Type = "PVAV" .OR. 
        Type = "VAV" .OR. 
        Type = "DOASCV" .OR. 
        Type = "DOASVAV" )
    then 1
    else 0
    endif  
ENDRULE
RULE NEW ZnSys:IsMultiZnSys
  DATATYPE
    Integer
  LONGFORM
    IsMultiZoneSystem
  INPUTCLASS
    NotInput
  DEFAULT
    0
  SIZING
    0
ENDRULE

// -----------------------------------------------------------------------------
// Whether system is a single-zone system
RULE NEW AirSys:IsSglZnSys
  DATATYPE
    Integer
  LONGFORM
    IsSingleZoneSystem
  DESCRIPTION
    "A flag that indicates if the AirSys:Type is classified as a 'single-zone' 
     system for rules."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsMultiZnSys = 0 ) then 1 else 0 endif
  SIZING
    if( IsMultiZnSys = 0 ) then 1 else 0 endif
ENDRULE
RULE NEW ZnSys:IsSglZnSys
  DATATYPE
    Integer
  LONGFORM
    IsSingleZoneSystem
  INPUTCLASS
    NotInput
  DEFAULT
    1
  SIZING
    1
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:IsVAVSys
  DATATYPE
    Integer
  LONGFORM
    IsVariableAirVolumeSystem
  DESCRIPTION
    "A flag that indicates if the AirSys:Type is classified as a 'variable air volume' 
     system for rules."
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalStatus( Type ) = 0 )
    then 0
    else if( IsExhSys > 0 .AND. ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
    then 1
    else
      switch( Type )
        case "PVAV"      :  1
        case "VAV"       :  1
        case "SZVAVAC"   :  1
        case "SZVAVHP"   :  1
        case "SZVAVDFHP" :  1
        case "DOASVAV"   :  1
        default          :  0
      endswitch
    endif endif
  SIZING
    if( IsExhSys > 0 .AND. ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
    then 1
    else
    switch( Type )
      case "PVAV"      :  1
      case "VAV"       :  1
      case "SZVAVAC"   :  1
      case "SZVAVHP"   :  1
      case "SZVAVDFHP" :  1
      case "DOASVAV"   :  1
      default          :  0
    endswitch
    endif
ENDRULE
RULE NEW ZnSys:IsVAVSys
  DATATYPE
    Integer
  LONGFORM
    IsVariableAirVolumeSystem
  DESCRIPTION
    "A flag that indicates if the ZnSys:Type is classified as a 'variable air volume' 
     system for rules."
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalStatus( Type ) = 0 )
    then 0
    else if( IsExhSys > 0 .AND. ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
    then 1
    else if( Type = "FPFC" .AND. SumChildrenIf( Fan:FlowCap, Fan:CtrlMthd = "VariableSpeedDrive" ) )
    then 1
    else 0
    endif endif endif
  SIZING
    if( IsExhSys > 0 .AND. ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
    then 1
    else if( Type = "FPFC" .AND. SumChildrenIf( Fan:FlowCap, Fan:CtrlMthd = "VariableSpeedDrive" ) )
    then 1
    else 0
    endif endif
ENDRULE
  
// -----------------------------------------------------------------------------
// Is a health care system
RULE AirSys:IsHlthCareSys
  DESCRIPTION
    "A flag that indicates if the AirSys:Type is classified as a 'health care'
     system for compliance."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT : T24N_2016 S901G
    0 
  DEFAULT : T24N
    if( ServesThrmlZn .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:VentSysRef ) .AND.
        SumRevRef( ThrmlZn:ExhSysRef[1], ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:ExhSysRef[1] ) )
    then 1
    else 0
    endif
  CHECKCODE : T24N_2016 S901G
    UNCHANGED
  CHECKCODE : T24N
    if( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsHlthCare ) +
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsHlthCare ) +
        SumRevRef( ThrmlZn:ExhSysRef[1], ThrmlZn:IsHlthCare ) > 0 .AND.
        IsHlthCareSys = 0 )
    then
      PostError("AirSystem '%s' serves a combination of healthcare and non-healthcare 
                 zones. This is not allowed for compliance.",Name)
    else UNCHANGED
    endif 
ENDRULE
RULE ZnSys:IsHlthCareSys
  DESCRIPTION
    "A flag that indicates if the ZnSys:Type is classified as a 'health care'
     system for compliance."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT : T24N_2016 S901G
    0 
  DEFAULT : T24N
    if( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:PriAirCondgSysRef[2] ) .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:VentSysRef ) .AND.
        SumRevRef( ThrmlZn:ExhSysRef[1], ThrmlZn:IsHlthCare ) = 
          CountRefs( ThrmlZn:ExhSysRef[1] ) )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Is a T24 'Retail' system
RULE NEW AirSys:IsRetailBaseSys
  DATATYPE
    Integer
  LONGFORM
    IsRetailBaselineSystem
  DESCRIPTION
    "A flag that indicates the AirSys is a baseline 'Retail' system."
  INPUTCLASS
    NotInput
  SIZING
    if( ( SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:RetailAreaNew ) + 
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:RetailAreaAltered ) +
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:RetailAreaExisting ) ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Is a T24 'School' system
RULE NEW AirSys:IsSchoolBaseSys
  DATATYPE
    Integer
  LONGFORM
    IsSchoolBaselineSystem
  DESCRIPTION
    "A flag that indicates the AirSys is a baseline 'School' system."
  INPUTCLASS
    NotInput
  SIZING
    if( ( SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:SchoolAreaNew ) + 
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:SchoolAreaAltered ) +
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:SchoolAreaExisting ) ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Is a T24 'Office' system
RULE NEW AirSys:IsOfficeBaseSys
  DATATYPE
    Integer
  LONGFORM
    IsOfficeBaselineSystem
  DESCRIPTION
    "A flag that indicates the AirSys is a baseline 'Office' system."
  INPUTCLASS
    NotInput
  SIZING
    if( ( SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:OfficeAreaNew ) + 
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:OfficeAreaAltered ) +
          SumRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:OfficeAreaExisting ) ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Is a T24 'Office' system in a Warehouse
RULE NEW AirSys:IsWarehouseOfficeBaseSys
  DATATYPE
    Integer
  LONGFORM
    IsWarehouseOfficeBaselineSystem
  DESCRIPTION
    "A flag that indicates the AirSys is a baseline 'Office' system in a warehouse building."
  INPUTCLASS
    NotInput
  SIZING
    if( Bldg:IsWarehouseBldg > 0 .AND. IfValidAnd( BaseSysNum = 7 ) )
    then 1
    else 0
    endif
ENDRULE

// =========================== ZoneSystem Type Report ==========================
RULE ZnSys:TypeRpt
  RULESETS
    T24N
  DESCRIPTION
    "Translates Type to TypeRpt."
  INPUTCLASS 
    NotInput
  ANNUAL_PROPOSED
    switch( Type )
      case "SZAC"        : "SZAC" 
      case "SZHP"        : "SZHP"
      case "SZDFHP"      : "SZHP"
      case "PTAC"        : "PTAC" 
      case "PTHP"        : "PTHP"   
      case "FPFC"        : "FPFC"
      case "Baseboard"   : "Baseboard"
      case "WSHP"        : "WSHP"
      case "Exhaust"     : "Exhaust"
      case "VRF"         : "VRF"
      case "MiniSplitAC" : "MiniSplitAC"
      case "MiniSplitHP" : "MiniSplitHP"
      case "PassiveBeam" : "PassiveBeam"
      case "Furnace"     : "Furnace"
      default : UNDEFINED
    endswitch
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE

  

// ********** Status ***********************************************************
// =========================== AirSystem =======================================
RULE AirSys:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  HELP
    "The following criteria should be used to determine the status of the system:


     New:      The system and all of it's components are completely 'New'.


     Existing: All of the system components (Fans, Coils, OutsideAirControl, etc),
               not including TerminalUnits with cooling coils, are 'Existing'. 
               If the system has a mix of 'Existing' and 'New' TerminalUnits, 
               and the system's cooling capacity and/or efficiency has not changed,
               the system should be classified as 'Existing'.


     Altered:  The system is 'Existing', but some components (other than only
               TerminalUnits) have been modified or replaced such that the cooling
               capacity and/or cooling efficiency of the system, or in the case
               of chilled water coils, the cooling capacity and/or efficiency 
               of the plant, has changed."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New' 
;   New
;   Existing 
;   Altered
  DEFAULT
// Status defined from top-down
    if( Proj:IsAlt = 1 )
    then "Existing"
    else "New"
    endif
  CHECKCODE
    if( ( Proj:IsNewMech > 0 .AND. Status != "New" ) .OR.
        ( Proj:IsAlt = 0 .AND. Status = "Altered" ) )
    then
      PostWarning("AirSystem '%s' has a Status of '%s', but ComplianceType is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else UNCHANGED
    endif
  SIZING
    if( IfValidAnd( BaseSysNum > 0 ) .OR. Proj:IsNewMech > 0 )
    then "New"
    else Status
    endif
  ANNUAL
    z:Status
ENDRULE

RULE NEW AirSys:IsFuture
  DATATYPE
    Integer  
  DESCRIPTION
    "The flag that indicates the system serves nothing but zones that are HasUnknownHVAC."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( ServesThrmlZn .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:VentSysRef ) .AND.
        SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:ExhSysRef ) )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:NotEnclosedSys
  RULESETS
    S901G ECBC
  DATATYPE
    Integer
  LONGFORM
    NotEnclosedSystem
  INPUTCLASS
    NotInput
  DESCRIPTION
    "A flag that is used to indicate if the system serves zone(s) classified
     as 'NotEnclosed'."
  DEFAULT
    if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:NotEnclosedZn ) = 1 .OR.
        MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:NotEnclosedZn ) = 1 )
    then 1
    else 0
    endif
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( NotEnclosedSys )
    then 
      if( ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:NotEnclosedZn ) !=
            CountRefs( ThrmlZn:PriAirCondgSysRef[1], 1 ) )
           .OR.
          ( SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:NotEnclosedZn ) !=
            CountRefs( ThrmlZn:VentSysRef ) ) )
      then 
        PostError("AirSystem '%s' serves a combination of 'NotEnclosed' and
                   'Enclosed' zones. This is currently not supported.
                   NotEnclosed zones include mechanically or naturally ventilated
                   parking garages, attics, and crawlspaces.", Name )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
ENDRULE

RULE NEW AirSys:IsCondgSys
  DATATYPE
    Integer
  LONGFORM
    ConditioningSystem
  INPUTCLASS
    NotInput
  DESCRIPTION
    "A flag that is used to indicate if the system serves zone(s) classified
     as 'Conditioned'."
  DEFAULT
    if( IsExhSys > 0 )
    then 0
    else
    if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ) = 1 .OR.
        MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) = 1 ) 
    then 1
    else
    if (ServesResZn)
    then 1
    else 0
    endif endif endif
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( IsCondgSys > 0 )
    then     
      if( ( ServesThrmlZn .AND.
            ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ) !=
              CountRefs( ThrmlZn:PriAirCondgSysRef[1], 1 ) ) ) .OR.
          ( ServesResZn .AND.
              SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:CondFlrAreaWithMult ) !=
              SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:FlrAreaWithMult )   ) )
;          .OR.
 ;        ( SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) !=
 ;          CountRefs( ThrmlZn:VentSysRef ) ) )
      then 
        PostError("AirSystem '%s' serves a combination of 'Conditioned' and
                   'Unconditioned' zones. This is currently not supported.", Name )
      else UNCHANGED
      endif  
    else UNCHANGED
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW AirSys:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Existing" .AND. Proj:IsAddOrAlt > 0 ) 
    then 1 
    else 0 
    endif
  SIZING
    if( Status = "Existing" .AND. Proj:IsAddOrAlt > 0 ) 
    then 1 
    else 0 
    endif
  ANNUAL
    z:IsExisting
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:IsAltered
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" .AND. Proj:IsAddOrAlt > 0 ) 
    then 1
    else
    if( IsExisting > 0 )
    then 
      if( SumChildrenIf( CoilClg:CapHydronic, CoilClg:CapHydronic > 0 ) > 0 .AND.
          IfValidAnd( Bldg:PlantClgCapNew > 0 ) )
      then // System has hydronic cooling coils that are served by a new plant
        1
      else
      if( IfValidAnd( Bldg:TotClgCap = 0 ) .AND.
          SumChildrenIf( CoilHtg:CapHydronic, CoilHtg:CapHydronic > 0 ) > 0 .AND.
          IfValidAnd( Bldg:PlantHtgCapNew > 0 ) )
      then // Building is heating only, and this system has hydronic heating coils
           // that are served by a new plant
        1
      else 0
      endif endif
    else 0
    endif endif
  SIZING
    if( Status = "Altered" .AND. Proj:IsAddOrAlt > 0 ) 
    then 1
    else
    if( IsExisting > 0 )
    then 
      if( SumChildrenIf( CoilClg:CapHydronic, CoilClg:CapHydronic > 0 ) > 0 .AND.
          Bldg:PlantClgCapNew > 0 )
      then // System has hydronic cooling coils that are served by a new plant
        1
      else
      if( Bldg:TotClgCap = 0 .AND.
          SumChildrenIf( CoilHtg:CapHydronic, CoilHtg:CapHydronic > 0 ) > 0 .AND.
          Bldg:PlantHtgCapNew > 0 )
      then // Building is heating only, and this system has hydronic heating coils
           // that are served by a new plant
        1
      else 0
      endif endif
    else 0
    endif endif
  ANNUAL
    z:IsAltered
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech > 0 )
    then 1
    else if( IsAltered > 0 )
    then 1 // 'Altered' systems are considered 'New'
    else 0
    endif endif
  SIZING : T24N
    if( Status = "New" .OR. Proj:IsNewMech > 0 )
    then 1
    else if( IsAltered > 0 )
    then 1 // 'Altered' systems are considered 'New'
    else 0
    endif endif
  SIZING : S901G ECBC
    if( Status = "New" .AND. IfValidAnd( NotEnclosedSys = 1 ) = 0 ) 
    then 1
    else 0
    endif
  ANNUAL
    z:IsNew
ENDRULE


// =========================== ZnSystem ========================================
RULE ZnSys:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  HELP
    "The following criteria should be used to determine the status of the system:


     New:      The system and all of it's components are completely 'New'.


     Existing: All of the system components (Fans and Coils) are 'Existing'. 


     Altered:  The system is 'Existing', but some components have been modified
               or replaced such that the cooling capacity and/or cooling efficiency
               of the system, or in the case of chilled water coils, the cooling
               capacity and/or efficiency of the plant, has changed."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New' 
;   New
;   Existing 
;   Altered
  DEFAULT
// Status defined from top-down
    if( Proj:IsAlt = 1 )
    then "Existing"
    else
    if( Type = "VRF" .AND. LocalCompAssigned( VRFSysRef ) )
    then VRFSysRef:Status
    else "New" 
    endif endif
  CHECKCODE
    if( Proj:IsNewMech > 0 .AND. Status != "New"  )
    then
      PostWarning("ZoneSystem '%s' has a Status of '%s', but Compliance Type is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else
    if( Type = "VRF" )
    then
      if ( LocalCompAssigned( VRFSysRef ) = 0 )
      then UNCHANGED
      else 
      if( Status != VRFSysRef:Status )
      then 
        PostWarning("VRF indoor unit '%s' has a Status of '%s', but the outdoor unit Status is '%s'.
                     The status of the indoor unit will be changed to '%s' for compliance
                     analysis.", Name, Status, VRFSysRef:Status, VRFSysRef:Status )
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif
  SIZING
    if( Type = "VRF" )
    then VRFSysRef:Status
    else UNCHANGED
    endif  
// For other objects, SIZING and ANNUAL rules not used; status of any new objects created by rules 
// is defined by BEMEnums default
ENDRULE

RULE NEW ZnSys:IsFuture
  DATATYPE
    Integer  
  DESCRIPTION
    "The flag that indicates the system serves nothing but zones that are HasUnknownHVAC."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:PriAirCondgSysRef[2] ) .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:VentSysRef ) .AND.
        SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HasUnknownHVAC ) = CountRefs( ThrmlZn:ExhSysRef ) )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:NotEnclosedSys
  RULESETS
    S901G ECBC
  DATATYPE
    Integer
  LONGFORM
    NotEnclosedSystem
  INPUTCLASS
    NotInput
  DESCRIPTION
    "A flag that is used to indicate if the system serves a zone that is classified
     as 'NotEnclosed'."
  DEFAULT
    if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:NotEnclosedZn ) = 1 .OR.
        MaxRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:NotEnclosedZn ) = 1 .OR.
        MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:NotEnclosedZn ) = 1 )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ZnSys:IsCondgSys
  DATATYPE
    Integer
  LONGFORM
    ConditioningSystem
  INPUTCLASS
    NotInput
  DESCRIPTION
    "A flag that is used to indicate if the system serves zone(s) classified
     as 'Unconditioned'."
  DEFAULT
    if( Type = "Exhaust" .OR. Type = "VentilationOnly" )
    then 0
    else
    if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ) = 1 .OR.
        MaxRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:IsCond ) = 1 .OR.
        MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) = 1 )
    then 1
    else 0
    endif endif
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( IsCondgSys > 0 )
    then 
      if( ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ) !=
            CountRefs( ThrmlZn:PriAirCondgSysRef[1], 1 ) )
           .OR.
          ( SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:IsCond ) !=
            CountRefs( ThrmlZn:PriAirCondgSysRef[2], 1 ) )
           .OR.
          ( SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) !=
            CountRefs( ThrmlZn:VentSysRef ) ) )
      then 
        PostError("ZoneSystem '%s' serves a combination of 'Conditioned' and
                   'Unconditioned' zones. This is currently not supported.", Name )
      else UNCHANGED
      endif
   else UNCHANGED
   endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Existing" .AND. Proj:IsAlt > 0 ) 
    then 1 
    else 0 
    endif
  SIZING
    if( Status = "Existing" .AND. Proj:IsAlt > 0 ) 
    then 1 
    else 0 
    endif
  ANNUAL
    z:IsExisting
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:IsAltered
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" .AND. Proj:IsAlt > 0 ) 
    then 1
    else
    if( IsExisting > 0 )
    then 
      if( SumChildrenIf( CoilClg:CapHydronic, CoilClg:CapHydronic > 0 ) > 0 .AND.
          IfValidAnd( Bldg:PlantClgCapNew > 0 ) )
      then // System has hydronic cooling coils that are served by a new plant
        1
      else
      if( IfValidAnd( Bldg:TotClgCap = 0 ) .AND.
          SumChildrenIf( CoilHtg:CapHydronic, CoilHtg:CapHydronic > 0 ) > 0 .AND.
          IfValidAnd( Bldg:PlantHtgCapNew > 0 ) )
      then // Building is heating only, and this system has hydronic heating coils
           // that are served by a new plant
        1
      else 0
      endif endif 
    else 0
    endif endif
  SIZING
    if( Status = "Altered" .AND. Proj:IsAlt > 0 ) 
    then 1
    else
    if( IsExisting > 0 )
    then 
      if( SumChildrenIf( CoilClg:CapHydronic, CoilClg:CapHydronic > 0 ) > 0 .AND.
          Bldg:PlantClgCapNew > 0 )
      then // System has hydronic cooling coils that are served by a new plant
        1
      else
      if( Bldg:TotClgCap = 0 .AND.
          SumChildrenIf( CoilHtg:CapHydronic, CoilHtg:CapHydronic > 0 ) > 0 .AND.
          Bldg:PlantHtgCapNew > 0 )
      then // Building is heating only, and this system has hydronic heating coils
           // that are served by a new plant
        1
      else 0
      endif endif 
    else 0
    endif endif
  ANNUAL
    z:IsAltered
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech > 0 )
    then 1
    else if( IsAltered > 0 )
    then 1 // 'Altered' systems are considered 'New'
    else 0
    endif endif
  SIZING : T24N
    if( Status = "New" .OR. Proj:IsNewMech > 0 )
    then 1
    else if( IsAltered > 0 )
    then 1 // 'Altered' systems are considered 'New'
    else 0
    endif endif
  SIZING : S901G ECBC
    if( Status = "New" .AND. IfValidAnd( NotEnclosedSys = 1 ) = 0 ) 
    then 1
    else 0
    endif
  ANNUAL
    z:IsNew
ENDRULE


// ********** System Count *****************************************************
// =========================== AirSystem =======================================
RULE AirSys:Cnt
  DESCRIPTION
    "The number of duplicate systems represented by the current AirSystem."
  HELP
    "The number of duplicate systems (i.e. Count > 1) is limited to the
     following individual scenarios:

     a) For single-zone systems (SZAZ/HP, SZVAVAC/HP, HV), all attributes of 
     the system are the same and the AirSystem represents multiple
     systems that serve a ThermalZone with an HVACZoneCount > 1. In this
     scenario, the ThermalZone:HVACZoneCount and AirSystem:Count should be 
     consistent with eachother. 

     b) For multi-zone systems (PVAV and VAV), all attributes of the system are
     the same and the AirSystem represents multiple systems that serve
     ThermalZones that also have a Multiplier > 1. In this scenario, the
     ThermalZone:Multiplier (derived from the user input for Story:Multiplier)
     and AirSystem:Count should be consistent with eachother. 
     NOTE: For multi-zone systems where multiple similar HVACZones are combined
     into a single ThermalZone, it is expected that the TerminalUnit:Count 
     is > 1.

     For any valid scenario that Count is > 1, all system inputs
     (capacities, power, etc) should be specified as if it is single system.
     The ruleset will multiply the appropriate inputs by the Count value for
     final simulation."
  INPUTCLASS 
    Default
  MINIMUM
    1
  COMMONMAXIMUM
    20
  REPORTPRECISION
    0
  DEFAULT
    1
  CHECKCODE
    if( BypassCheckCode > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( IsExhSys > 0 .OR. IsVentOnlySys > 0 )
    then UNCHANGED
    else if( IsMultiZnSys = 1 ) 
    then // Is a multizone system
      if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:Mult ) > 0 )
      then // The AirSys is referenced as the PriAirCondgSys of a zone
        if( Cnt != MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:Mult ) .OR.
            Cnt != MinRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:Mult ) )
        then 
          PostWarning("The AirSystem:Count for multi-zone system '%s' is not
                     consistent with the maximum or minimum ThermalZone:Multiplier
                     of the zones the system serves.
                     This indicates the HVAC system serves more
                     than 1 floor, or that Multipliers or system Count may be
                     improperly defined. Check inputs for consistency.",
                     Name)
        else UNCHANGED
        endif
      else if( MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:Mult ) > 0 )
      then // The AirSys is referenced as the VentSys of a zone 
        if( Cnt != MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:Mult ) .OR.
            Cnt != MinRevRef( ThrmlZn:VentSysRef, ThrmlZn:Mult ) )
        then 
          PostWarning("The AirSystem:Count for multi-zone system '%s' is not
                     consistent with the maximum or minimum ThermalZone:Multiplier
                     of the zones the system serves.
                     This indicates the HVAC system serves more
                     than 1 floor, or that Multipliers or system Count may be
                     improperly defined. Check inputs for consistency.",
                     Name)
        else UNCHANGED
        endif
      else UNCHANGED
      endif endif
    else if( IsMultiZnSys = 0 ) // Is a single-zone system
    then
      if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HVACZnCntWithMult ) > 0 .AND.
          Cnt != SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HVACZnCntWithMult ) )
      then // The AirSys:Cnt is not conistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
        PostWarning("The AirSystem:Count for single-zone system '%s' is not
                   consistent with the total ThermalZone:HVACZoneCount
                   that the system serves.
                   This indicates either a) the single-zone system has slave zones, or
                   b) HVACZnCount may be improperly defined. Check inputs for consistency.",
                   Name)
      else
      if( MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:HVACZnCntWithMult ) > 0 .AND.
          Cnt != SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HVACZnCntWithMult ) )
      then // The AirSys:Cnt is not conistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
        PostWarning("The AirSystem:Count for single-zone system '%s' is not
                   consistent with the total ThermalZone:HVACZoneCount
                   that the system serves.
                   This indicates either a) the single-zone system has slave zones, or
                   b) HVACZnCount may be improperly defined. Check inputs for consistency.",
                   Name)
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif endif endif
  SIZING : T24N
    if( BaseSysNum > 0 )
    then 
      if( BaseSysNum = 5 .OR. BaseSysNum = 6 )
      then MaxRevRef( Story:BaseFlrSysRef, Story:Mult )
      else // Floor-by-floor VAV
      if( BaseSysNum = 12 .AND. ( Type = "PVAV" .OR. Type = "VAV" ) )
      then // LabSys
        MaxRevRef( Story:LabFlrSysRef, Story:Mult )
      else
      if( BaseSysNum > 200 )
      then // SZ AirSys serving res common areas (ResOtherZn) - SAC 05/09/22
        1
      else
      if( BaseSysNum > 100 )
      then // AirSys (DOAS) VentSys
        MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:HVACZnCntWithMult )
      else // All other cases where baseline is AirSys 
        MaxRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:HVACZnCntWithMult )
      endif endif endif endif
    else Cnt
    endif
  SIZING : S901G ECBC
    if( BaseSysNum > 0 )
    then 
      if( BaseSysNum >= 5 .AND. BaseSysNum <= 8 )
      then Max( MaxRevRef( Story:BasePredominantFuelFlrSysRef, Story:Mult ), 
                MaxRevRef( Story:BaseNonPredominantFuelFlrSysRef, Story:Mult ) ) 
      else MaxRevRef( ThrmlZn:BaseZnAirSysRef, ThrmlZn:PropZnSysCnt )
      endif
    else Cnt
    endif
  ANNUAL
    z:Cnt   
ENDRULE

// =========================== ZoneSystem ======================================
RULE ZnSys:Cnt
  DESCRIPTION
    "The number of duplicate  systems represented by the current ZoneSystem."
  HELP
    "The number of duplicate systems (i.e. Count > 1) is limited to the
     following scenarios, which may be combined:

     b) All attributes of the system are the same and the ZoneSystem represents
     multiple systems that serve a ThermalZone with an HVACZoneCount > 1.
     In this scenario, the ThermalZone:HVACZoneCount and ZoneSystem:Count 
     should be consistent with eachother. 

     a) All attributes of the system are the same and the ZoneSystem represents
     multiple systems that serve ThermalZones that also have a Multiplier > 1. 
     In this scenario, the ThermalZone:Multiplier (derived from the user input 
     for Story:Multiplier) and ZoneSystem:Count should be consistent with eachother. 

     NOTE: When these two scenarios are combined, i.e. ThermalZone:Multiplier > 1
     and ThermalZone:HVACZoneCount > 1, it is expected that the ZoneSystem:Count
     equal to the ThermalZone Multiplier * HVACZoneCount.

     For any valid scenario that Count is > 1, all system inputs
     (capacities, power, etc) should be specified as if it is single system.
     The ruleset will multiply the appropriate inputs by the Count value for
     final simulation."
  INPUTCLASS 
    Default
  MINIMUM
    1
  COMMONMAXIMUM
    500
  REPORTPRECISION
    0
  DEFAULT
    1
  CHECKCODE
    if( BypassCheckCode > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( Type = "PassiveBeam" ) 
    then UNCHANGED
    else
    if( IsExhSys > 0 )
    then
      if( MaxRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HVACZnCntWithMult ) > 0 )
      then 
        if( Cnt != SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:HVACZnCntWithMult ) )
        then // The ZnSys:Cnt is not consistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
          PostError("The ZoneSystem:Count for '%s' is not consistent with the product
                     of ThermalZone:Multiplier * ThermalZone:HVACZoneCount for the
                     zone that the system serves. These numbers should be consistent
                     with eachother.", Name)
        else UNCHANGED
        endif
      else
      if( MaxRevRef( ThrmlZn:ExhSysRef, ThrmlZn:Mult ) > 0 )
      then 
        if( Cnt != SumRevRef( ThrmlZn:ExhSysRef, ThrmlZn:Mult ) )
        then // The ZnSys:Cnt is not consistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
          PostError("The ZoneSystem:Count for '%s' is not consistent with the 
                     ThermalZone:Multiplier for the zone that the system serves.
                     These numbers should be consistent with eachother.", Name)
        else UNCHANGED
        endif
      else UNCHANGED
      endif endif
    else
    if( MaxRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HVACZnCntWithMult ) > 0 .AND.
        Cnt != SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HVACZnCntWithMult ) )
    then // The ZnSys:Cnt is not consistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
      PostError("The ZoneSystem:Count for '%s' is not consistent with the product
                 of ThermalZone:Multiplier * ThermalZone:HVACZoneCount for the
                 zone that the system serves. These numbers should be consistent
                 with eachother.", Name)
    else
    if( MaxRevRef( ThrmlZn:VentSysRef, ThrmlZn:HVACZnCntWithMult ) > 0 .AND.
        Cnt != SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HVACZnCntWithMult ) )
    then // The ZnSys:Cnt is not conistent with ThrmlZn:Mult*ThrmlZn:HVACZnCnt
      PostError("The ZoneSystem:Count for '%s' is not consistent with the product
                 of ThermalZone:Multiplier * ThermalZone:HVACZoneCount for the
                 zone that the system serves. These numbers should be consistent
                 with eachother.", Name)
    else UNCHANGED
    endif endif endif endif endif
  SIZING
    if( BaseSysNum > 0 )
    then 
      if( ZnSys:AddClgToPropSys > 0 )
      then // Set Cnt for added single-zone cooling system
        MaxRevRef( ThrmlZn:SimSysRef[1], ThrmlZn:HVACZnCntWithMult )
      else // Set Cnt for system map ZnSys
        MaxRevRef( ThrmlZn:BaseZnZnSysRef, ThrmlZn:HVACZnCntWithMult )
      endif
    else Cnt
    endif
  ANNUAL
    z:Cnt   
ENDRULE


// ********** Air Distribution Type ********************************************
// TO DO: Revise to Rule once added to BEMBase
RULE NEW AirSys:AirDistType
  DATATYPE
    Enumeration
  LONGFORM
    AirDistributionType
  DESCRIPTION
    "Type of air distribution system that is coupled with the HVAC system."
  REFERENCE 
    NACM Section 5.7.1 
  INPUTCLASS 
    Prescribed
  OPTION
    Mixing
    UnderFloorAirDistribution
    DisplacementVentilation
    None    
  DEFAULT
    "Mixing"
  SIZING    
    if( BaseSysNum > 0 )
    then "Mixing"
    else AirDistType
    endif
  ANNUAL
    z:AirDistType
ENDRULE

// -----------------------------------------------------------------------------
// TO DO: Revise to Rule once added to BEMBase
RULE NEW ZnSys:AirDistType
  DATATYPE
    Enumeration
  LONGFORM
    AirDistributionType
  DESCRIPTION
    "Type of air distribution system that is coupled with the HVAC system."
  REFERENCE 
    NACM Section 5.7.1 
  INPUTCLASS 
    Prescribed     
  OPTION
    Mixing
    None    
  DEFAULT
    "Mixing"
  SIZING    
    if( BaseSysNum > 0 )
    then "Mixing"
    else ValidOr( AirDistType, "Mixing" )
    endif
  ANNUAL
    z:AirDistType
ENDRULE


// ********** Design Outside Air Flow ******************************************
// =========================== AirSystem =======================================
// Calculate the Uncorrected air flow (Vou)
RULE NEW AirSys:SysVentFlowUncorrected
  DATATYPE
    Float
  LONGFORM
    SystemVentilationFlowUncorrected
  DESCRIPTION
    "The design outside air flow of the AirSytem, uncorrected for system
     ventilation efficiency (Vou)."
  HELP
    ""
  INPUTCLASS 
    NotInput         
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:TotalIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
    endif 
  SIZING
    if( ServesResZn > 0 )
    then
      if( BaseSysNum > 0 )
      then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:MinIAQCFM )
      else SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:TotalIAQCFM )
      endif
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
    endif 
  ANNUAL
    if( ServesResZn > 0 )
    then
      if( BaseSysNum > 0 )
      then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:MinIAQCFM )
      else SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:TotalIAQCFM )
      endif
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
    endif 
ENDRULE

RULE NEW AirSys:SysMinVentFlow
  DATATYPE
    Float
  LONGFORM
    SystemMinimumVentilationFlow
  DESCRIPTION
    "The total minimum ventilation air flow of the zones served by the AirSystem."
  INPUTCLASS
    NotInput       
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:StdDesignIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
    endif 
  SIZING
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:StdDesignIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
    endif
  ANNUAL
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:StdDesignIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
    endif 
ENDRULE

RULE NEW AirSys:SysCodeVentFlow
  DATATYPE
    Float
  LONGFORM
    SystemCodeVentilationFlow
  DESCRIPTION
    "The total code ventilation air flow of the zones served by the AirSystem."
  INPUTCLASS
    NotInput
  UNITS 
    cfm
  DEFAULT
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:MinIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
    endif 
  SIZING
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:MinIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
    endif 
  ANNUAL
    if( ServesResZn > 0 )
    then SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:MinIAQCFM )
    else SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
    endif 
ENDRULE

RULE AirSys:SysVentFlow 
  DESCRIPTION
    "The design outside air flow of the AirSytem."
  INPUTCLASS : T24N
    NotInput       
  INPUTCLASS : S901G ECBC
    Default  
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    if( ( Type = "DOASCV" .OR. Type = "DOASVAV" ) .AND.
        LocalStatus( IsAllOA ) = 2 )
    then // IsAllOA is defaulted, use SysVentFlow
      ValidOr( SysVentFlowUncorrected, 0 )
    else
    if( IfValidAnd( IsAllOA > 0 ) )
    then // Reset based on SupFanCap
      ValidOr( SupFanCap, 0 )
    else // Use SysVentFlow
      ValidOr( SysVentFlowUncorrected, 0 )
    endif endif
  CHECKCODE
    if( BypassCheckCode > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( SysVentFlow > 0 )
    then 
      if( IsExhSys = 0 .AND. 
          SupFanCap < SysVentFlow * ( 1 - Proj:VentTolMaxLimNonRes ) )
      then
        PostError("The supply fan capacity of AirSystem '%s' is %.0f cfm, which less than
                   the design OA flow, %.0f cfm, calculated from the ThermalZones served
                   (defined by VentilationSystemReference). Check inputs for
                   consistency.", Name, SupFanCap, SysVentFlow)
// The checks are handled under *Sys:ExhFanCap
;     else if( IsExhSys > 0 )
;     then
;       if( ExhFanCap > 0 .AND. 
;           ExhFanCap < SysVentFlow * ( 1 - Proj:VentTolMaxLimNonRes ) )
;       then
;       PostError("The exhaust fan capacity of AirSystem '%s' is %.0f cfm, which less than
;                  the design OA flow, %.0f cfm, calculated from the ThermalZones served
;                  (defined by VentilationSystemReference). Check inputs for
;                  consistency.", Name, ExhFanCap, SysVentFlow)
;       else 
;       if( SupFanCap > 0 .AND. 
;           SupFanCap < SysVentFlow * ( 1 - Proj:VentTolMaxLimNonRes ) )
;       then
;       PostError("The supply fan capacity of AirSystem '%s' is %.0f cfm, which less than
;                  the design OA flow, %.0f cfm, calculated from the ThermalZones served
;                  (defined by VentilationSystemReference). Check inputs for
;                  consistency.", Name, SupFanCap, SysVentFlow)
;       else UNCHANGED
;       endif endif
      else UNCHANGED
      endif ;endif
    else UNCHANGED
    endif endif
// This check moved to AirSys:OACtrlRef
; CHECKSIM
;   if( BypassCheckSim > 0 )
;   then UNCHANGED // Bypass QC checks for system
;   else
;   if( SysVentFlow > 0 .AND.
;       IsExhSys = 0 .AND.
;       SumChildren( OACtrl:SysCnt ) = 0 )
;   then 
;     PostError("AirSystem '%s' has no OutsideAirControl object defined.",
;                Name)    
;   else UNCHANGED
;   endif endif
  SIZING
    if( ServesResZn > 0 )
    then
      if( BaseSysNum > 0 )
      then SysCodeVentFlow
      else SysVentFlowUncorrected
      endif
    else SysVentFlowUncorrected
    endif 
  ANNUAL
    if( ServesResZn > 0 )
    then
      if( BaseSysNum > 0 )
      then SysCodeVentFlow
      else SysVentFlowUncorrected
      endif
    else SysVentFlowUncorrected
    endif 
ENDRULE


RULE NEW AirSys:OAFlowRat
  DATATYPE
    Float
  LONGFORM
    OutsideAirFlowRatio
  DESCRIPTION
    "The design outside air flow ratio as a fraction of supply flow rate."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( SysVentFlow >= 0 ) .AND.
        IfValidAnd( SupFanCap > 0 ) )
    then Max( SysVentFlow / SupFanCap, 0 )
    else 0
    endif
ENDRULE

RULE AirSys:RetAirFlowRat
  DESCRIPTION
    "The design return air flow rate as a fraction of supply flow rate when there
     is no exhaust flow."
  HELP
    "This may be used to set zero return air flow for a DOAS system or to model a
     pressurized system where the return flow is a fraction of the supply fow. 
     The return air fow rate will never be greater than the current supply air 
     flow rate multiplied by this fraction. It may be less if there is unbalanced
     exhaust from any zones served by this AirSystem."
  INPUTCLASS
    NotInput
  DEFAULT
    1 // See also HVACSecondary-HeatRecovery.rule. Set here for rule eval purposes
ENDRULE

// =========================== ZoneSystem ======================================
// Calculate the Uncorrected air flow (Vou)
RULE NEW ZnSys:SysVentFlowUncorrected
  DATATYPE
    Float
  LONGFORM
    SystemVentilationFlowUncorrected
  DESCRIPTION
    "The design outside air flow of the AirSytem, uncorrected for system
     ventilation efficiency (Vou)."
  HELP
    ""
  INPUTCLASS 
    NotInput
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
  SIZING
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
  ANNUAL
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt
ENDRULE

// Outdoor air intake air flow (Vot)
RULE ZnSys:SysVentFlow
  DESCRIPTION
    "The rate of outside air that needs to be delivered by the system at 
     design conditions."
  HELP : T24N
    "Minimum ventilation requirements specified by Standard 120(b)2 as the greater
     of 15 cfm/person and the minimum ventilation rates specified in Appendix 5.4
     For systems serving laboratory spaces, the system shall be 100% outside air, 
     with ventilation rates determined by the Authority Having Jurisdiction."
  REFERENCE 
    NACM Section 5.7.4.1
  INPUTCLASS 
    NotInput
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    SysVentFlowUncorrected
  CHECKCODE
    if( BypassCheckCode > 0 .OR.
        Type = "Baseboard" .OR.
        ( Type = "Furnace" .AND. SubType != "Other" ) .OR.
        ( Type = "Furnace" .AND. IfValidAnd( FurnaceType = "Gravity" ) ) ) 
    then UNCHANGED // Bypass QC checks for system
    else
    if( SysVentFlow > 0 .AND.
        Type != "Exhaust" .AND.
        Type != "VentilationOnly" )
    then 
      if( SupFanCap < SysVentFlow * ( 1 - Proj:VentTolMaxLimNonRes ) )
      then
        PostError("The supply fan capacity of ZoneSystem '%s' is %.0f cfm, which less than
                   the design OA flow, %.0f cfm, calculated from the ThermalZones served
                   (defined by VentilationSystemReference). Check inputs for
                   consistency.", Name, SupFanCap, SysVentFlow)
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
  SIZING
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt 
  ANNUAL
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:VentFlowWithMult ) / Cnt 
ENDRULE
RULE NEW ZnSys:SysMinVentFlow 
  DATATYPE
    Float
  LONGFORM
    SystemMinimumVentilationFlow
  DESCRIPTION
    "The total minimum ventilation air flow of the zones served by the ZoneSystem."
  INPUTCLASS
    NotInput       
  UNITS 
    cfm
  REPORTPRECISION
    0
  DEFAULT
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
  SIZING
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
  ANNUAL
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:MinVentFlowWithMult ) / Cnt
ENDRULE

RULE NEW ZnSys:SysCodeVentFlow
  DATATYPE
    Float
  LONGFORM
    SystemCodeVentilationFlow
  DESCRIPTION
    "The total code ventilation air flow of the zones served by the ZoneSystem,
     for simulation."
  INPUTCLASS
    NotInput
  UNITS 
    cfm
  DEFAULT
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
  SIZING
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
  ANNUAL
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CodeVentFlowWithMult ) / Cnt
ENDRULE

RULE NEW ZnSys:OAFlowRat
  DATATYPE
    Float
  LONGFORM
    OutsideAirFlowRatio
  DESCRIPTION
    "The design outside air flow ratio as a fraction of supply flow rate."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( SysVentFlow >= 0 ) .AND.
        IfValidAnd( SupFanCap > 0 ) )
    then Max( SysVentFlow / SupFanCap, 0 )
    else 0
    endif
ENDRULE


// ********** System-level HVAC AutoSizing *************************************
// This is currently not supported by Reverse Translator
// =========================== AirSystem ======================================
;RULE AirSys:HVACAutoSizing
;  DESCRIPTION
;    "Whether or not the air system is to be sized by the simulation."
;  INPUTCLASS 
;    Optional
;  DEFAULT
;    Proj:HVACAutoSizing
;  SIZING
;    1 // AutoSize baseline systems
;  ANNUAL
;    0
;ENDRULE
;// =========================== ZoneSystem =====================================
;RULE ZnSys:HVACAutoSizing
;  DESCRIPTION
;    "Whether or not the air system is to be sized by the simulation."
;  INPUTCLASS 
;    Optional
;  DEFAULT
;    Proj:HVACAutoSizing
;  SIZING
;    1 // AutoSize systems
;  ANNUAL
;    0
;ENDRULE

RULE AirSys:ResHVACAutoSizing
  DESCRIPTION
    "Whether or not the air system is to be sized by the residential simulation."
  INPUTCLASS 
    Prescribed
  DEFAULT
    0
  SIZING_PROPOSED
    if (IfValidAnd( ServesResZn > 0 ) .AND. IfValidAnd( u:Proj:AutoHardSize > 0 ))
    then  UNCHANGED  // only size Prop if Proj flag + user input
    else  0 
    endif
  SIZING_BASELINE
    if (IfValidAnd( ServesResZn > 0 ))
    then  1  // AutoSize baseline systems
    else  0 
    endif
  ANNUAL
    UNCHANGED
ENDRULE


// ********** Total Cooling Capacity *******************************************
// See HVACSecondary-CoilCooling-General.rule


// ********** Mandatory Efficiency Checks **************************************
// Additional SDD attributes used to determine unitary mandatory efficiency 
// requirements for designed proposed systems.  Not applicable to partial 
// compliance proposed HVAC systems or baseline HVAC system designs; these are 
// defined by ruleset to meet/exceed mandatory efficiency requirements.

// =========================== AirSystem ======================================
RULE AirSys:SubType
  DESCRIPTION
    "Property used to define rating conditions and efficiency requirements of 
     system components."
  RESETS
    ResetThisWhenTheFollowingIsModified
      Type
  INPUTCLASS 
    Default
// Enums defined in BEMEnums.txt since property referenced by Library object   
//OPTION
//  Packaged3Phase
//  Packaged1Phase
//  Split3Phase   
//  Split1Phase   
//  CRAC         
//  CRAH
//  NA          
  DEFAULT
    if( ServesResZn > 0 )
    then "Packaged3Phase"
    else
    if( Type = "HV" .OR. IsExhSys > 0 )
    then "NA"
    else "Packaged3Phase"
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:DXCap
  DATATYPE
    Float
  LONGFORM
    DirectExpansionCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:Type = "DirectExpansion" )   
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:ClgCap
  DATATYPE
    Float
  LONGFORM
    CoolingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then SumChildren( CoilClg:CapTotNetRtd )
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:ElecResHtgCap
  DATATYPE
    Float
  LONGFORM
    ElectricResistanceHeatingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "Resistance" )
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:HtPumpCap
  DATATYPE
    Float
  LONGFORM
    HeatPumpCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" )
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:NonElecHtgCap
  DATATYPE
    Float
  LONGFORM
    NonElectricHeatingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then 
      SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "Furnace" ) +
      SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" ) +
      SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HotWater" ) +
      SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "Steam" ) 
    else 0
    endif 
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:HtgCap
  DATATYPE
    Float
  LONGFORM
    HeatingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    if( Type = "Supply" )
    then SumChildren( CoilHtg:CapTotNetRtd ) - SumChildren( CoilHtg:SuppHtgCap )
    else 0
    endif 
ENDRULE

// =========================== ZoneSystem ======================================
RULE ZnSys:SubType
  DESCRIPTION
    "Property used to define rating conditions and efficiency requirements of 
     system components."
  RESETS
    ResetThisWhenTheFollowingIsModified
      Type
  INPUTCLASS 
    Default
// Enums defined in BEMEnums.txt since property referenced by Library object   
//OPTION
//  Packaged3Phase
//  Packaged1Phase
//  Split3Phase   
//  Split1Phase
//  NA
//  Wall
//  Floor         
//  Room          
//  Other         
//DEFAULT
// ZnSys assumed to be used for residential applications, so default to
// Split1Phase
// If Furnace, default to
// Wall
ENDRULE

RULE ZnSys:FurnaceType
  DESCRIPTION
    "For Type = 'Furnace' systems, this indicates whether the system
     is configured with a fan or is a 'Gravity' heating system (no fan)."
  RESETS
    ResetThisWhenTheFollowingIsModified
      Type
  OPTION
    Fan
    Gravity 
  INPUTCLASS
    Default
  DEFAULT
    if( Type = "Furnace" )
    then "Fan"
    else UNDEFINED
    endif
ENDRULE

RULE NEW ZnSys:HtPumpCap
  DATATYPE
    Float
  LONGFORM
    HeatPumpCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" )
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:DXCap
  DATATYPE
    Float
  LONGFORM
    DirectExpansionCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:Type = "DirectExpansion" )   
ENDRULE

RULE NEW ZnSys:VRFNetClgCap
  DATATYPE
    Float
  LONGFORM
    VariableRefrigerantFlowNetCoolingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:Type = "VRF" )   
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:NonElecHtgCap
  DATATYPE
    Float
  LONGFORM
    NonElectricHeatingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "Furnace" ) +
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" ) +
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HotWater" ) +
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "Steam" ) 
ENDRULE

RULE NEW ZnSys:VRFNetHtgCap
  DATATYPE
    Float
  LONGFORM
    VariableRefrigerantFlowNetHeatingCapacity
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "VRF" )   
ENDRULE

// -----------------------------------------------------------------------------
// Identify unitary equipment category for mandatory efficiency checks
RULE NEW AirSys:UnitaryCat
  DATATYPE
    Enumeration
  LONGFORM
    UnitaryCategory
  INPUTCLASS 
    NotInput
  OPTION
    AC
    HP
    NA
  DEFAULT
    if( SumChildren( AirSeg:HtPumpCap ) > 1 )
    then "HP"
    else if( SumChildren( AirSeg:DXCap ) > 1 )
    then "AC"
    else "NA"
    endif endif 
ENDRULE

RULE NEW ZnSys:UnitaryCat
  DATATYPE
    Enumeration
  LONGFORM
    UnitaryCategory
  INPUTCLASS 
    NotInput
  OPTION
    AC
    HP
    NA
  DEFAULT
    if( HtPumpCap > 1 )
    then "HP"
    else if( DXCap > 1 )
    then "AC"
    else "NA"
    endif endif 
ENDRULE

// -----------------------------------------------------------------------------
// SEER2/EER2/HSPF2 efficiency metric

// For defaulting at Project Level
RULE Proj:EffMetric
  INPUTCLASS 
    Default
  DESCRIPTION
    "The default efficiency rating metric input option for HVAC systems in the project."
  HELP
    "Only applicable to 1-phase and 3-phase unitary split and packaged air conditioners and 
     heat pumps that characterized with new SEER2/EER2/HSPF2 ratings."
  OPTION
    SEER/EER/HSPF
    SEER2/EER2/HSPF2
  DEFAULT
    "SEER/EER/HSPF"
ENDRULE
// Set a flag at ThrmlZn indicating the standard design system is SEER2
// -----------------------------------------------------------------------------
RULE NEW ThrmlZn:StdIsSEER2
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates SEER2/EER2/HSPF2 efficiency ratings should be used for the standard design."
  INPUTCLASS 
    NotInput
  DEFAULT : T24N_2019
    if( IfValidAnd( IsCond > 0 ) .AND. 
        IfValidAnd( IsNewHVAC > 0 ) .AND.
        ( IfValidAnd( IsHighRiseRes > 0 ) .OR. IfValidAnd( IsHotelMotelGuestRm > 0 ) ) ) 
    then
      if( IfValidAnd( PriAirCondgSysRef[1]:EffMetric = "SEER2/EER2/HSPF2" ) .AND.
          IfValidAnd( PriAirCondgSysRef[1]:SEER2IsValid > 0 ) )
      then 1
      else 0
      endif
    else 0
    endif
  DEFAULT 
    0
ENDRULE
RULE NEW Proj:StdIsSEER2
  DATATYPE
    Integer
  DESCRIPTION
    "The number of zones that will use SEER2/EER2/HSPF2 efficiency ratings in the standard design."
  INPUTCLASS 
    NotInput
  DEFAULT
    SumChildrenIf( ThrmlZn:StdIsSEER2, ThrmlZn:FlrArea > 0 )
ENDRULE

// =========================== AirSystem ======================================
RULE NEW AirSys:SEER2IsValid
  DATATYPE
    Integer
  LONGFORM
    SEER2IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( ( Type = "SZAC" .OR. Type = "SZHP" .OR. Type = "SZDFHP" .OR. 
          Type = "SZVAVAC" .OR. Type = "SZVAVHP" .OR. Type = "SZVAVDFHP" ) .AND. 
        SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:Type = "DirectExpansion" ) < 65000 .AND. 
        SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:CndsrType = "Air" ) > 0 .AND. 
        Proj:StdsVersionYr >= 2019 .AND. 
        IfValidAnd( UnitaryCat != "NA" ) .AND.
        ( IfValidAnd( SubType != "CRAC" ) .AND. IfValidAnd( SubType != "CRAH" ) .AND. IfValidAnd( SubType != "NA" ) ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW AirSys:HSPF2IsValid
  DATATYPE
    Integer
  LONGFORM
    HSPF2IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SEER2IsValid > 0 .AND.
        SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" ) > 0 .AND.
        SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:CndsrType = "Air" ) > 0 )
    then 1 
    else 0
    endif
ENDRULE

RULE NEW AirSys:EER2IsValid
  DATATYPE
    Integer
  LONGFORM
    EER22IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SEER2IsValid > 0 .AND.
        HSPF2IsValid = 0 .AND.
        ( IfValidAnd( SubType = "Split1Phase" ) .OR. IfValidAnd( SubType = "Packaged1Phase" ) ) )
    // EER2 is only applicable to Split and Packaged 1-phase air-conditioners
    then 1 
    else 0
    endif
ENDRULE

RULE AirSys:EffMetric
  INPUTCLASS 
    Default
  DESCRIPTION
    "The efficiency rating metric input option for the system."
  HELP
    "Only applicable to 1-phase and 3-phase unitary split and packaged air conditioners and 
     heat pumps that characterized with new SEER2/EER2/HSPF2 ratings."
  OPTION
    SEER/EER/HSPF
    SEER2/EER2/HSPF2
  DEFAULT
    if( SEER2IsValid > 0 .OR. HSPF2IsValid > 0 .OR. EER2IsValid > 0 )
    then Proj:EffMetric
    else UNDEFINED
    endif
ENDRULE

// =========================== ZoneSystem ======================================
RULE NEW ZnSys:SEER2IsValid
  DATATYPE
    Integer
  LONGFORM
    SEER2IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( ( Type = "SZAC" .OR. Type = "SZHP" .OR. Type = "SZDFHP" ) .AND.
        SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:Type = "DirectExpansion" ) < 65000 .AND. 
        SumChildrenIf( CoilClg:CapTotNetRtd, CoilClg:CndsrType = "Air" ) > 0 .AND. 
        Proj:StdsVersionYr >= 2019 .AND. 
        IfValidAnd( UnitaryCat != "NA" ) .AND.
        IfValidAnd( SubType != "NA" ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ZnSys:HSPF2IsValid
  DATATYPE
    Integer
  LONGFORM
    HSPF2IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SEER2IsValid > 0 .AND.
        SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:Type = "HeatPump" ) > 0 .AND.
        SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:CndsrType = "Air" ) > 0 )
    then 1 
    else 0
    endif
ENDRULE

RULE NEW ZnSys:EER2IsValid
  DATATYPE
    Integer
  LONGFORM
    EER2IsValid
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SEER2IsValid > 0 .AND. 
        HSPF2IsValid = 0 .AND.
        ( IfValidAnd( SubType = "Split1Phase" ) .OR. IfValidAnd( SubType = "Packaged1Phase" ) ) )
    // EER2 is only applicable to Split and Packaged 1-phase air-conditioners
    then 1 
    else 0
    endif
ENDRULE

RULE ZnSys:EffMetric
  INPUTCLASS 
    Default
  DESCRIPTION
    "The efficiency rating metric input option for the system."
  HELP
    "Only applicable to 1-phase and 3-phase unitary split and packaged air conditioners and 
     heat pumps that characterized with new SEER2/EER2/HSPF2 ratings."
  OPTION
    SEER/EER/HSPF
    SEER2/EER2/HSPF2
  DEFAULT
    if( SEER2IsValid > 0 .OR. HSPF2IsValid > 0 .OR. EER2IsValid > 0 )
    then Proj:EffMetric
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Mandatory EER requirement deduction for AC/HP units
RULE NEW AirSys:EERDeduction
  DATATYPE
    Float
  LONGFORM
    EERDeduction
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SubType = "CRAC" .OR. SubType = "CRAH" )
    then 0.0
    else if( SumChildren( AirSeg:NonElecHtgCap ) > 1 )
    then
      if( UnitaryCat = "HP" )
      then 0.0 // Look-up table UnitaryACHPMinEffReq includes 0.2 deduction for HP
      else 0.2
      endif
    else 0.0
    endif endif
ENDRULE

RULE NEW ZnSys:EERDeduction
  DATATYPE
    Float
  LONGFORM
    EERDeduction
  INPUTCLASS 
    NotInput
  DEFAULT
    if( NonElecHtgCap > 1 )
    then
      if( UnitaryCat = "HP" )
      then 0.0 // Look-up table UnitaryACHPMinEffReq includes 0.2 deduction for HP
      else 0.2
      endif
    else 0.0
    endif
ENDRULE


//  Calculate conditioned floor area
// =========================== AirSystem =======================================
RULE NEW AirSys:TotCondFlrArea
  DATATYPE
    Float
  LONGFORM
    TotalConditionedFloorArea
  DESCRIPTION
    "The total conditioned floor area served by the AirSystem,
     including multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondFlrAreaWithMult ),
              SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondFlrAreaWithMult ) ),
              SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:CondFlrAreaWithMult ) )
  SIZING
    Max( Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondFlrAreaWithMult ),
              SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondFlrAreaWithMult ) ),
              SumRevRef( ResOtherZn:ActiveHVACSystem, ResOtherZn:CondFlrAreaWithMult ) )
  ANNUAL
    z:TotCondFlrArea
ENDRULE 
// =========================== ZoneSystem ======================================
RULE NEW ZnSys:TotCondFlrArea
  DATATYPE
    Float
  LONGFORM
    TotalConditionedFloorArea
  DESCRIPTION
    "The total conditioned floor area served by the ZoneSystem,
     including multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondFlrAreaWithMult ),
         SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:CondFlrAreaWithMult ) )
  SIZING
    Max( Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondFlrAreaWithMult ),
              SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:CondFlrAreaWithMult ) ),
         Max( SumRevRef( ThrmlZn:SimSysRef[1], ThrmlZn:CondFlrAreaWithMult ),
              SumRevRef( ThrmlZn:SimSysRef[2], ThrmlZn:CondFlrAreaWithMult ) ) )
  ANNUAL
    z:TotCondFlrArea
ENDRULE 


// ********** Default CoilClg/Htg references ***********************************
// First, specify the indix used to create reference
RULE NEW CoilClg:CoilClgIdx
  DATATYPE
    Integer
  LONGFORM
    CoilCoolingIndex
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildIndex( 1 ) 
ENDRULE
RULE NEW CoilHtg:CoilHtgIdx
  DATATYPE
    Integer
  LONGFORM
    CoilHeatingIndex
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( SuppHtgCap = 0 ) )
    then ChildIndex( 1 ) // Is not the supplemental heating coil for HP system
    else 0 
    endif
ENDRULE
RULE NEW Fan:FanIdx
  DATATYPE
    Integer
  LONGFORM
    FanIndex
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildIndex( 1 ) 
ENDRULE

// Create references at coil parent level
// =========================== AirSystem =======================================
RULE AirSeg:CoilClgRef
  DESCRIPTION
    "Reference to the primary cooling coil for the AirSystem."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilClg:CoilClgIdx ) > 0 ) )
    then ChildRef( CoilClg:Name, MaxChild( CoilClg:CoilClgIdx ) )
    else UNDEFINED
    endif
ENDRULE 
RULE AirSeg:CoilHtgRef
  DESCRIPTION
    "Reference to the primary heating coil for the AirSystem."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilHtg:CoilHtgIdx ) > 0 ) )
    then ChildRef( CoilHtg:Name, MaxChild( CoilHtg:CoilHtgIdx ) )
    else UNDEFINED
    endif
ENDRULE 
RULE NEW AirSeg:CoilHtgHtPumpRef
  DATATYPE
    CoilHtg
  LONGFORM
    CoilHeatingHeatPumpReference
  DESCRIPTION
    "Reference to the primary Heat Pump heating coil for the AirSystem."
  INPUTCLASS 
    NotInput
  DEFAULT
    if (MaxChild( CoilHtg:HtPumpCprsrHt ) > 0)
    then  MaxChildComp( CoilHtg:HtPumpCprsrHt )
    else  UNDEFINED
    endif
  SIZING
    if (MaxChild( CoilHtg:HtPumpCprsrHt ) > 0)
    then  MaxChildComp( CoilHtg:HtPumpCprsrHt )
    else  UNDEFINED
    endif
  ANNUAL
    if (MaxChild( CoilHtg:HtPumpCprsrHt ) > 0)
    then  MaxChildComp( CoilHtg:HtPumpCprsrHt )
    else  UNDEFINED
    endif
ENDRULE 
RULE AirSeg:FanRef
  DESCRIPTION
    "Reference to the primary fan for this AirSeg."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( Fan:FanIdx ) > 0 ) )
    then ChildRef( Fan:Name, MaxChild( Fan:FanIdx ) )
    else UNCHANGED
    endif
ENDRULE 

// =========================== ZoneSystem ======================================
RULE ZnSys:CoilClgRef
  DESCRIPTION
    "Reference to the primary cooling coil for the ZoneSystem."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilClg:CoilClgIdx ) > 0 ) )
    then ChildRef( CoilClg:Name, MaxChild( CoilClg:CoilClgIdx ) )
    else UNDEFINED
    endif
ENDRULE 
RULE ZnSys:CoilHtgRef
  DESCRIPTION
    "Reference to the primary heating coil for the ZoneSystem."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilHtg:CoilHtgIdx ) > 0 ) )
    then ChildRef( CoilHtg:Name, MaxChild( CoilHtg:CoilHtgIdx ) )
    else UNDEFINED
    endif
ENDRULE
RULE ZnSys:FanRef
  DESCRIPTION
    "Reference to the primary fan for the ZoneSystem."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( Fan:FanIdx ) > 0 ) )
    then ChildRef( Fan:Name, MaxChild( Fan:FanIdx ) )
    else UNDEFINED
    endif
ENDRULE

// =========================== TerminalUnit ======================================
RULE TrmlUnit:CoilClgRef
  DESCRIPTION
    "Reference to the primary cooling coil for the TerminalUnit."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilClg:CoilClgIdx ) > 0 ) )
    then ChildRef( CoilClg:Name, MaxChild( CoilClg:CoilClgIdx ) )
    else UNDEFINED
    endif
  
ENDRULE 
RULE TrmlUnit:CoilHtgRef
  DESCRIPTION
    "Reference to the primary heating coil for the TerminalUnit."
  HELP
    "This property is only used for default of proposed inputs and creating
     baseline systems."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( MaxChild( CoilHtg:CoilHtgIdx ) > 0 ) )
    then ChildRef( CoilHtg:Name, MaxChild( CoilHtg:CoilHtgIdx ) )
    else UNDEFINED
    endif
ENDRULE 



// ********** QC/Report Rules **************************************************
RULE NEW AirSys:NumThrmlZnCond
  DATATYPE
    Integer
  LONGFORM
    NumberThermalZoneConditioned
  DESCRIPTION
    "The number of conditioned zones served by the AirSystem, not including
     multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) )
  SIZING
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:IsCond ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:IsCond ) )
ENDRULE 

RULE NEW AirSys:NumThrmlZnWithDCV
  DATATYPE
    Integer
  LONGFORM
    NumberThermalZoneConditioned
  DESCRIPTION
    "The number of conditioned zones served by the AirSystem that have DCV,
     not including multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasDCV ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasDCV ) )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( IsMultiZnSys > 0 .AND. NumThrmlZnWithDCV >= NumThrmlZnCond .AND. NumThrmlZnCond > 1 )
// .AND. SysOARat < ( 1 - Proj:OATolLim ) ) 
// DOAS is not impacted by this issue, but baseline is not DOAS. 
// See SST 100015 model for example of why DOAS needs to be included.
    then 
      PostError("AirSystem '%s' is a multizone system and all conditioned zones
                 have DCV. This results in abnormal E+ simulation results. Revise
                 model zoning so that at least one thermal zone served by the 
                 system does not have DCV.", AirSys:Name)
    else UNCHANGED
    endif endif
ENDRULE 

RULE NEW AirSys:NumThrmlZnVent
  DATATYPE
    Integer
  LONGFORM
    NumberThermalZoneVentilated
  DESCRIPTION
    "The number of zones ventilated by the AirSystem, not including
     multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasVentSys)
  SIZING
    SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:HasVentSys )
ENDRULE 

RULE NEW AirSys:CondHVACZnCntWithMult
  DATATYPE
    Integer
  LONGFORM
    ConditionedHVACZoneCountWithMultiplier
  DESCRIPTION
    "The number of HVAC zones the AirSystem serves, including multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondHVACZnCntWithMult ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondHVACZnCntWithMult ) )
  SIZING
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondHVACZnCntWithMult ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondHVACZnCntWithMult ) )           
ENDRULE 

RULE NEW ZnSys:CondHVACZnCntWithMult
  DATATYPE
    Integer
  LONGFORM
    ConditionedHVACZoneCountWithMultiplier
  DESCRIPTION
    "The number of HVAC zones the ZoneSystem serves, including multipliers."
  INPUTCLASS 
    NotInput
  DEFAULT
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondHVACZnCntWithMult ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondHVACZnCntWithMult ) )
  SIZING
    Max( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:CondHVACZnCntWithMult ), 
         SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:CondHVACZnCntWithMult ) )   
ENDRULE 

// -----------------------------------------------------------------------------
// QC number of child objects in AirSys
RULE NEW AirSys:AirSegCnt
  DATATYPE
    Integer
  LONGFORM
    AirSegmentCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( AirSeg )
  CHECKSIM 
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else   
    if( AirSegCnt > 2 )
    then 
      PostError("AirSys '%s' has more than two child AirSegment objects.
                 This is currently not supported.", Name)
    else if( AirSegCnt = 2 .AND. IsExhSys > 0 .AND. ExhFanCap < 1 )
    then // Add check. See issue 1269
      PostError("AirSys '%s' is Type = 'Exhaust' but has no exhaust fan capacity.
                 This may be due to not specifying at least one AirSegment:Type
                 = 'Exhaust', or the child fan has 0 flow capacity.", Name)
    else if( AirSegCnt = 1 .AND. IsExhSys = 0 .AND. SysOARat < 0.99 .AND. IsAllOA = 0 )
    then 
      PostError("AirSys '%s' only has one child AirSegment object. All AirSystems
                 except Type = 'Exhaust' and 100% OA systems require both a
                 'Supply' AirSegment, and a Return or Relief AirSegment.", Name)
    else if( AirSegCnt = 0 )
    then 
      PostError("AirSystem '%s' has no a child AirSegment objects. This is an
                 indication of incorrect model structure.", Name)  
    else UNCHANGED
    endif endif endif endif endif
ENDRULE 

// -----------------------------------------------------------------------------
RULE NEW AirSys:OACtrlCnt
  DATATYPE
    Integer
  LONGFORM
    OutsideAirControlCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( OACtrl )
  CHECKSIM   
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else 
    if( OACtrlCnt > 1 )
    then 
      PostError("AirSys '%s' has more than one child OutsideAirControl object.
                 This is currently not supported.", Name)
// Check moved to AirSys:OACtrlRef
;    else if( OACtrlCnt = 0 .AND. IsExhSys = 0 .AND. SysVentFlow > 0 )
;    then 
;      PostError("AirSystem '%s' has a design ventilation air flow rate of %.0f cfm 
;                 but no child OutsideAirControl object has been defined.", 
;                 Name, SysVentFlow)  
    else UNCHANGED
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSeg:CoilClgCnt
  DATATYPE
    Integer
  LONGFORM
    CoilCoolingCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( CoilClg )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( CoilClgCnt > 0 .AND. Type != "Supply" )
    then    
      PostError("AirSegment '%s' has a child CoilCooling object, but is not
                 Type = 'Supply'. This is currently not supported.", Name)     
    else if( CoilClgCnt > 1 )
    then 
      PostError("AirSegment '%s' has more than one child CoilCooling object.
                 This is currently not supported.", Name)
    else UNCHANGED
    endif endif endif
ENDRULE 

// -----------------------------------------------------------------------------
RULE NEW AirSeg:CoilHtgCnt
  DATATYPE
    Integer
  LONGFORM
    CoilHeatingCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( CoilHtg )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( CoilHtgCnt > 0 .AND. Type != "Supply" )
    then    
      PostError("AirSegment '%s' has a child CoilHeating object, but is not
                 Type = 'Supply'. This is currently not supported.", Name)     
    else if( CoilHtgCnt > 1 .AND. SumChildren( CoilHtg:SuppHtgCap ) = 0 ) 
    then // Two coil heating objects and no supplementary heating
      PostError("AirSegment '%s' has more than one child CoilHeating objects.
                 This is currently only supported if one coil is referenced as the
                 supplemental heating coil of a Type = 'HeatPump' coil.", Name)
    else UNCHANGED
    endif endif endif
ENDRULE 

// -----------------------------------------------------------------------------
RULE NEW AirSeg:FanCnt
  DATATYPE
    Integer
  LONGFORM
    FanCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( Fan )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( FanCnt > 1 )
    then 
      PostError("AirSegment '%s' has more than one child Fan object.
                 This is currently not supported.", Name)
    else if( ( Type = "Supply" .OR. IsExhSys > 0 ) .AND. FanCnt = 0 )
    then
      PostError("AirSegment '%s' is a supply or exhaust segment, but has
                 no child Fan object.", Name)  
    else UNCHANGED
    endif endif endif
ENDRULE 


// -----------------------------------------------------------------------------
// QC number of child objects in ZnSys
RULE NEW ZnSys:CoilClgCnt
  DATATYPE
    Integer
  LONGFORM
    CoilCoolingCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( CoilClg )
  CHECKSIM  
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( CoilClgCnt > 1 )
    then 
      PostError("ZoneSystem '%s' has more than one child CoilCooling object.
                 This is currently not supported.", Name)
    else UNCHANGED
    endif endif
ENDRULE 

// -----------------------------------------------------------------------------
RULE NEW ZnSys:CoilHtgCnt
  DATATYPE
    Integer
  LONGFORM
    CoilHeatingCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( CoilHtg )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( CoilHtgCnt > 1 .AND. SumChildren( CoilHtg:SuppHtgCap ) = 0 )  
    then  // Two coil heating objects and no supplementary heating
      PostError("ZoneSystem '%s' has more than one child CoilHeating objects.
                 This is currently only supported if one coil is referenced as the
                 supplemental heating coil of a Type = 'HeatPump' coil.", Name)
    else UNCHANGED
    endif endif
  SIZING
    ChildCount( CoilHtg )  
ENDRULE 

// -----------------------------------------------------------------------------
RULE NEW ZnSys:FanCnt
  DATATYPE
    Integer
  LONGFORM
    FanCount
  INPUTCLASS 
    NotInput
  DEFAULT
    ChildCount( Fan )
  CHECKSIM
    if( BypassCheckSim > 0 )
    then UNCHANGED // Bypass QC checks for system
    else
    if( FanCnt > 1 )
    then 
      PostError("ZoneSystem '%s' has more than one child Fan object.
                 This is currently not supported.", Name)
    else
    if( FanCnt > 0 .AND. Type = "VentilationOnly" )
    then 
      PostError("ZoneSystem '%s' is Type = 'VentilationOnly' and has 
                 a child Fan object. This system Type does not require
                 a fan and therefore it should be deleted.", Name)
    else UNCHANGED
    endif endif endif
ENDRULE 


// Reporting rules
// =============================================================================
RULE AirSys:AcceptanceTestReq
  DESCRIPTION
    "A boolean (0/1) flag to indicate whether an acceptance test is required 
     for an HVAC system, used for reporting."
  INPUTCLASS
    Default
  DEFAULT
    0
  ANNUAL_PROPOSED
    if( BaseSysNum > 0 ) then 0 
    else AcceptanceTestReq
    endif
  ANNUAL_BASELINE
    0
ENDRULE


// =============================================================================
RULE ZnSys:AcceptanceTestReq
  DESCRIPTION
    "A boolean (0/1) flag to indicate whether an acceptance test is required 
     for an HVAC system, used for reporting."
  INPUTCLASS
    Default
  DEFAULT
    0
  ANNUAL_PROPOSED
    if( BaseSysNum > 0 .OR. IfValidAnd( IsActiveBeam > 0 ) ) then 0 
    else AcceptanceTestReq
    endif
  ANNUAL_BASELINE
    0
ENDRULE


// =============================================================================
RULE AirSys:AcceptanceTestReqRpt
  DESCRIPTION
    "A text string (Yes or No) showing whether an acceptance test is required 
     for an HVAC system for reporting."
  INPUTCLASS 
    NotInput
  ANNUAL_PROPOSED
    if( AcceptanceTestReq = 0 ) then "No"
    else "Yes" 
    endif
  ANNUAL_BASELINE
    "NA"
ENDRULE


// =============================================================================
RULE ZnSys:AcceptanceTestReqRpt
  DESCRIPTION
    "A text string (Yes or No) showing whether an acceptance test is required 
     for an HVAC system for reporting."
  INPUTCLASS 
    NotInput
  ANNUAL_PROPOSED
    if( AcceptanceTestReq = 0 ) then "No"
    else "Yes" 
    endif
  ANNUAL_BASELINE
    "NA"
ENDRULE


// =============================================================================
//RULE AirSys:OperableWinHaveInterlock
//  DESCRIPTION
//    ""
//  INPUTCLASS 
//    NotInput
//  ANNUAL
//    if ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:OperableWinHaveInterlock ) > 0 )
//    then 1
//    else 0
//    endif
//ENDRULE
    
// =============================================================================
RULE NEW AirSys:WinInterlockRptFlag
  DATATYPE
    Integer
  LONGFORM
    WindowInterlockReportFlag
  DESCRIPTION
    ""  
  HELP
    ""  
  INPUTCLASS
    NotInput 
  ANNUAL
    if ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:WinInterlockRptFlag ) = 0 )
    then 0
    else if ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:WinInterlockRptFlag ) > 0 )
    then 1
    else -1
    endif endif 
ENDRULE


// =============================================================================
RULE AirSys:OperableWinInterlockRpt
  DESCRIPTION
    ""
  INPUTCLASS 
    NotInput
  ANNUAL
    if ( WinInterlockRptFlag = 0 )
    then "NA"
    else if ( WinInterlockRptFlag > 0 )
    then "Yes"
    else "No"
    endif endif 
ENDRULE


// =============================================================================
RULE NEW ZnSys:WinInterlockRptFlag
  DATATYPE
    Integer
  LONGFORM
    WindowInterlockReportFlag
  DESCRIPTION
    ""  
  HELP
    ""  
  INPUTCLASS
    NotInput 
  ANNUAL
    if ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:WinInterlockRptFlag ) = 0 )
    then 0
    else if ( SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:WinInterlockRptFlag ) > 0 )
    then 1
    else -1
    endif endif 
ENDRULE


// =============================================================================
RULE ZnSys:OperableWinInterlockRpt
  DESCRIPTION
    ""
  INPUTCLASS 
    NotInput
  ANNUAL
    if ( Type <> "VRF") then
	    if ( WinInterlockRptFlag = 0 )
	    then "NA"
	    else if ( WinInterlockRptFlag > 0 )
	    then "Yes"
	    else "No"
	    endif endif
	  else
	    UNDEFINED
	  endif 
ENDRULE	  