// Project - HVAC Sizing
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


// -----------------------------------------------------------------------------
// Initialize System BaseSysNum values to 0
RULE AirSys:BaseSysNum
  DESCRIPTION
    "An integer value used to identify the baseline HVAC system number."
  INPUTCLASS 
    NotInput
  DEFAULT
    0  
  SIZING_PROPOSED
    if( IfValidAnd( u:Proj:AutoHardSize > 0 ) .AND.
        IfValidAnd( ResHVACAutoSizing > 0 ) .AND. 
        IfValidAnd( ServesResZn > 0 ) )
    then 210  // facilitate autosizing of Proposed Res systems
    else UNCHANGED
    endif
ENDRULE
RULE ZnSys:BaseSysNum
  DESCRIPTION
    "An integer value used to identify the baseline HVAC system number."
  INPUTCLASS 
    NotInput
  DEFAULT
    0 
ENDRULE
RULE VRFSys:BaseSysNum
  DESCRIPTION
    "An integer value used to identify the baseline HVAC system number."
  INPUTCLASS 
    NotInput
  DEFAULT
    0 
ENDRULE
RULE FluidSys:IsBaseSys
  DESCRIPTION
    "An integer value used to identify that the fluid system is a baseline system."
  INPUTCLASS 
    NotInput
  DEFAULT
    0 
ENDRULE

// BaseSysNumOpt, mainly to support T24 'a', 'b', 'c' single-zone system flavors
RULE AirSys:BaseSysNumOpt
  DESCRIPTION
    "An string value used to identify a sub-option to the baseline HVAC system number."
  INPUTCLASS 
    NotInput  
  SIZING_PROPOSED
    if( IfValidAnd( u:Proj:AutoHardSize > 0 ) .AND.
        IfValidAnd( ResHVACAutoSizing > 0 ) .AND. 
        IfValidAnd( ServesResZn > 0 ) )
    then // facilitate autosizing of Proposed Res systems
      switch( AirSys:Type )
        case  "SZAC"        : "a"
        case  "SZHP"        : "b"
        case  "SZDFHP"      : "c"
        default             : "a"
      endswitch
    else UNCHANGED
    endif
ENDRULE
RULE ZnSys:BaseSysNumOpt
  DESCRIPTION
    "An string value used to identify a sub-option to the baseline HVAC system number."
  INPUTCLASS 
    NotInput
ENDRULE


// --------------------------------------------------------------------------------------------------
// Set high level flags for presence or creation of baseline HVAC systems
RULE NEW Proj:HasBaseHVAC
  DATATYPE
    Integer
  LONGFORM
    HasBaseHVAC
  DESCRIPTION
    "A global flag that indicates whether an baseline HVAC system will be present in the transform."
  HELP
    "For NewComplete, and select partial compliance cases, the ruleset installs 
     a NACM baseline HVAC system for the analysis. This flag indicates when 
     the baseline HVAC rules are evaluated."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. Proj:IsNoAddMech > 0 )
    then 1 // This is a partial compliance analysis that requires creation of 
           // baseline HVAC for the proposed model
    else
    if( SumAll( ThrmlZn:HasUnknownHVAC ) > 0 )
    then 1 // Some zones have unknown HVAC
    else 0 // The proposed HVAC is user defined in all other cases
    endif endif
  SIZING_PROPOSED
    if( Proj:IsNoMech > 0 .OR. Proj:IsNoAddMech > 0 )
    then 1 // This is a partial compliance analysis that requires creation of 
           // baseline HVAC for the proposed model
    else
    if( SumAll( ThrmlZn:HasUnknownHVAC ) > 0 )
    then 1 // Some zones have unknown HVAC
    else 0 // The proposed HVAC is user defined in all other cases
    endif endif
  SIZING_BASELINE
    if( zp:Proj:HasBaseHVAC > 0 )
    then // The proposed has some baseline systems, so we know baseline model will as well
      1
    else 
    if( BaseHVACSameAsPropHVAC > 0 .AND. 
        IsNoAddMech = 0 .AND. 
        IsNoMech = 0 )
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else
    if( SumAll( zp:AirSys:ExcludeFromSizing ) + 
        SumAll( zp:ZnSys:ExcludeFromSizing ) = 0 )
    then // None of the proposed secondary systems are being excluded
      0
    else // Baseline model will have baseline systems
      1 
    endif endif endif
  ANNUAL
    z:HasBaseHVAC // Creation of HVAC only performed in SIZING transform
ENDRULE

// Initialize ThrmlZn/Spc BaseSysNum values to 999 in SIZING if value will be set for BASELINE
// This value is reset in BaselineHVACSystems.rule based on actual baseline system number determined
// for zone.
RULE NEW ThrmlZn:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( Proj:HasBaseHVAC > 0 .AND. IsNewHVAC > 0 .AND. 
        LocalCompAssigned( PriAirCondgSysRef[1] ) = 0 )
    then 999
    else 0
    endif
ENDRULE
RULE NEW Spc:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( LocalCompAssigned( ThrmlZnRef ) )
    then ThrmlZnRef:BaseSysNum
    else 0
    endif
ENDRULE

// ********** System Sizing Parameters *****************************************
RULE Proj:HVACAutoSizing
  DESCRIPTION
    "Whether or not the HVAC systems are to be sized by the simulation."
  INPUTCLASS
    Optional    
  DEFAULT 
    0
  CHECKCODE
;   if( AnalysisType != "Research" .AND. HVACAutoSizing > 0 )
;   then
;     PostWarning("HVACAutoSizing is currently not supported.") 
;   else 
    if( HVACAutoSizing > 0 )
    then
      if( IfValidAnd( IsNewMech = 0 ) .OR. 
          IfValidAnd( IsAddMech = 0 ) )
      then
        PostWarning("HVACAutoSizing is checked, but the ComplianceType does not include Mechanical.
                     This input will be ignored.")
      else
      if( IfValidAnd( IsPartMech > 0 ) )
      then
        PostWarning("HVACAutoSizing is checked, but this option is not supported for PartialMechanical
                     compliance. This input will be ignored.")
      else
        PostWarning("HVACAutoSizing is checked, so all HVAC capacities, even if 
                     defined in the model, will be ignored and Autosized by EnergyPlus.
                     This will not be a valid compliance run.") 
      endif endif 
    else UNCHANGED
    endif
  SIZING
    1  
  ANNUAL_PROPOSED
    if( IfValidAnd( u:HVACAutoSizing > 0 ) .AND. 
        ( IfValidAnd( IsNewMech > 0 ) .OR. 
          IfValidAnd( IsAddMech > 0 ) ) .AND.
        IfValidAnd( IsPartMech = 0 ) )
    then 1
    else 0
    endif
  ANNUAL_BASELINE
    0
ENDRULE
RULE Proj:SimDsgnDays
  DESCRIPTION
    "Whether or not the simulation is to analyze design days (defined in DDY files)."
  INPUTCLASS 
    Optional
  DEFAULT
    if( HVACAutoSizing > 0 ) 
    then 1 
    else 0
    endif
  SIZING
    1  
  ANNUAL_PROPOSED
    if( HVACAutoSizing > 0 ) 
    then 1 
    else 0
    endif
  ANNUAL_BASELINE
    0
ENDRULE
RULE Proj:ClgSizingRat
  DESCRIPTION
    "Multiplier used to adjust the capacity of cooling components."
  INPUTCLASS 
    Prescribed
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  DEFAULT 
    1.15
  SIZING_PROPOSED
    if( IfValidAnd( HVACAutoSizing > 0 ) )
    then ClgSizingRat
    else 1.15
    endif
  SIZING_BASELINE
    1.15
  ANNUAL_PROPOSED
    if( IfValidAnd( HVACAutoSizing > 0 ) ) 
    then ClgSizingRat
    else 1.15
    endif 
  ANNUAL_BASELINE
    1.15
ENDRULE
RULE Proj:HtgSizingRat
  DESCRIPTION
    "Multiplier used to adjust the capacity of heating components."
  INPUTCLASS 
    Prescribed
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  DEFAULT 
    1.25
  SIZING_PROPOSED
    if( IfValidAnd( HVACAutoSizing > 0 ) )
    then HtgSizingRat
    else 1.25
    endif
  SIZING_BASELINE
    1.25
  ANNUAL_PROPOSED
    if( IfValidAnd( HVACAutoSizing > 0 ) ) 
    then HtgSizingRat
    else 1.25
    endif 
  ANNUAL_BASELINE
    1.25
ENDRULE


// ********** AutoHardSizing and AutoEfficiencyInput ***************************
// The following properties and rules are primarily for development purposes.  
// AutoHardSize: Allows generalized sizing of HVAC system capacities based on 
// cfm/ft2, cfm/ton, and other parameters specifed by the user or in DEFAULT rules.
// AutoEffInput: Sets the efficiency of HVAC equipment to match the minimum 
// where applicable.
// In both cases, the rules are not complete, some user-defined HVAC configurations
// and equipment types are not supported.

// Project level inputs
RULE Proj:AutoHardSize
  HELP
    "A project level boolean (0/1) flag that specifies the status of AutoHardSize."
  INPUTCLASS
    Prescribed
  DEFAULT
    0
  SIZING
    UNDEFINED
  ANNUAL_PROPOSED
    u:AutoHardSize
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE

RULE Proj:AutoEffInput
  HELP
    "A project level boolean (0/1) flag that specifies the status of AutoEffInput."
  INPUTCLASS
    Prescribed
  DEFAULT
    0
  SIZING
    UNDEFINED
  ANNUAL_PROPOSED
    u:AutoEffInput
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE

RULE Proj:AutoHardSizeAirFlowPerSqFt
  HELP
    "Used for AutoHardSizing only, a project level specification of air side 
     supply air flow capacity based on system conditioned floor area. Used to 
     default system level properties."
  UNITS
    cfm/ft2
  INPUTCLASS
    Prescribed
  DEFAULT
    if( LocalStatus(AutoHardSize) = 0 .OR. AutoHardSize = 0 )
    then UNDEFINED
    else 1.0 ; cfm/ft2
    endif  
  SIZING
    UNDEFINED
  ANNUAL_PROPOSED
    u:AutoHardSizeAirFlowPerSqFt
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE

RULE Proj:AutoHardSizeAirFlowPerTon
  HELP
    "Used for AutoHardSizing only, a project level specification of air side 
     cooling capacity based on supply air flow.  Used to default  system level 
     properties."
  UNITS
    cfm/ton
  INPUTCLASS
    Prescribed
  DEFAULT
    if( LocalStatus(AutoHardSize) = 0 .OR. AutoHardSize = 0 )
    then UNDEFINED
    else 400 ; cfm/ton (net) cooling
    endif
  SIZING
    UNDEFINED
  ANNUAL_PROPOSED
    u:AutoHardSizeAirFlowPerTon
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE


// System specific inputs
// =========================== AirSystem =======================================
RULE AirSys:AirFlowPerSqFt
  DESCRIPTION
    "Used for AutoHardSizing only, a system level specification of air side 
     supply air flow capacity based on system conditioned floor area. This value
     is referenced for AutoHardSizing of system capacities."
  UNITS
    cfm/ft2
  INPUTCLASS
    Prescribed
  DEFAULT
    if( Type = "DOASVAV" .OR. Type = "DOASCV" ) 
    then 0.001 // Small value to allow sizing based on SysVentFlow
    else Proj:AutoHardSizeAirFlowPerSqFt
    endif
ENDRULE

RULE AirSys:AirFlowPerTon
  DESCRIPTION
    "Used for AutoHardSizing only, a system level specification of air side 
     cooling capacity based on supply air flow. This value is referenced for 
     AutoHardSizing of system capacities."
  UNITS
    cfm/ton
  INPUTCLASS
    Prescribed
  DEFAULT
    Proj:AutoHardSizeAirFlowPerTon
ENDRULE

// =========================== ZoneSystem ======================================
RULE ZnSys:AirFlowPerSqFt
  DESCRIPTION
    "Used for AutoHardSizing only, a system level specification of air side 
     supply air flow capacity based on system conditioned floor area. This value
     is referenced for AutoHardSizing of system capacities."
  UNITS
    cfm/ft2
  INPUTCLASS
    Prescribed
  DEFAULT
    Proj:AutoHardSizeAirFlowPerSqFt
ENDRULE
RULE ZnSys:AirFlowPerTon
  DESCRIPTION
    "Used for AutoHardSizing only, a system level specification of air side 
     cooling capacity based on supply air flow. This value is referenced for 
     AutoHardSizing of system capacities."
  UNITS
    cfm/ton
  INPUTCLASS
    Prescribed
  DEFAULT
    Proj:AutoHardSizeAirFlowPerTon
ENDRULE

// =========================== Res HVAC AutoSizing ======================================
RULE NEW AirSys:AirSeg:CoilHtg:UserTypeToRestore
  DATATYPE
    String
  LONGFORM
    UserTypeToRestore
  DESCRIPTION
    "to keep track of the user-specified heater type for autosized systems that may switch to Resistance for sizing"
  SIZING_PROPOSED
    if( IfValidAnd( AirSys:BaseSysNum = 210 ) .AND.
        Type = "HeatPump" )
    then "HeatPump"
    else UNDEFINED
    endif
ENDRULE


