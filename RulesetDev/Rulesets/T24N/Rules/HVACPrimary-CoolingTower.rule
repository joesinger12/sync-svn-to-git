// HVAC Primary Systems - Cooling Towers
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//

// According to 5.8.3 ACM Draft April 17 2012, items:
//     Cooling Tower Type
//     Cooling Tower Fan Type
//     Cooling Tower Capacity
//     Cooling Tower Number of Cells
//     Cooling Tower Total Fan Horse Power
//     Cooling Tower Design Wet-Bulb
//     Cooling Tower Design Entering Water Temperature
//     Cooling Tower Design Return Water Temperature
//     Cooling Tower Capacity Adjustment Curve
//     Cooling Tower Set Point Control
//     Cooling Tower Capacity Control
//     Cooling Tower Low-Speed Airflow Ratio
//     Cooling Tower Low-Speed kW Ratio
//     Cooling Tower Power Adjustment Curve
//     Cooling Tower Minimum Speed
//
// The main objects in the SDD are:
//     Project:HtRej
//     Project:FluidSys:FluidSegment
//     Curve
//

// ********** <Condenser Plant Capacity> ***************************************
RULE NEW FluidSys:CWLoopLd
  DATATYPE
    FLOAT
  DESCRIPTION
    "The total peak load on cooling towers from sizing run."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  MINIMUM
    0
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND. Type = "CondenserWater" ) 
    then SumChildren( z:HtRej:CapRtdSim )
    else z:CWLoopLd
    endif
ENDRULE


// -----------------------------------------------------------------------------
// ********** <Number of Condensers in Condenser Water System> *****************
RULE NEW FluidSys:HtRejCnt
  DATATYPE
    INTEGER
  DESCRIPTION
    "The number of cooling towers in a FluidSystem:Type = CondenserWater."
  INPUTCLASS
    NotInput
  MINIMUM
    0
  COMMONMINIMUM
    1
  DEFAULT
    if( Type = "CondenserWater" ) then
      ChildCount( HtRej )
    else UNDEFINED
    endif
  SIZING
    if( IsBaseSys .AND. Type = "CondenserWater" )
    then 1
    else ChildCount( HtRej )
    endif
  ANNUAL
    if( IsBaseSys .AND. Type = "CondenserWater" )
    then Proj:BaseChWFluidSysRef:ChlrCnt
    else z:HtRejCnt
    endif
ENDRULE

// -----------------------------------------------------------------------------
// ********** <Number of Heat Rejections in Project> ***************************
RULE Proj:HtRejCnt
  DESCRIPTION
    "The number of towers or other heat rejection devices in the project."
  HELP
    ""
  INPUTCLASS
    NotInput
  REPORTPRECISION
    0
  DEFAULT
    SumChildren(FluidSys:HtRejCnt)
  SIZING
    u:HtRejCnt
  ANNUAL
    z:HtRejCnt
ENDRULE


// ********** <Create a second baseline tower> *********************************
RULE NEW FluidSys:Twr2Ref
  DATATYPE
    HtRej
  DESCRIPTION
    "Creates a second tower for the baseline when there are two or more
    chillers."
  HELP
    "The baseline has one cooling tower for each chiller.  Additional towers
    are created here."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  ANNUAL
    if( IsBaseSys .AND.
        Type = "CondenserWater" .AND. 
        ParentCompAssigned( BaseChWFluidSysRef ) )
    then
      if( Proj:BaseChWFluidSysRef:ChlrCnt > 1 )
      then RuleLibrary(HtRej, "Base Tower", 1, "BaseCWSystem")
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a third baseline tower> **********************************
RULE NEW FluidSys:Twr3Ref
  DATATYPE
    HtRej
  DESCRIPTION
    "Creates a third tower for the baseline when there are three or more
    chillers."
  HELP
    "The baseline has one cooling tower for each chiller.  Additional towers
    are created here."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND.
        Type = "CondenserWater" .AND. 
        ParentCompAssigned( BaseChWFluidSysRef ) )
    then
      if( Proj:BaseChWFluidSysRef:ChlrCnt > 2 )
      then RuleLibrary(HtRej, "Base Tower", 1, "BaseCWSystem")
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a fourth baseline tower> *********************************
RULE NEW FluidSys:Twr4Ref
  DATATYPE
    HtRej
  DESCRIPTION
    "Creates a fourth tower for the baseline when there are four or more
    chillers."
  HELP
    "The baseline has one cooling tower for each chiller.  Additional towers
    are created here."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND.
        Type = "CondenserWater" .AND. 
        ParentCompAssigned( BaseChWFluidSysRef ) )
    then
      if( Proj:BaseChWFluidSysRef:ChlrCnt > 3 )
      then RuleLibrary(HtRej, "Base Tower", 1, "BaseCWSystem")
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a fifth baseline tower> **********************************
RULE NEW FluidSys:Twr5Ref
  DATATYPE
    HtRej
  DESCRIPTION
    "Creates a fifth tower for the baseline when there are five or more
    chillers."
  HELP
    "The baseline has one cooling tower for each chiller.  Additional towers
    are created here."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND.
        Type = "CondenserWater" .AND. 
        ParentCompAssigned( BaseChWFluidSysRef ) )
    then
      if( Proj:BaseChWFluidSysRef:ChlrCnt > 4 )
      then RuleLibrary(HtRej, "Base Tower", 1, "BaseCWSystem")
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a sixth baseline tower> **********************************
RULE NEW FluidSys:Twr6Ref
  DATATYPE
    HtRej
  DESCRIPTION
    "Creates a sixth tower for the baseline when there are six or more
    chillers."
  HELP
    "The baseline has one cooling tower for each chiller.  Additional towers
    are created here."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND.
        Type = "CondenserWater" .AND. 
        ParentCompAssigned( BaseChWFluidSysRef ) )
    then
      if( Proj:BaseChWFluidSysRef:ChlrCnt > 5 )
      then RuleLibrary(HtRej, "Base Tower", 1, "BaseCWSystem")
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Connect new heat rejection equip, if present, to condenser water fluid segments
RULE HtRej:FluidSegInRef
  ANNUAL
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( FluidSegInRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseCWPriRetSeg" ) )
    then "BaseCWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE HtRej:FluidSegOutRef
  ANNUAL
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( FluidSegOutRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseCWPriSupSeg" ) )
    then "BaseCWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE


// ********** Status *********************************************************** 
RULE HtRej:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New'
;   New
;   Existing 
  DEFAULT
// Status defined from top-down
    if( Parent( IsNew ) ) then "New" else "Existing" endif
  CHECKCODE
    if( ( Proj:IsNewMech .AND. Status != "New" ) .OR.
        ( Proj:IsAlt = 0 .AND. Status = "Altered" ) )
    then
      PostWarning("HeatRejection '%s' has a Status of '%s', but Compliance Type is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else UNCHANGED
    endif
   SIZING
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else u:Status
    endif
  ANNUAL
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else z:Status
    endif
ENDRULE

RULE NEW HtRej:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  SIZING
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  ANNUAL
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
ENDRULE

RULE NEW HtRej:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    if( IsNew ) then 0 else 1 endif
ENDRULE

RULE NEW HtRej:IsBaseHlthCare
  DATATYPE
    Integer
  LONGFORM
    IsBaselineHealthCare
  DESCRIPTION
    "Whether the cooling tower is a baseline cooling tower that is part of FluidSys
     that serves a healthcare facility."
  INPUTCLASS
    NotInput
  SIZING
    ValidOr( FluidSys:IsBaseHlthCareSys, 0 )
ENDRULE 

// -----------------------------------------------------------------------------
// -------------- Heat Rejection Type ------------------------------------------
RULE HtRej:Type
  DESCRIPTION
    "The type of heat rejection device."
  HELP
    "Heat rejection devices include cooling towers and ground source types.
     The available options are OpenTower and ClosedTower."
;     The available options are OpenTower, ClosedTower, GroundSourceHeatExchanger,
;     Lake and Well."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  OPTION
    OpenTower
    ClosedTower
    ClosedTowerEvaporative
;    GroundSourceHeatExchanger
;    Lake
;    Well
  DEFAULT
    "OpenTower"
  SIZING
    if( IsBaseSys ) then
      "OpenTower"
    else u:Type
    endif
  ANNUAL
    if( IsBaseSys ) then
      "OpenTower"
    else z:Type
    endif
ENDRULE


// -----------------------------------------------------------------------------
RULE HtRej:BypassClosedTowerChecks
  DESCRIPTION
    "A flag which determines whether certain checks on closed towers and 
     evaporative closed towers throw errors or warnings."
  INPUTCLASS
    Default
  DEFAULT
    0
  SIZING
    if( IsBaseSys ) then
      UNDEFINED
    else  
      u:BypassClosedTowerChecks
    endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else
      z:BypassClosedTowerChecks
    endif

ENDRULE


// -----------------------------------------------------------------------------
RULE HtRej:CalcMthd
  DESCRIPTION
    "The calculation method to be used by EnergyPlus."
  INPUTCLASS
    Prescribed
  OPTION
    StandardDesign
    UserSpecified
    UAFactor
  DEFAULT
    if( Type = "ClosedTower" ) then
      "UserSpecified"
    else if( Type = "ClosedTowerEvaporative" ) then
      "UAFactor"
    else
      undefined
    endif endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED
    else  
      u:CalcMthd
    endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else
      z:CalcMthd
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Set a flag to indicate the cooling tower type
RULE NEW HtRej:IsOpenTwr
  DATATYPE
    INTEGER
  DESCRIPTION
    "A flag to indicate that the cooling tower is an open cooling tower."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "OpenTower" ) then
      1
    else 0
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Set a flag to indicate the cooling tower type
RULE NEW HtRej:IsClosedTwr
  DATATYPE
    INTEGER
  DESCRIPTION
    "A flag to indicate that the cooling tower is a closed cooling tower."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "ClosedTower" ) then
      1
    else 0
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Set a flag to indicate the cooling tower type
RULE NEW HtRej:IsClosedEvaporativeTwr
  DATATYPE
    INTEGER
  DESCRIPTION
    "A flag to indicate that the cooling tower is an evaporative closed 
     cooling tower."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "ClosedTowerEvaporative" ) then
      1
    else 0
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Number of Open Towers in Condenser Water System
RULE NEW FluidSys:OpenTwrCnt
  DATATYPE
    INTEGER
  DESCRIPTION
    "The number of open cooling towers in a condenser water system."
  INPUTCLASS
    NotInput
  MINIMUM
    0
  DEFAULT
    if( Type = "CondenserWater" ) then
      SumChildren( HtRej:IsOpenTwr )
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Number of Closed Towers in Condenser Water System
RULE NEW FluidSys:ClosedTwrCnt
  DATATYPE
    INTEGER
  DESCRIPTION
    "The number of closed cooling towers in a condenser water system."
  INPUTCLASS
    NotInput
  MINIMUM
    0
  DEFAULT
    if( Type = "CondenserWater" ) then
      SumChildren( HtRej:IsClosedTwr )
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Number of Evaporative Closed Towers in Condenser Water System
RULE NEW FluidSys:EvaporativeClosedTwrCnt
  DATATYPE
    INTEGER
  DESCRIPTION
    "The number of evaporative closed cooling towers in a condenser water 
     system."
  INPUTCLASS
    NotInput
  MINIMUM
    0
  DEFAULT
    if( Type = "CondenserWater" ) then
      SumChildren( HtRej:IsClosedEvaporativeTwr )
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//             Check that all cooling towers on a fluid system are the same type
RULE NEW FluidSys:ClgTwrConfigCheck
  DATATYPE
    INTEGER
  DESCRIPTION
    "Check that all the cooling towers in a condenser water system are the 
     same type."
  INPUTCLASS
    NotInput
  CHECKSIM
    if( Type = "CondenserWater" ) then
      if( ( OpenTwrCnt > 0 .AND. ClosedTwrCnt > 0 ) .OR.
          ( OpenTwrCnt > 0 .AND. EvaporativeClosedTwrCnt > 0 ) .OR.
          ( ClosedTwrCnt > 0 .AND. EvaporativeClosedTwrCnt > 0 ) ) then
        PostError("Fluid system '%s' includes cooling towers of different
                   types.  This is not allowed, all the cooling towers must be 
                   of a single type, OpenTower, ClosedTower, or 
                   ClosedTowerEvaporative.", Name)
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// -------------- Cooling Tower Fan Type ---------------------------------------
RULE HtRej:FanType
  DESCRIPTION
    "The type of fan used in a cooling tower."
  HELP
    "Fan type options are axial or centrifugal."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  OPTION
    Axial
    Centrifugal
  DEFAULT
    "Axial"
  SIZING
    if( IsBaseSys ) then
      "Axial"
    else if( Type = "OpenTower" .OR. 
             Type = "ClosedTower" .OR. 
             Type = "ClosedTowerEvaporative" ) then
      u:FanType
    else UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      "Axial"
    else if( IsBaseHlthCare > 0 .AND.
             Type = "OpenTower" .AND. 
             WtrFlowCap >= 900 )
    then "Axial"
    else z:FanType
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
//             Cooling Tower Capacity Control
RULE HtRej:ModCtrl
  DESCRIPTION
    "The method used by the heat rejection device to modulate capacity."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  OPTION
    Bypass
    Cycling
    TwoSpeed
    VariableSpeedDrive
  DEFAULT
    "VariableSpeedDrive"
  SIZING : S901G
    if( IsBaseSys ) then
      "TwoSpeed"
    else if( Type = "OpenTower" .OR. Type = "ClosedTower" .OR. 
             Type = "ClosedTowerEvaporative" ) then
      u:ModCtrl
    else UNDEFINED
    endif endif
  SIZING
    if( IsBaseSys ) then
      "VariableSpeedDrive"
    else if( Type = "OpenTower" .OR. Type = "ClosedTower" .OR. 
             Type = "ClosedTowerEvaporative" ) then
      u:ModCtrl
    else UNDEFINED
    endif endif
  ANNUAL : S901G
    if( IsBaseSys ) then
      "TwoSpeed"
    else z:ModCtrl
    endif
  ANNUAL
    if( IsBaseSys ) then
      "VariableSpeedDrive"
    else if( IsBaseHlthCare > 0 .AND.
             WtrFlowCap/CodeMinGMPPerHP >= 7.5 )
    then "VariableSpeedDrive"
    else z:ModCtrl
    endif endif
ENDRULE


// -------------- Cooling Tower Number of Cells --------------------
RULE HtRej:CellCnt
  DESCRIPTION
    "The number of cells in the cooling tower.  Each cell has its own fan and
    water flow allowing for responding to lower load conditions"
  HELP
    "The number of cells in the cooling tower. Each cell will be modeled as equal
    size. Cells are subdivisions of cooling towers into individual cells, each
    with their own fan and water flow, allowing the cooling system to respond
    more efficiently to lower load conditions."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  MINIMUM
    1
  COMMONMINIMUM
    1
  COMMONMAXIMUM
    6
  REPORTPRECISION
    0
  DEFAULT
    1
  SIZING
    if( IsBaseSys ) then
      1
    else u:CellCnt
    endif
  ANNUAL
    if( IsBaseSys ) then
      1
    else z:CellCnt
    endif
ENDRULE

// ********** <Tower Capacity> *************************************************
RULE NEW HtRej:CapRtdNoPump
  DATATYPE
    FLOAT
  DESCRIPTION
    "Cooling capacity of the heat rejection device at full load and rated 
     conditions."
  HELP
    "The nominal full load output of the heat rejection at design operating 
     conditions."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  DEFAULT
    if( IfValidAnd( ParentValid(HtRejCnt) > 0 ) .AND.
        LocalCompAssigned( FluidSegOutRef ) ) then
      if( FluidSegOutRef:TotHtRejFromChlr > 0 ) then
        FluidSegOutRef:TotHtRejFromChlr / Parent(HtRejCnt)
      else 
      // Old rule
;       //  Adjust for minimum EER of coils = 7
;       ( FluidSegOutRef:WtrSrcCoilClgCap / Parent(HtRejCnt) ) * ( 1 + 3.412 / 7 )
      // New rule
        FluidSegOutRef:CoilHtRejLd / Parent(HtRejCnt)
      endif
    else 0
    endif
  ANNUAL
    if( IsBaseSys ) then
      FluidSegOutRef:TotHtRejFromChlr/Parent(HtRejCnt)
    else UNDEFINED
    endif
ENDRULE


// ********** <Pump Heat> ******************************************************
RULE NEW HtRej:PumpHt
  DATATYPE
    FLOAT
  DESCRIPTION
    "Heat added by the pump to the fluid loop."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  DEFAULT : S901G ECBC
    if( IfValidAnd( ParentValid(DsgnSupWtrDelT) > 0 ) ) then 
      CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) * 60 / 3960 / 0.6667 * 3.412
    else 0
    endif  
  DEFAULT
    if( IfValidAnd( ParentValid(DsgnSupWtrDelT) > 0 ) ) then 
      CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) * 45 / 3960 / 0.7 * 3.412
    else 0
    endif  
  ANNUAL : S901G ECBC
    // Pump:Pwr(W) = BHP * 746/MtrEff
    // BHP = FlowCap * TotHd / 3960 / ImpellerEff
    // FlowCap = Parent(CapRtd)/(Parent(DsgnSupWtrDelT)*500.19)
    // TotHd = 60
    // ImpellerEff = 0.6667
    // MtrEff = 0.90            Not included-Motor heat not added to fluid
    if( IsBaseSys ) then
      CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) * 60 / 3960 / 0.6667 * 3.412
    else UNDEFINED
    endif
  ANNUAL
    // Pump:Pwr(W) = BHP * 746/MtrEff
    // BHP = FlowCap * TotHd / 3960 / ImpellerEff
    // FlowCap = Parent(CapRtd)/(Parent(DsgnSupWtrDelT)*500.19)
    // TotHd = 45
    // ImpellerEff = 0.7
    // MtrEff = 0.855            Not included-Motor heat not added to fluid
    if( IsBaseSys ) then
      CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) * 45 / 3960 / 0.7 * 3.412
    else UNDEFINED
    endif
ENDRULE


// ********** <Tower Rated Capacity> *******************************************
RULE HtRej:CapRtd
  DESCRIPTION
    "The rated cooling capacity at CTI test conditions."
  HELP
    "The cooling capacity at rated conditions of 95F condenser water return,
    85F condenser water supply, and 78F wet-bulb with a 3 gpm/nominal ton
    water flow, where a nominal ton is 15,000 Btu/hr.  Adjusted for pump heat
    and the project cooling sizing ratio."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Required
  MINIMUM
    0
  COMMONMINIMUM
    60000
  UNITS
    Btu/hr
  REPORTPRECISION
    -3
  DEFAULT
    if( Proj:AutoHardSize = 1 .AND.
        LocalCompAssigned(FluidSegInRef) ) then
        // For PROPOSED AutoHardSizing only
        // Heat rejection load from attached chillers
      CapRtdNoPump + PumpHt       // ClgSizingRat included in the chiller sizing
    else UNDEFINED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED // AutoSize
    else
      u:CapRtd
    endif
  ANNUAL
    if( IsBaseSys ) then
      CapRtdNoPump + PumpHt
    else if( IsExisting )
    then zp:CapRtd
    else z:CapRtd
    endif endif
ENDRULE


// ********** <Tower Rated Capacity> *******************************************
RULE HtRej:CapRtdSim
  DESCRIPTION
    "The rated cooling capacity at CTI test conditions for simulation."
  HELP
    "The cooling capacity at rated conditions of 95F condenser water return,
    85F condenser water supply, and 78F wet-bulb with a 3 gpm/nominal ton
    water flow, where a nominal ton is 15,000 Btu/hr.  Adjusted for pump heat
    and the project cooling sizing ratio."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  REPORTPRECISION
    -3
  DEFAULT
    CapRtd
  SIZING
    if( IsBaseSys > 0 )
    then UNDEFINED
    else CapRtd
    endif
  ANNUAL
    if( IsBaseSys ) then
      CapRtdNoPump + PumpHt
    else if( IsExisting )
    then zp:CapRtd
    else z:CapRtd
    endif endif
ENDRULE


// ********** <Tower Rated Capacity> *******************************************
RULE HtRej:FreeConvCapRtdSim
  DESCRIPTION
    "The rated cooling capacity at in free convenction mode for simulation."
  HELP
    "The cooling capacity in free convection mode required for simulation of
     two-speed cooling towers in EnergyPlus."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  REPORTPRECISION
    -3
  SIZING
    if( ModCtrl = "TwoSpeed" )
    then
      CapRtdSim * 0.05
    else
      UNDEFINED
    endif
  ANNUAL
    if( ModCtrl = "TwoSpeed" )
    then
      CapRtdSim * 0.05
    else
      UNDEFINED
    endif
ENDRULE


// -------------- Cooling Tower Design Leaving (Supply) Water Temperature ------
// Input restriction summary:
//      As designed. Default to 85F
// Baseline rule summary:
//      lower of 85F or 10F above design wetbulb
//
RULE HtRej:LvgTempDsgn
  DESCRIPTION
    "The temperature of the condenser water leaving the cooling tower at
    design conditions."
  HELP
    "The temperature of the condenser water leaving the cooling tower at
    design conditions."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
    JA2-Table2-3
  INPUTCLASS
    Default
  MINIMUM
    32
  COMMONMINIMUM
    60
  COMMONMAXIMUM
    100
  MAXIMUM
    212
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    Parent(DsgnSupWtrTemp)    // 85
  CHECKCODE
      if( Type = "ClosedTower" ) then
        if( LvgTempDsgn <= Proj:DsgnDBT ) then
        PostError("Cooling Tower '%s' has a design supply water temperature 
                   that is not higher than the design temperature (%.1f) for the 
                   project site.  The leaving water temperature of the 
                   condenser loop must be 
                   increased to assure proper operation.", Name, Proj:DsgnDBT )
        else if( LvgTempDsgn <= Proj:DsgnDBT + 8 ) then
          if( BypassClosedTowerChecks = 1 ) then
            PostWarning("Cooling Tower '%s' has a design supply water temperature 
                       that is only slightly higher than the design temperature 
                       (%.1f) for the project site.  The leaving water temperature 
                       of the condenser loop 
                       may need to be increased to assure proper operation.", Name, 
                       Proj:DsgnDBT )
          else
            PostError("Cooling Tower '%s' has a design supply water temperature 
                       that is only slightly higher than the design temperature 
                       (%.1f) for the project site.  The leaving water temperature 
                       of the condenser loop 
                       must be increased to assure proper operation.", Name, 
                       Proj:DsgnDBT )
          endif
        else UNCHANGED
        endif endif
      else if( Type = "ClosedTowerEvaporative" ) then
        if( LvgTempDsgn <= Proj:DsgnWBT ) then
        PostError("Cooling Tower '%s' has a design supply water temperature 
                   that is not higher than the design wet-bulb temperature (%.1f) 
                   for the project site.  The leaving water temperature of the 
                   condenser loop must be 
                   increased to assure proper operation.", Name, Proj:DsgnWBT )
        else if( LvgTempDsgn <= Proj:DsgnWBT + 8 ) then
          if( BypassClosedTowerChecks = 1 ) then
            PostWarning("Cooling Tower '%s' has a design supply water temperature 
                       that is only slightly higher than the design temperature 
                       (%.1f) for the project site.  The leaving water temperature 
                       of the condenser loop 
                       may need to be increased to assure proper operation.", Name, 
                       Proj:DsgnWBT )
          else
            PostError("Cooling Tower '%s' has a design supply water temperature 
                       that is only slightly higher than the design temperature 
                       (%.1f) for the project site.  The leaving water temperature 
                       of the condenser loop 
                       must be increased to assure proper operation.", Name, 
                       Proj:DsgnWBT )
          endif
        else UNCHANGED
        endif endif
      else UNCHANGED
      endif endif
  SIZING
    Parent(DsgnSupWtrTemp)
  ANNUAL
    if( IsBaseSys ) then
      Parent(DsgnSupWtrTemp)
    else Parent(z:DsgnSupWtrTemp)
    endif
ENDRULE


// -------------- Cooling Tower Design Return (Entering) Water Temperature -----
// Input restriction summary:
//      As designed. Default to 95F
// Baseline rule summary:
//      Set to a range of 10 F.

RULE HtRej:EntTempDsgn
  DESCRIPTION
    "Cooling Tower Design Return Water Temperature"
  HELP
    "The design condenser water return temperature (entering tower) that was used for selection
    and sizing of the cooling tower."
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  MINIMUM
    32
  COMMONMINIMUM
    60
  COMMONMAXIMUM
    100
  MAXIMUM
    212
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    if (ParentStatus(DsgnSupWtrDelT) > 0)
    then  LvgTempDsgn + Parent(DsgnSupWtrDelT)
    else  95  endif
    // 95
  CHECKSIM
    if( ParentStatus( DsgnSupWtrDelT ) > 0 .AND.
        LocalStatus( LvgTempDsgn ) > 0 )
    then
      if( EntTempDsgn != LvgTempDsgn + Parent( DsgnSupWtrDelT ) )
      then
        PostWarning("Cooling Tower '%s' has a design entering water temperature that 
                     differs from the parent loop return water 
                     temperature.  Water flow rates are based on the loop 
                     temperatures.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  SIZING
    LvgTempDsgn + Parent(DsgnSupWtrDelT)
  ANNUAL
    if( IsBaseSys ) then
      LvgTempDsgn + Parent(DsgnSupWtrDelT)
    else LvgTempDsgn + Parent(z:DsgnSupWtrDelT)
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE HtRej:UAFactor
  DESCRIPTION
    "The U-factor times area which describes the tower's heat transfer 
     characteristics.  Calculated as CapRtd / ( EntTempDsgn - DsgnWBT )
     for evaporative towers and as CapRtd / ( EntTempDsgn - DsgnDBT ) for 
     dry closed towers."
  HELP
    ""
  INPUTCLASS
    NotInput
  MINIMUM
    0
  UNITS
    Btu/hr-°F
  REPORTPRECISION
    1
  DEFAULT
    if( CalcMthd = "UAFactor" ) then
      if( Type = "ClosedTower" ) then
        if( IfValidAnd( CapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnDBT ) ) then
          CapRtd / ( EntTempDsgn - Proj:DsgnDBT )
        else UNDEFINED
        endif
      else if( Type = "ClosedTowerEvaporative" ) then
        if( IfValidAnd( CapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnWBT ) ) then
          CapRtd / ( EntTempDsgn - Proj:DsgnWBT )
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( IfValidAnd( UAFactor < 0 ) .OR.
        IfValidAnd( UAFactor > 3980000 ) ) then
      PostError("Cooling Tower '%s' has a UA value which is outside the 
                 acceptable range.  Generally this occurs because the design
                 entering water temperature is not high enough relative to the
                 design air temperature (wet-bulb for evaporative towers, 
                 dry-bulb for dry fluid coolers).", Name)
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED
    else if( CalcMthd = "UAFactor" ) then
      u:UAFactor
    else UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else if( IsExisting )
    then zp:UAFactor
    else z:UAFactor
    endif endif
ENDRULE


// -------------- Cooling Tower Water Flow -------------------------------------
RULE HtRej:WtrFlowCap
  DESCRIPTION
    "The cooling tower condenser water flow rate."
  HELP
    ""
  INPUTCLASS
    Default
  MINIMUM
    0
  UNITS
    gpm
  REPORTPRECISION
    1
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) .AND.
        IfValidAnd( ParentValid( DsgnSupWtrDelT ) > 0 ) ) then
    // gpm = Btuh / 8.3 (lb/gal) / 60 (min/hr) / Design loop dT 
    // (note: 8.3365 lb/gal was previously used.  That is the density of water 
    //  at about 60F instead of 90F )  Rule changed 6/1/18 by rlh
      CapRtd/498/FluidSys:DsgnSupWtrDelT
    else 0
    endif
  CHECKSIM
    if( WtrFlowCap > CapRtd/498/FluidSys:DsgnSupWtrDelT * 1.1 .OR.
        WtrFlowCap < CapRtd/498/FluidSys:DsgnSupWtrDelT * 0.9 ) then
      if( BypassClosedTowerChecks = 1 ) then
        PostWarning("Cooling Tower '%s' has a water flow rate and delta T which 
                     does not match the capacity of the tower. You may want to 
                     check inputs for consistency.", Name)
      else
        PostError("Cooling Tower '%s' has a water flow rate and delta T which 
                   does not match the capacity of the tower. You may want to 
                   check inputs for consistency.", Name)
      endif
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED          // AutoSize
    else u:WtrFlowCap
    endif
  ANNUAL
    if( IsBaseSys ) then
      CapRtdSim/498/Parent(DsgnSupWtrDelT)
    else z:WtrFlowCap
    endif
ENDRULE


// -------------- Cooling Tower Fan Total HP -----------------------------------
// Mandatory requirement from Table 110.2-F.
TABLE T24HtRejMinGPMPerHP
  Type                     FanType       GPMPerHP
  OpenTower                Axial         42.1
  OpenTower                Centrifugal   20.0
  ClosedTower              Axial         16.1
  ClosedTower              Centrifugal   7.0
  ClosedTowerEvaporative   Axial         16.1
  ClosedTowerEvaporative   Centrifugal   7.0
  *                        *             0
ENDTABLE

RULE NEW HtRej:CodeMinGMPPerHP
  DESCRIPTION
    "The minimum gpm per fan hp meets the code mandatory and prescriptive requirments."
  DATATYPE
    Float
  LONGFORM
    CodeMinimumGPMPerHP
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "OpenTower" .AND. WtrFlowCap >= 900 .AND.
        Proj:CliZnNum != 1 .AND. Proj:CliZnNum != 16 ) then 60
    else T24HtRejMinGPMPerHP:GPMPerHP( "Type", Type, "FanType", FanType )
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( Type = "OpenTower" .AND. CapRtdSim < 300 * 12000 .AND.
          Proj:CliZnNum != 1 .AND. Proj:CliZnNum != 16 ) then 60
      else T24HtRejMinGPMPerHP:GPMPerHP( "Type", Type, "FanType", FanType )
      endif
    else UNCHANGED
    endif
ENDRULE
  
RULE HtRej:TotFanHP
  DESCRIPTION
    "The sum of the nameplate rated horsepower (hp) of all fan motors on the
    cooling tower"
  HELP
    ""
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
    110.2-F
  INPUTCLASS
    Required
  UNITS
    hp
  REPORTPRECISION
    3
  DEFAULT : S901G
    if( LocalStatus(u:TotFanHP) < 5 .AND. Proj:AutoHardSize = 1 )
    then
    // For PROPOSED AutoHardSizing only
      if( FanType = "Axial" .AND. Type = "OpenTower" ) then WtrFlowCap / 38.2
      else if( FanType = "Axial" .AND. Type = "ClosedTower" ) then WtrFlowCap / 14.0
      else if( FanType = "Axial" .AND. Type = "ClosedTowerEvaporative" ) then WtrFlowCap / 14.0
      else if( FanType = "Centrifugal" .AND. Type = "OpenTower" ) then WtrFlowCap / 20.0
      else if( FanType = "Centrifugal" .AND. Type = "ClosedTower" ) then WtrFlowCap / 7.0
      else if( FanType = "Centrifugal" .AND. Type = "ClosedTowerEvaporative" ) then WtrFlowCap / 7.0
      else UNDEFINED
      endif endif endif endif endif endif
    else UNDEFINED
    endif   
  DEFAULT
    if( LocalStatus(u:TotFanHP) < 5 .AND. Proj:AutoHardSize = 1 .AND. CodeMinGMPPerHP > 0 ) then
    // For PROPOSED AutoHardSizing only
      WtrFlowCap / CodeMinGMPPerHP
    else UNDEFINED
    endif
  CHECKCODE : S901G
    if( Status = "New" ) then
      if( (FanType = "Axial" .AND. Type = "OpenTower" .AND.
           WtrFlowCap / TotFanHP < 38.2 )
          .OR.
          (FanType = "Axial" .AND. Type = "ClosedTower" .AND.
           WtrFlowCap / TotFanHP < 14.0 )
          .OR.
          (FanType = "Axial" .AND. Type = "ClosedTowerEvaporative" .AND.
           WtrFlowCap / TotFanHP < 14.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "OpenTower" .AND.
           WtrFlowCap / TotFanHP < 20.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "ClosedTower" .AND.
           WtrFlowCap / TotFanHP < 7.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "ClosedTowerEvaporative" .AND.
           WtrFlowCap / TotFanHP < 7.0 )
        )
      then
        PostError("Total Fan HP of Tower '%s' does not comply with the code 
                   minimum required performance from Table 6.8.1G.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  CHECKCODE : T24N_2016
    if( Status = "New" ) then
      if( (FanType = "Axial" .AND. Type = "OpenTower" .AND.
           WtrFlowCap / TotFanHP < 42.1 )
          .OR.
          (FanType = "Axial" .AND. Type = "ClosedTower" .AND.
           WtrFlowCap / TotFanHP < 14.0 )
          .OR.
          (FanType = "Axial" .AND. Type = "ClosedTowerEvaporative" .AND.
           WtrFlowCap / TotFanHP < 14.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "OpenTower" .AND.
           WtrFlowCap / TotFanHP < 20.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "ClosedTower" .AND.
           WtrFlowCap / TotFanHP < 7.0 )
          .OR.
          (FanType = "Centrifugal" .AND. Type = "ClosedTowerEvaporative" .AND.
           WtrFlowCap / TotFanHP < 7.0 )
        )
      then
        PostError("Total Fan HP of Tower '%s' does not comply with the code 
                   minimum required performance from Table 110.2-G.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  CHECKCODE
    if( Status = "New" ) then
      if( WtrFlowCap / TotFanHP < T24HtRejMinGPMPerHP:GPMPerHP( "Type", Type, "FanType", FanType ) * 0.99 )
      then
        PostError("Total Fan HP of Tower '%s' does not comply with the code 
                   minimum required performance from Table 110.2-F.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED          // AutoSize
    else u:TotFanHP
    endif
  ANNUAL : S901G
    if( IsBaseSys ) then
      WtrFlowCap/38.2
    else z:TotFanHP
    endif
  ANNUAL : T24N_2016
    if( IsBaseSys ) then
      WtrFlowCap/60
;      WtrFlowCap/42.1
    else z:TotFanHP
    endif  
  ANNUAL
    if( IsBaseSys .OR. IfValidAnd( IsBaseHlthCare > 0 ) ) then
      WtrFlowCap/CodeMinGMPPerHP
    else z:TotFanHP
    endif
ENDRULE

// -------------- Cooling Tower Airflow ----------------------------------------
RULE HtRej:AirFlowCap
  DESCRIPTION
    "The cooling tower airflow rate."
  HELP
    ""
  INPUTCLASS
    Required
  MINIMUM
    0
  UNITS
    cfm
  REPORTPRECISION
    0
  DEFAULT
    if( LocalStatus(AirFlowCap) < 5 .AND. Proj:AutoHardSize = 1 ) then
    // For PROPOSED AutoHardSizing only
    // EnergyPlus default Flow (m3/s) = FanPwr (W) * 0.5 (fan eff.) / 190 pa
    // TotFanHP * 746(W/hp) * 0.5 / 190 * 2119(cfm/(m3/s))
    //   This airflow is too low and will cause UA errors for closed towers.
    //   Reduce the static from 190 pa to 100 pa since no water effects.
      if( Type = "OpenTower" ) then
        TotFanHP * 746 * 0.5 / 190 * 2119
      else if( Type = "ClosedTower" ) then
        TotFanHP * 746 * 0.5 / 100 * 2119
      else if( Type = "ClosedTowerEvaporative" ) then
        TotFanHP * 746 * 0.5 / 190 * 2119
      else UNDEFINED
      endif endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( Type = "ClosedTower" ) then
      if( AirFlowCap / WtrFlowCap < 450 ) then
        if( BypassClosedTowerChecks = 1 ) then
          PostWarning("Cooling Tower '%s' has a combination of airflow and water 
                       flow rates which may result in an EnergyPlus error 
                       related to calculation of a UA value.  Increasing 
                       the ratio of airflow to water flow may resolve the 
                       error.", Name)
        else
          PostError("Cooling Tower '%s' has a combination of airflow and water 
                     flow rates which may result in an EnergyPlus error 
                     related to calculation of a UA value.  Increasing 
                     the ratio of airflow to water flow may resolve the 
                     error.", Name)
        endif
      else if( CapRtd / AirFlowCap / 1.08 > 17 ) then
        if( BypassClosedTowerChecks = 1 ) then
          PostWarning("Cooling Tower '%s' has an airflow which may result 
                     in an EnergyPlus error related to calculation of a 
                     UA value.  Increasing the airflow may resolve the 
                     error.", Name)
        else
          PostError("Cooling Tower '%s' has an airflow which may result 
                   in an EnergyPlus error related to calculation of a 
                   UA value.  Increasing the airflow may resolve the 
                   error.", Name)
        endif
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED          // AutoSize
    else u:AirFlowCap
    endif
  ANNUAL
    if( IsBaseSys .OR. 
        IfValidAnd( IsBaseHlthCare > 0 ) ) then
      TotFanHP * 746 * 0.5 / 190 * 2119
    else z:AirFlowCap
    endif
ENDRULE

// -----------------------------------------------------------------------------
//               Evaporative Closed Tower Water Spray Rate
RULE HtRej:SprayWtrFlowCap
  DESCRIPTION
    "The water spray flow rate for an evaporative closed cooling tower."
  HELP
    ""
  INPUTCLASS
    CondRequired
  MINIMUM
    0
  UNITS
    gpm
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "ClosedTowerEvaporative" .AND. Proj:AutoHardSize = 1 ) then
    // For PROPOSED AutoHardSizing only
    // Based on Marley DT fluid cooler engineering data DT-TS-16.pdf found at
    // http://spxcooling.com/library/detail/marley-dt-fluid-cooler-engineering-data
    // on April 25, 2018, the water flow rate is fixed for different models 
    // covering fairly wide capacity ranges, but can be approximated by 
    // 1 gpm per 100 cfm of airflow.
      AirFlowCap / 100
    else UNDEFINED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED          // ClosedTowerEvaporative not used for baseline
    else u:SprayWtrFlowCap
    endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED          // ClosedTowerEvaporative not used for baseline
    else z:SprayWtrFlowCap
    endif
ENDRULE

// -------------- Cooling Tower Capacity Control -------------------------------
// Input restriction summary:
//      As designed
// Baseline rule summary:
//      Two speed fan for motors 7.5 hp or larger
// Not clear what should be used when exception is triggered. If the exceptions
// for Cooling Tower Capacity Control are triggered what capacity control
// method should be used?  Seems contradictory to rule in Cooling Tower Type.
// In Cooling Tower Type the capacity control is specified as two-speed but
// in Cooling Tower Capacity Control it is more complex. Which is it?
// Project:HtRej:ModCtrl is previously specified



// -------------- Cooling Tower Low-Speed Airflow Ratio ------------------------
// Input restriction summary:
//      As designed
// Baseline rule summary:
//      Not used - baseline is variable speed

RULE HtRej:LowSpdAirFlowRat
  DESCRIPTION
    "Ratio of the low-speed airflow to full speed airflow."
  HELP
    "The percentage full load airflow that the tower has at low speed or with
    the pony motor operating. This is equivalent to the percentage full load
    capacity when operating at low speed. "
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0.2
  COMMONMAXIMUM
    0.8
  MAXIMUM
    1.0
  REPORTPRECISION
    2
  DEFAULT
    if( ModCtrl = "TwoSpeed" .OR.
        ModCtrl = "VariableSpeedDrive" )
    then
      0.50
    else UNDEFINED
    endif
  CHECKSIM
    if( ModCtrl = "TwoSpeed" .OR.
        ModCtrl = "VariableSpeedDrive" )
    then
      if( IfValidAnd(LowSpdAirFlowRat > 0) )
      then UNCHANGED
      else
      PostError("Cooling Tower '%s' must have a nonzero low speed air flow 
                 ratio.  Enter an appropriate value.", Name)
      endif
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      0.50
    else u:LowSpdAirFlowRat
    endif
  ANNUAL
    if( IsBaseSys ) then
      0.50
    else if( IsBaseHlthCare > 0 .AND.
             TotFanHP >= 7.5 )
    then 0.50
    else z:LowSpdAirFlowRat
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
RULE HtRej:LowSpdAirFlowCap
  DESCRIPTION
    "The airflow at low-speed."
  INPUTCLASS
    NotInput
  MINIMUM
    0
  REPORTPRECISION
    0
  SIZING
    if( IsBaseSys ) then
      UNDEFINED
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      u:LowSpdAirFlowRat * AirFlowCap
    else UNDEFINED
    endif endif
  ANNUAL 
    if( IsBaseSys ) then
      UNDEFINED
    else if( IsBaseHlthCare > 0 .AND.
             TotFanHP >= 7.5 )
    then LowSpdAirFlowRat * AirFlowCap
    else if( IsExisting )
    then zp:LowSpdAirFlowCap
    else z:LowSpdAirFlowCap
    endif endif endif
ENDRULE


// -------------- Cooling Tower Low-Speed kW Ratio -----------------------------
// Input restriction summary:
//      Calculated using the as-designed flow ratio and the cooling tower
//      power adjustment curve.
// Baseline rule summary:
//      Not used - baseline is variable speed
//  does it makes sense to set proposed design based on using VSD curve for
//  two-speed fan. The proposed value for the Cooling Tower Low-Speed kW Ratio
//  which is for two speed fans is described as: "Calculated, using the
//  as-designed flow ratio and the cooling tower power adjustment curve below"
//  where the cooling tower power adjustment curve is for variable speed fans.
//  Is the intention really to use a variable speed curve to compute the
//  low-speed power ratio for a two-speed tower?

RULE HtRej:LowSpdPwrRat
  DESCRIPTION
    "The ratio of the power at low air speed to the full load power."
  HELP
    "The percentage full load power that the tower fans draw at low speed or with the pony
    motor operating"
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    NotInput
  MINIMUM
    0
  COMMONMINIMUM
    0.1
  COMMONMAXIMUM
    0.6
  MAXIMUM
    1.0
  SIZING : S901G
    if( IsBaseSys ) then
      0.30
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      0.33162901 -
      0.88567609*LowSpdAirFlowRat +
      0.60556507*LowSpdAirFlowRat**2 +
      0.9484823 *LowSpdAirFlowRat**3
    else
      UNDEFINED
    endif endif
  SIZING
    if( IsBaseSys ) then
      0.25
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      0.33162901 -
      0.88567609*LowSpdAirFlowRat +
      0.60556507*LowSpdAirFlowRat**2 +
      0.9484823 *LowSpdAirFlowRat**3
    else
      UNDEFINED
    endif endif
  ANNUAL : S901G
    if( IsBaseSys ) then
      0.30
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      0.33162901 -
      0.88567609*LowSpdAirFlowRat +
      0.60556507*LowSpdAirFlowRat**2 +
      0.9484823 *LowSpdAirFlowRat**3
    else
      UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      0.25
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      0.33162901 -
      0.88567609*LowSpdAirFlowRat +
      0.60556507*LowSpdAirFlowRat**2 +
      0.9484823 *LowSpdAirFlowRat**3
    else
      UNDEFINED
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
//                Cooling Tower Low-Speed Capacity
RULE HtRej:LowSpdCapRtd
  DESCRIPTION
    "Heat rejection capacity of the cooling tower at low-speed."
  INPUTCLASS
    Default
  UNITS
    Btu/hr
  REPORTPRECISION
    -3
  DEFAULT
    if( LocalCompAssigned(FluidSegInRef) .AND. 
        ( ModCtrl = "TwoSpeed" .OR. 
          ModCtrl = "VariableSpeedDrive" ) )
    then
      CapRtd * LowSpdAirFlowRat
    else UNDEFINED
    endif
  SIZING
    if( IsBaseSys .OR. 
        ModCtrl = "Bypass" .OR. 
        ModCtrl = "Cycling" )
    then
      UNDEFINED
    else CapRtd * LowSpdAirFlowRat
    endif
  ANNUAL
    if ( IsBaseSys )
    then 
      UNDEFINED
    else if( ModCtrl = "TwoSpeed" .OR.
             ModCtrl = "VariableSpeedDrive" )
    then
      CapRtd * LowSpdAirFlowRat
    else
      UNDEFINED
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
RULE HtRej:LowSpdUAFactor
  DESCRIPTION
    "The U-factor times area which describes the tower's heat transfer 
     characteristics at low air speed.  Calculated as 
     LowSpdCapRtd / ( EntTempDsgn - DsgnWBT ) for evaporative towers and as 
     LowSpdCapRtd / ( EntTempDsgn - DsgnDBT ) for dry closed towers."
  HELP
    ""
  INPUTCLASS
    NotInput
  MINIMUM
    0
  UNITS
    Btu/hr-°F
  REPORTPRECISION
    1
  DEFAULT
    if( CalcMthd = "UAFactor" ) then
      if( Type = "ClosedTower" ) then
        if( IfValidAnd( LowSpdCapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnDBT ) ) then
          LowSpdCapRtd / ( EntTempDsgn - Proj:DsgnDBT )
        else UNDEFINED
        endif
      else if( Type = "ClosedTowerEvaporative" ) then
        if( IfValidAnd( LowSpdCapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnWBT ) ) then
          LowSpdCapRtd / ( EntTempDsgn - Proj:DsgnWBT )
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( IfValidAnd( LowSpdUAFactor <= 0 ) .OR.
        IfValidAnd( LowSpdUAFactor > 3980000 ) ) 
    then
      PostError("Cooling Tower '%s' has a low speed UA value which is outside the 
                 acceptable range.  Generally this occurs because the design
                 entering water temperature is not high enough relative to the
                 design air temperature (wet-bulb for evaporative towers, 
                 dry-bulb for dry fluid coolers).", Name)
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED
    else if( CalcMthd = "UAFactor" ) then
      if( Type = "ClosedTower" ) then
        if( IfValidAnd( LowSpdCapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnDBT ) ) then
          LowSpdCapRtd / ( EntTempDsgn - Proj:DsgnDBT )
        else UNDEFINED
        endif
      else if( Type = "ClosedTowerEvaporative" ) then
        if( IfValidAnd( LowSpdCapRtd > 0 ) .AND.
            IfValidAnd( EntTempDsgn > Proj:DsgnWBT ) ) then
          LowSpdCapRtd / ( EntTempDsgn - Proj:DsgnWBT )
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else if( IsExisting )
    then zp:LowSpdUAFactor
    else z:LowSpdUAFactor
    endif endif
ENDRULE



// -------------- Cooling Tower Power Adjustment Curve -------------------------
// Input restriction summary:
//      As designed
// Baseline rule summary:
//      Use default.

RULE HtRej:Pwr_fPLRCrvRef
  DESCRIPTION
    "Power adjustment curve as a function of part load ratio for variable
    speed fan control."
  HELP
    "A curve that varies the cooling tower fan energy usage as a function
    of part-load ratio for cooling towers with variable speed fan control. "
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Prescribed
  SIZING
    if( IsBaseSys ) then
      RuleLibrary(CrvCubic,"HtRejVSDFanPwrRatio_fQRatio")
    else if( Type = "OpenTower" ) then
      if( ModCtrl = "VariableSpeedDrive" ) then
        RuleLibrary(CrvCubic,"HtRejVSDFanPwrRatio_fQRatio")
      else UNDEFINED
      endif
    else UNDEFINED
    endif endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      UNDEFINED   // Baseline is TwoSpeedFan and therefore has no curve
    else z:Pwr_fPLRCrvRef
    endif
  ANNUAL
    if( IsBaseSys ) then
      RuleLibrary(CrvCubic,"HtRejVSDFanPwrRatio_fQRatio")
    else z:Pwr_fPLRCrvRef
    endif
ENDRULE

// -------------- Cooling Tower Minimum Speed ----------------------------------
// Input restriction summary:
//      As designed
// Baseline rule summary:
//      Not applicable since applicable to varaible speed and baseline is two-speed

RULE HtRej:MinSpdRat
  DESCRIPTION
    "Minimum fan speed for a variable speed tower."
  HELP
    "The minimum fan speed setting of a VSD controlling a cooling tower fan expressed
    as a ratio of full load speed. "
  REFERENCE
    ACM Section 5.8.3-Cooling Towers
  INPUTCLASS
    Default
  MINIMUM
    0.0
  COMMONMINIMUM
    0.1
  COMMONMAXIMUM
    0.7
  MAXIMUM
    1.0
  UNITS
    dimensionless
  REPORTPRECISION
    2
  DEFAULT : S901G
    0.3
  DEFAULT
    0.50
  SIZING : S901G
    if( IsBaseSys ) then
      0.30
    else u:MinSpdRat
    endif
  SIZING
    if( IsBaseSys ) then
      0.50
    else u:MinSpdRat
    endif
  ANNUAL : S901G
    if( IsBaseSys ) then
      0.30
    else z:MinSpdRat
    endif
  ANNUAL
    if( IsBaseSys ) then
      0.50
    else if( IsBaseHlthCare > 0 .AND.
             TotFanHP >= 7.5 )
    then 0.50
    else z:MinSpdRat
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
// Count the number of chillers on demand side of FluidSystem segments
RULE NEW FluidSys:FluidSeg:NumChlrComp
  DATATYPE
    Integer
  LONGFORM
    NumberChillerComponents
  DESCRIPTION
    "The number of chiller components connected to supply-side of FluidSys"
  INPUTCLASS
    NotInput
  SIZING
    if( Type = "PrimarySupply" .OR. Type = "SecondarySupply" ) then
      CountRefs(Chlr:CndsrFluidSegInRef)
    else 0
    endif
  ANNUAL
    if( IsBaseSys ) 
    then
      z:NumChlrComp
    else if( Type = "PrimarySupply" .OR. Type = "SecondarySupply" )
    then
      CountRefs(Chlr:CndsrFluidSegInRef)
    else 0
    endif endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW HtRej:CapRtdNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) ) 
    then
      CapRtd * IsNew 
    else 0 
    endif
  SIZING
    UNDEFINED
  ANNUAL
    if( IfValidAnd( CapRtd > 0 ) ) 
    then
      CapRtd * IsNew 
    else 0 
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW HtRej:CapRtdExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) ) 
    then
      CapRtd * IsExisting 
      else 0 
      endif
  SIZING
    UNDEFINED
  ANNUAL
    if( IfValidAnd( CapRtd > 0 ) ) 
    then
      CapRtd * IsExisting 
    else 0 
    endif
ENDRULE